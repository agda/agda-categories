<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Diagram.Limit</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Keyword">open</a> <a id="42" class="Keyword">import</a> <a id="49" href="Categories.Category.html" class="Module">Categories.Category</a>
<a id="69" class="Keyword">open</a> <a id="74" class="Keyword">import</a> <a id="81" href="Categories.Functor.html" class="Module">Categories.Functor</a> <a id="100" class="Keyword">hiding</a> <a id="107" class="Symbol">(</a><a id="108" href="Categories.Functor.Core.html#1229" class="Function">id</a><a id="110" class="Symbol">)</a>

<a id="113" class="Comment">-- Limit of a Cone over a Functor F : J → C</a>
<a id="157" class="Keyword">module</a> <a id="164" href="Categories.Diagram.Limit.html" class="Module">Categories.Diagram.Limit</a>
  <a id="191" class="Symbol">{</a><a id="192" href="Categories.Diagram.Limit.html#192" class="Bound">o</a> <a id="194" href="Categories.Diagram.Limit.html#194" class="Bound">ℓ</a> <a id="196" href="Categories.Diagram.Limit.html#196" class="Bound">e</a><a id="197" class="Symbol">}</a> <a id="199" class="Symbol">{</a><a id="200" href="Categories.Diagram.Limit.html#200" class="Bound">o′</a> <a id="203" href="Categories.Diagram.Limit.html#203" class="Bound">ℓ′</a> <a id="206" href="Categories.Diagram.Limit.html#206" class="Bound">e′</a><a id="208" class="Symbol">}</a> <a id="210" class="Symbol">{</a><a id="211" href="Categories.Diagram.Limit.html#211" class="Bound">C</a> <a id="213" class="Symbol">:</a> <a id="215" href="Categories.Category.Core.html#375" class="Record">Category</a> <a id="224" href="Categories.Diagram.Limit.html#192" class="Bound">o</a> <a id="226" href="Categories.Diagram.Limit.html#194" class="Bound">ℓ</a> <a id="228" href="Categories.Diagram.Limit.html#196" class="Bound">e</a><a id="229" class="Symbol">}</a> <a id="231" class="Symbol">{</a><a id="232" href="Categories.Diagram.Limit.html#232" class="Bound">J</a> <a id="234" class="Symbol">:</a> <a id="236" href="Categories.Category.Core.html#375" class="Record">Category</a> <a id="245" href="Categories.Diagram.Limit.html#200" class="Bound">o′</a> <a id="248" href="Categories.Diagram.Limit.html#203" class="Bound">ℓ′</a> <a id="251" href="Categories.Diagram.Limit.html#206" class="Bound">e′</a><a id="253" class="Symbol">}</a> <a id="255" class="Symbol">(</a><a id="256" href="Categories.Diagram.Limit.html#256" class="Bound">F</a> <a id="258" class="Symbol">:</a> <a id="260" href="Categories.Functor.Core.html#407" class="Record">Functor</a> <a id="268" href="Categories.Diagram.Limit.html#232" class="Bound">J</a> <a id="270" href="Categories.Diagram.Limit.html#211" class="Bound">C</a><a id="271" class="Symbol">)</a> <a id="273" class="Keyword">where</a>

<a id="280" class="Keyword">private</a>
  <a id="290" class="Keyword">module</a> <a id="C"></a><a id="297" href="Categories.Diagram.Limit.html#297" class="Module">C</a> <a id="299" class="Symbol">=</a> <a id="301" href="Categories.Category.Core.html#375" class="Module">Category</a> <a id="310" href="Categories.Diagram.Limit.html#211" class="Bound">C</a>
  <a id="314" class="Keyword">module</a> <a id="J"></a><a id="321" href="Categories.Diagram.Limit.html#321" class="Module">J</a> <a id="323" class="Symbol">=</a> <a id="325" href="Categories.Category.Core.html#375" class="Module">Category</a> <a id="334" href="Categories.Diagram.Limit.html#232" class="Bound">J</a>
<a id="336" class="Keyword">open</a> <a id="341" href="Categories.Diagram.Limit.html#297" class="Module">C</a>
<a id="343" class="Keyword">open</a> <a id="348" href="Categories.Category.Core.html#2040" class="Module">HomReasoning</a>
<a id="361" class="Keyword">open</a> <a id="366" href="Categories.Functor.Core.html#407" class="Module">Functor</a> <a id="374" href="Categories.Diagram.Limit.html#256" class="Bound">F</a>

<a id="377" class="Keyword">open</a> <a id="382" class="Keyword">import</a> <a id="389" href="Level.html" class="Module">Level</a>
<a id="395" class="Keyword">open</a> <a id="400" class="Keyword">import</a> <a id="407" href="Data.Product.html" class="Module">Data.Product</a> <a id="420" class="Keyword">using</a> <a id="426" class="Symbol">(</a><a id="427" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a><a id="432" class="Symbol">)</a>

<a id="435" class="Keyword">open</a> <a id="440" class="Keyword">import</a> <a id="447" href="Categories.Category.Construction.Cones.html" class="Module">Categories.Category.Construction.Cones</a> <a id="486" href="Categories.Diagram.Limit.html#256" class="Bound">F</a> <a id="488" class="Keyword">renaming</a> <a id="497" class="Symbol">(</a>Cone⇒ <a id="504" class="Symbol">to</a> _⇨_<a id="510" class="Symbol">)</a>
<a id="512" class="Keyword">open</a> <a id="517" class="Keyword">import</a> <a id="524" href="Categories.Object.Terminal.html" class="Module">Categories.Object.Terminal</a> <a id="551" class="Symbol">as</a> <a id="554" class="Module">T</a> <a id="556" class="Keyword">hiding</a> <a id="563" class="Symbol">(</a><a id="564" href="Categories.Object.Terminal.html#986" class="Function">up-to-iso</a><a id="573" class="Symbol">;</a> <a id="575" href="Categories.Object.Terminal.html#1150" class="Function">transport-by-iso</a><a id="591" class="Symbol">)</a>
<a id="593" class="Keyword">open</a> <a id="598" class="Keyword">import</a> <a id="605" href="Categories.Morphism.Reasoning.html" class="Module">Categories.Morphism.Reasoning</a> <a id="635" href="Categories.Diagram.Limit.html#211" class="Bound">C</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Categories.Morphism.html" class="Module">Categories.Morphism</a> <a id="669" href="Categories.Diagram.Limit.html#211" class="Bound">C</a>
<a id="671" class="Keyword">open</a> <a id="676" class="Keyword">import</a> <a id="683" href="Categories.Morphism.html" class="Module">Categories.Morphism</a> <a id="703" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="709" class="Symbol">as</a> <a id="MC"></a><a id="712" href="Categories.Diagram.Limit.html#712" class="Module">MC</a> <a id="715" class="Keyword">using</a> <a id="721" class="Symbol">()</a> <a id="724" class="Keyword">renaming</a> <a id="733" class="Symbol">(</a><a id="734" href="Categories.Morphism.html#980" class="Record Operator">_≅_</a> <a id="738" class="Symbol">to</a> <a id="MC._≅_"></a><a id="741" href="Categories.Diagram.Limit.html#741" class="Record Operator">_⇔_</a><a id="744" class="Symbol">)</a>

<a id="747" class="Keyword">private</a>
  <a id="757" class="Keyword">variable</a>
    <a id="770" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="772" href="Categories.Diagram.Limit.html#772" class="Generalizable">K′</a>  <a id="776" class="Symbol">:</a> <a id="778" href="Categories.Diagram.Cone.html#526" class="Record">Cone</a>
    <a id="787" href="Categories.Diagram.Limit.html#787" class="Generalizable">A</a> <a id="789" href="Categories.Diagram.Limit.html#789" class="Generalizable">B</a>   <a id="793" class="Symbol">:</a> <a id="795" href="Categories.Category.Core.html#492" class="Function">J.Obj</a>
    <a id="805" href="Categories.Diagram.Limit.html#805" class="Generalizable">X</a> <a id="807" href="Categories.Diagram.Limit.html#807" class="Generalizable">Y</a> <a id="809" href="Categories.Diagram.Limit.html#809" class="Generalizable">Z</a> <a id="811" class="Symbol">:</a> <a id="813" href="Categories.Category.Core.html#492" class="Function">Obj</a>
    <a id="821" href="Categories.Diagram.Limit.html#821" class="Generalizable">q</a>     <a id="827" class="Symbol">:</a> <a id="829" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="831" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="833" href="Categories.Diagram.Limit.html#772" class="Generalizable">K′</a>

<a id="837" class="Comment">-- A Limit is a Terminal object in the category of Cones</a>
<a id="894" class="Comment">--   (This could be unpacked...)</a>
<a id="927" class="Keyword">record</a> <a id="Limit"></a><a id="934" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a> <a id="940" class="Symbol">:</a> <a id="942" class="PrimitiveType">Set</a> <a id="946" class="Symbol">(</a><a id="947" href="Categories.Diagram.Limit.html#192" class="Bound">o</a> <a id="949" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="951" href="Categories.Diagram.Limit.html#194" class="Bound">ℓ</a> <a id="953" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="955" href="Categories.Diagram.Limit.html#196" class="Bound">e</a> <a id="957" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="959" href="Categories.Diagram.Limit.html#200" class="Bound">o′</a> <a id="962" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="964" href="Categories.Diagram.Limit.html#203" class="Bound">ℓ′</a> <a id="967" href="Agda.Primitive.html#636" class="Primitive Operator">⊔</a> <a id="969" href="Categories.Diagram.Limit.html#206" class="Bound">e′</a><a id="971" class="Symbol">)</a> <a id="973" class="Keyword">where</a>
  <a id="981" class="Keyword">field</a>
    <a id="Limit.terminal"></a><a id="991" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a> <a id="1000" class="Symbol">:</a> <a id="1002" href="Categories.Object.Terminal.html#519" class="Record">Terminal</a> <a id="1011" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a>

  <a id="1020" class="Keyword">module</a> <a id="terminal"></a><a id="1027" href="Categories.Diagram.Limit.html#1027" class="Module">terminal</a> <a id="1036" class="Symbol">=</a> <a id="1038" href="Categories.Object.Terminal.html#519" class="Module">Terminal</a> <a id="1047" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a>

  <a id="1059" class="Keyword">open</a> <a id="1064" href="Categories.Diagram.Limit.html#1027" class="Module">terminal</a> <a id="1073" class="Keyword">using</a> <a id="1079" class="Symbol">()</a> <a id="1082" class="Keyword">renaming</a> <a id="1091" class="Symbol">(</a><a id="1092" href="Categories.Object.Terminal.html#564" class="Function">⊤</a> <a id="1094" class="Symbol">to</a> <a id="Limit.terminal.⊤"></a><a id="1097" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a><a id="1102" class="Symbol">)</a> <a id="1104" class="Keyword">public</a>
  <a id="1113" class="Keyword">open</a> <a id="1118" href="Categories.Diagram.Cone.html#526" class="Module">Cone</a> <a id="1123" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="1129" class="Keyword">hiding</a> <a id="1136" class="Symbol">(</a>apex<a id="1141" class="Symbol">)</a> <a id="1143" class="Keyword">renaming</a> <a id="1152" class="Symbol">(</a>N <a id="1155" class="Symbol">to</a> apex<a id="1162" class="Symbol">;</a> ψ <a id="1166" class="Symbol">to</a> proj<a id="1173" class="Symbol">;</a> commute <a id="1183" class="Symbol">to</a> limit-commute<a id="1199" class="Symbol">)</a> <a id="1201" class="Keyword">public</a>

  <a id="Limit.rep-cone"></a><a id="1211" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1220" class="Symbol">:</a> <a id="1222" class="Symbol">∀</a> <a id="1224" href="Categories.Diagram.Limit.html#1224" class="Bound">K</a> <a id="1226" class="Symbol">→</a> <a id="1228" href="Categories.Diagram.Limit.html#1224" class="Bound">K</a> <a id="1230" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="1232" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a>
  <a id="1240" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1249" href="Categories.Diagram.Limit.html#1249" class="Bound">K</a> <a id="1251" class="Symbol">=</a> <a id="1253" href="Categories.Object.Terminal.html#576" class="Function">terminal.!</a> <a id="1264" class="Symbol">{</a><a id="1265" href="Categories.Diagram.Limit.html#1249" class="Bound">K</a><a id="1266" class="Symbol">}</a>

  <a id="Limit.rep"></a><a id="1271" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="1275" class="Symbol">:</a> <a id="1277" class="Symbol">∀</a> <a id="1279" href="Categories.Diagram.Limit.html#1279" class="Bound">K</a> <a id="1281" class="Symbol">→</a> <a id="1283" href="Categories.Diagram.Cone.html#578" class="Field">Cone.N</a> <a id="1290" href="Categories.Diagram.Limit.html#1279" class="Bound">K</a> <a id="1292" href="Categories.Category.Core.html#508" class="Function Operator">⇒</a> <a id="1294" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a>
  <a id="1301" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="1305" href="Categories.Diagram.Limit.html#1305" class="Bound">K</a> <a id="1307" class="Symbol">=</a> <a id="1309" href="Categories.Diagram.Cone.html#717" class="Function">arr</a>
    <a id="1317" class="Keyword">where</a> <a id="1323" class="Keyword">open</a> <a id="1328" href="Categories.Diagram.Limit.html#507" class="Module">_⇨_</a> <a id="1332" class="Symbol">(</a><a id="1333" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1342" href="Categories.Diagram.Limit.html#1305" class="Bound">K</a><a id="1343" class="Symbol">)</a>

  <a id="Limit.unrep"></a><a id="1348" href="Categories.Diagram.Limit.html#1348" class="Function">unrep</a> <a id="1354" class="Symbol">:</a> <a id="1356" href="Categories.Diagram.Limit.html#805" class="Generalizable">X</a> <a id="1358" href="Categories.Category.Core.html#508" class="Function Operator">⇒</a> <a id="1360" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a> <a id="1365" class="Symbol">→</a> <a id="1367" href="Categories.Diagram.Cone.html#526" class="Record">Cone</a>
  <a id="1374" href="Categories.Diagram.Limit.html#1348" class="Function">unrep</a> <a id="1380" href="Categories.Diagram.Limit.html#1380" class="Bound">f</a> <a id="1382" class="Symbol">=</a> <a id="1384" class="Keyword">record</a> <a id="1391" class="Symbol">{</a>
    <a id="1397" href="Categories.Diagram.Cone.html#592" class="Field">apex</a>        <a id="1409" class="Symbol">=</a> <a id="1411" class="Keyword">record</a>
      <a id="1424" class="Symbol">{</a> <a id="1426" href="Categories.Diagram.Cone.html#415" class="Field">ψ</a>       <a id="1434" class="Symbol">=</a> <a id="1436" class="Symbol">λ</a> <a id="1438" href="Categories.Diagram.Limit.html#1438" class="Bound">A</a> <a id="1440" class="Symbol">→</a> <a id="1442" href="Categories.Diagram.Limit.html#1169" class="Function">proj</a> <a id="1447" href="Categories.Diagram.Limit.html#1438" class="Bound">A</a> <a id="1449" href="Categories.Category.Core.html#589" class="Function Operator">∘</a> <a id="1451" href="Categories.Diagram.Limit.html#1380" class="Bound">f</a>
      <a id="1459" class="Symbol">;</a> <a id="1461" href="Categories.Diagram.Cone.html#463" class="Field">commute</a> <a id="1469" class="Symbol">=</a> <a id="1471" class="Symbol">λ</a> <a id="1473" href="Categories.Diagram.Limit.html#1473" class="Bound">g</a> <a id="1475" class="Symbol">→</a> <a id="1477" href="Categories.Morphism.Reasoning.Core.html#1587" class="Function">pullˡ</a> <a id="1483" class="Symbol">(</a><a id="1484" href="Categories.Diagram.Limit.html#1186" class="Function">limit-commute</a> <a id="1498" href="Categories.Diagram.Limit.html#1473" class="Bound">g</a><a id="1499" class="Symbol">)</a>
      <a id="1507" class="Symbol">}</a>
    <a id="1513" class="Symbol">}</a>

  <a id="Limit.conify"></a><a id="1518" href="Categories.Diagram.Limit.html#1518" class="Function">conify</a> <a id="1525" class="Symbol">:</a> <a id="1527" class="Symbol">(</a><a id="1528" href="Categories.Diagram.Limit.html#1528" class="Bound">f</a> <a id="1530" class="Symbol">:</a> <a id="1532" href="Categories.Diagram.Limit.html#805" class="Generalizable">X</a> <a id="1534" href="Categories.Category.Core.html#508" class="Function Operator">⇒</a> <a id="1536" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a><a id="1540" class="Symbol">)</a> <a id="1542" class="Symbol">→</a> <a id="1544" href="Categories.Diagram.Limit.html#1348" class="Function">unrep</a> <a id="1550" href="Categories.Diagram.Limit.html#1528" class="Bound">f</a> <a id="1552" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="1554" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a>
  <a id="1562" href="Categories.Diagram.Limit.html#1518" class="Function">conify</a> <a id="1569" href="Categories.Diagram.Limit.html#1569" class="Bound">f</a> <a id="1571" class="Symbol">=</a> <a id="1573" class="Keyword">record</a>
    <a id="1584" class="Symbol">{</a> <a id="1586" href="Categories.Diagram.Cone.html#717" class="Field">arr</a>     <a id="1594" class="Symbol">=</a> <a id="1596" href="Categories.Diagram.Limit.html#1569" class="Bound">f</a>
    <a id="1602" class="Symbol">;</a> <a id="1604" href="Categories.Diagram.Cone.html#742" class="Field">commute</a> <a id="1612" class="Symbol">=</a> <a id="1614" href="Relation.Binary.Structures.html#1568" class="Function">C.Equiv.refl</a>
    <a id="1631" class="Symbol">}</a>

  <a id="Limit.commute"></a><a id="1636" href="Categories.Diagram.Limit.html#1636" class="Function">commute</a> <a id="1644" class="Symbol">:</a> <a id="1646" href="Categories.Diagram.Limit.html#1169" class="Function">proj</a> <a id="1651" href="Categories.Diagram.Limit.html#787" class="Generalizable">A</a> <a id="1653" href="Categories.Category.Core.html#589" class="Function Operator">∘</a> <a id="1655" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="1659" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="1661" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="1663" href="Categories.Diagram.Cone.html#415" class="Function">Cone.ψ</a> <a id="1670" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="1672" href="Categories.Diagram.Limit.html#787" class="Generalizable">A</a>
  <a id="1676" href="Categories.Diagram.Limit.html#1636" class="Function">commute</a> <a id="1684" class="Symbol">{</a><a id="1685" class="Argument">K</a> <a id="1687" class="Symbol">=</a> <a id="1689" href="Categories.Diagram.Limit.html#1689" class="Bound">K</a><a id="1690" class="Symbol">}</a> <a id="1692" class="Symbol">=</a> <a id="1694" href="Categories.Diagram.Cone.html#742" class="Field">_⇨_.commute</a> <a id="1706" class="Symbol">(</a><a id="1707" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1716" href="Categories.Diagram.Limit.html#1689" class="Bound">K</a><a id="1717" class="Symbol">)</a>

  <a id="Limit.unrep-cone"></a><a id="1722" href="Categories.Diagram.Limit.html#1722" class="Function">unrep-cone</a> <a id="1733" class="Symbol">:</a> <a id="1735" class="Symbol">(</a><a id="1736" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="1738" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="1740" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a><a id="1745" class="Symbol">)</a> <a id="1747" class="Symbol">→</a> <a id="1749" href="Categories.Diagram.Cone.html#526" class="Record">Cone</a>
  <a id="1756" href="Categories.Diagram.Limit.html#1722" class="Function">unrep-cone</a> <a id="1767" href="Categories.Diagram.Limit.html#1767" class="Bound">f</a> <a id="1769" class="Symbol">=</a> <a id="1771" href="Categories.Diagram.Limit.html#1348" class="Function">unrep</a> <a id="1777" class="Symbol">(</a><a id="1778" href="Categories.Diagram.Cone.html#717" class="Field">_⇨_.arr</a> <a id="1786" href="Categories.Diagram.Limit.html#1767" class="Bound">f</a><a id="1787" class="Symbol">)</a>

  <a id="Limit.g-η"></a><a id="1792" href="Categories.Diagram.Limit.html#1792" class="Function">g-η</a> <a id="1796" class="Symbol">:</a> <a id="1798" class="Symbol">∀</a> <a id="1800" class="Symbol">{</a><a id="1801" href="Categories.Diagram.Limit.html#1801" class="Bound">f</a> <a id="1803" class="Symbol">:</a> <a id="1805" href="Categories.Diagram.Limit.html#805" class="Generalizable">X</a> <a id="1807" href="Categories.Category.Core.html#508" class="Function Operator">⇒</a> <a id="1809" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a><a id="1813" class="Symbol">}</a> <a id="1815" class="Symbol">→</a> <a id="1817" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="1821" class="Symbol">(</a><a id="1822" href="Categories.Diagram.Limit.html#1348" class="Function">unrep</a> <a id="1828" href="Categories.Diagram.Limit.html#1801" class="Bound">f</a><a id="1829" class="Symbol">)</a> <a id="1831" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="1833" href="Categories.Diagram.Limit.html#1801" class="Bound">f</a>
  <a id="1837" href="Categories.Diagram.Limit.html#1792" class="Function">g-η</a> <a id="1841" class="Symbol">{</a><a id="1842" class="Argument">f</a> <a id="1844" class="Symbol">=</a> <a id="1846" href="Categories.Diagram.Limit.html#1846" class="Bound">f</a><a id="1847" class="Symbol">}</a> <a id="1849" class="Symbol">=</a> <a id="1851" href="Categories.Object.Terminal.html#604" class="Function">terminal.!-unique</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Categories.Diagram.Limit.html#1518" class="Function">conify</a> <a id="1877" href="Categories.Diagram.Limit.html#1846" class="Bound">f</a><a id="1878" class="Symbol">)</a>

  <a id="Limit.η-cone"></a><a id="1883" href="Categories.Diagram.Limit.html#1883" class="Function">η-cone</a> <a id="1890" class="Symbol">:</a> <a id="1892" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="1898" href="Categories.Category.html#610" class="Function Operator">[</a> <a id="1900" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1909" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="1915" href="Categories.Category.html#610" class="Function Operator">≈</a> <a id="1917" href="Categories.Category.Core.html#563" class="Field">Category.id</a> <a id="1929" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="1935" href="Categories.Category.html#610" class="Function Operator">]</a>
  <a id="1939" href="Categories.Diagram.Limit.html#1883" class="Function">η-cone</a> <a id="1946" class="Symbol">=</a> <a id="1948" href="Categories.Object.Terminal.html#802" class="Function">terminal.⊤-id</a> <a id="1962" class="Symbol">(</a><a id="1963" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="1972" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a><a id="1977" class="Symbol">)</a>

  <a id="Limit.η"></a><a id="1982" href="Categories.Diagram.Limit.html#1982" class="Function">η</a> <a id="1984" class="Symbol">:</a> <a id="1986" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="1990" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="1996" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="1998" href="Categories.Category.Core.html#563" class="Function">id</a>
  <a id="2003" href="Categories.Diagram.Limit.html#1982" class="Function">η</a> <a id="2005" class="Symbol">=</a> <a id="2007" href="Categories.Diagram.Limit.html#1883" class="Function">η-cone</a>

  <a id="Limit.rep-cone∘"></a><a id="2017" href="Categories.Diagram.Limit.html#2017" class="Function">rep-cone∘</a> <a id="2027" class="Symbol">:</a> <a id="2029" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2035" href="Categories.Category.html#610" class="Function Operator">[</a> <a id="2037" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2043" href="Categories.Category.html#701" class="Function Operator">[</a> <a id="2045" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="2054" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="2056" href="Categories.Category.html#701" class="Function Operator">∘</a> <a id="2058" href="Categories.Diagram.Limit.html#821" class="Generalizable">q</a> <a id="2060" href="Categories.Category.html#701" class="Function Operator">]</a> <a id="2062" href="Categories.Category.html#610" class="Function Operator">≈</a> <a id="2064" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="2073" href="Categories.Diagram.Limit.html#772" class="Generalizable">K′</a> <a id="2076" href="Categories.Category.html#610" class="Function Operator">]</a>
  <a id="2080" href="Categories.Diagram.Limit.html#2017" class="Function">rep-cone∘</a> <a id="2090" class="Symbol">{</a><a id="2091" class="Argument">K</a> <a id="2093" class="Symbol">=</a> <a id="2095" href="Categories.Diagram.Limit.html#2095" class="Bound">K</a><a id="2096" class="Symbol">}</a> <a id="2098" class="Symbol">{</a><a id="2099" class="Argument">q</a> <a id="2101" class="Symbol">=</a> <a id="2103" href="Categories.Diagram.Limit.html#2103" class="Bound">q</a><a id="2104" class="Symbol">}</a> <a id="2106" class="Symbol">=</a> <a id="2108" href="Relation.Binary.Structures.html#1594" class="Function">Equiv.sym</a> <a id="2118" class="Symbol">(</a><a id="2119" href="Categories.Object.Terminal.html#604" class="Function">terminal.!-unique</a> <a id="2137" class="Symbol">(</a><a id="2138" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2144" href="Categories.Category.html#701" class="Function Operator">[</a> <a id="2146" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="2155" href="Categories.Diagram.Limit.html#2095" class="Bound">K</a> <a id="2157" href="Categories.Category.html#701" class="Function Operator">∘</a> <a id="2159" href="Categories.Diagram.Limit.html#2103" class="Bound">q</a> <a id="2161" href="Categories.Category.html#701" class="Function Operator">]</a><a id="2162" class="Symbol">))</a>

  <a id="Limit.rep∘"></a><a id="2168" href="Categories.Diagram.Limit.html#2168" class="Function">rep∘</a> <a id="2173" class="Symbol">:</a> <a id="2175" class="Symbol">∀</a> <a id="2177" class="Symbol">{</a><a id="2178" href="Categories.Diagram.Limit.html#2178" class="Bound">q</a> <a id="2180" class="Symbol">:</a> <a id="2182" href="Categories.Diagram.Limit.html#772" class="Generalizable">K′</a> <a id="2185" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="2187" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a><a id="2188" class="Symbol">}</a> <a id="2190" class="Symbol">→</a> <a id="2192" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="2196" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="2198" href="Categories.Category.Core.html#589" class="Function Operator">∘</a> <a id="2200" href="Categories.Diagram.Cone.html#717" class="Field">_⇨_.arr</a> <a id="2208" href="Categories.Diagram.Limit.html#2178" class="Bound">q</a> <a id="2210" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="2212" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="2216" href="Categories.Diagram.Limit.html#772" class="Generalizable">K′</a>
  <a id="2221" href="Categories.Diagram.Limit.html#2168" class="Function">rep∘</a> <a id="2226" class="Symbol">{</a><a id="2227" class="Argument">q</a> <a id="2229" class="Symbol">=</a> <a id="2231" href="Categories.Diagram.Limit.html#2231" class="Bound">q</a><a id="2232" class="Symbol">}</a> <a id="2234" class="Symbol">=</a> <a id="2236" href="Categories.Diagram.Limit.html#2017" class="Function">rep-cone∘</a> <a id="2246" class="Symbol">{</a><a id="2247" class="Argument">q</a> <a id="2249" class="Symbol">=</a> <a id="2251" href="Categories.Diagram.Limit.html#2231" class="Bound">q</a><a id="2252" class="Symbol">}</a>

  <a id="Limit.rep-cone-self-id"></a><a id="2257" href="Categories.Diagram.Limit.html#2257" class="Function">rep-cone-self-id</a> <a id="2274" class="Symbol">:</a> <a id="2276" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2282" href="Categories.Category.html#610" class="Function Operator">[</a> <a id="2284" href="Categories.Diagram.Limit.html#1211" class="Function">rep-cone</a> <a id="2293" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2299" href="Categories.Category.html#610" class="Function Operator">≈</a> <a id="2301" href="Categories.Category.Core.html#563" class="Function">Cones.id</a> <a id="2310" href="Categories.Category.html#610" class="Function Operator">]</a>
  <a id="2314" href="Categories.Diagram.Limit.html#2257" class="Function">rep-cone-self-id</a> <a id="2331" class="Symbol">=</a> <a id="2333" href="Categories.Object.Terminal.html#604" class="Function">terminal.!-unique</a> <a id="2351" href="Categories.Category.Core.html#563" class="Function">Cones.id</a>

  <a id="Limit.rep-self-id"></a><a id="2363" href="Categories.Diagram.Limit.html#2363" class="Function">rep-self-id</a> <a id="2375" class="Symbol">:</a> <a id="2377" href="Categories.Diagram.Limit.html#1271" class="Function">rep</a> <a id="2381" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2387" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="2389" href="Categories.Category.Core.html#563" class="Function">id</a>
  <a id="2394" href="Categories.Diagram.Limit.html#2363" class="Function">rep-self-id</a> <a id="2406" class="Symbol">=</a> <a id="2408" href="Categories.Diagram.Limit.html#2257" class="Function">rep-cone-self-id</a>

<a id="2426" class="Keyword">open</a> <a id="2431" href="Categories.Diagram.Limit.html#934" class="Module">Limit</a>

<a id="up-to-iso-cone"></a><a id="2438" href="Categories.Diagram.Limit.html#2438" class="Function">up-to-iso-cone</a> <a id="2453" class="Symbol">:</a> <a id="2455" class="Symbol">(</a><a id="2456" href="Categories.Diagram.Limit.html#2456" class="Bound">L₁</a> <a id="2459" href="Categories.Diagram.Limit.html#2459" class="Bound">L₂</a> <a id="2462" class="Symbol">:</a> <a id="2464" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a><a id="2469" class="Symbol">)</a> <a id="2471" class="Symbol">→</a> <a id="2473" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2479" href="Categories.Diagram.Limit.html#2456" class="Bound">L₁</a> <a id="2482" href="Categories.Diagram.Limit.html#741" class="Record Operator">⇔</a> <a id="2484" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2490" href="Categories.Diagram.Limit.html#2459" class="Bound">L₂</a>
<a id="2493" href="Categories.Diagram.Limit.html#2438" class="Function">up-to-iso-cone</a> <a id="2508" href="Categories.Diagram.Limit.html#2508" class="Bound">L₁</a> <a id="2511" href="Categories.Diagram.Limit.html#2511" class="Bound">L₂</a> <a id="2514" class="Symbol">=</a> <a id="2516" href="Categories.Object.Terminal.html#986" class="Function">T.up-to-iso</a> <a id="2528" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2534" class="Symbol">(</a><a id="2535" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a> <a id="2544" href="Categories.Diagram.Limit.html#2508" class="Bound">L₁</a><a id="2546" class="Symbol">)</a> <a id="2548" class="Symbol">(</a><a id="2549" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a> <a id="2558" href="Categories.Diagram.Limit.html#2511" class="Bound">L₂</a><a id="2560" class="Symbol">)</a>

<a id="up-to-iso"></a><a id="2563" href="Categories.Diagram.Limit.html#2563" class="Function">up-to-iso</a> <a id="2573" class="Symbol">:</a> <a id="2575" class="Symbol">(</a><a id="2576" href="Categories.Diagram.Limit.html#2576" class="Bound">L₁</a> <a id="2579" href="Categories.Diagram.Limit.html#2579" class="Bound">L₂</a> <a id="2582" class="Symbol">:</a> <a id="2584" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a><a id="2589" class="Symbol">)</a> <a id="2591" class="Symbol">→</a> <a id="2593" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a> <a id="2598" href="Categories.Diagram.Limit.html#2576" class="Bound">L₁</a> <a id="2601" href="Categories.Morphism.html#980" class="Record Operator">≅</a> <a id="2603" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a> <a id="2608" href="Categories.Diagram.Limit.html#2579" class="Bound">L₂</a>
<a id="2611" href="Categories.Diagram.Limit.html#2563" class="Function">up-to-iso</a> <a id="2621" href="Categories.Diagram.Limit.html#2621" class="Bound">L₁</a> <a id="2624" href="Categories.Diagram.Limit.html#2624" class="Bound">L₂</a> <a id="2627" class="Symbol">=</a> <a id="2629" href="Categories.Category.Construction.Cones.html#2037" class="Function">iso-cone⇒iso-apex</a> <a id="2647" class="Symbol">(</a><a id="2648" href="Categories.Diagram.Limit.html#2438" class="Function">up-to-iso-cone</a> <a id="2663" href="Categories.Diagram.Limit.html#2621" class="Bound">L₁</a> <a id="2666" href="Categories.Diagram.Limit.html#2624" class="Bound">L₂</a><a id="2668" class="Symbol">)</a>

<a id="transport-by-iso-cone"></a><a id="2671" href="Categories.Diagram.Limit.html#2671" class="Function">transport-by-iso-cone</a> <a id="2693" class="Symbol">:</a> <a id="2695" class="Symbol">(</a><a id="2696" href="Categories.Diagram.Limit.html#2696" class="Bound">L</a> <a id="2698" class="Symbol">:</a> <a id="2700" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a><a id="2705" class="Symbol">)</a> <a id="2707" class="Symbol">→</a> <a id="2709" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2715" href="Categories.Diagram.Limit.html#2696" class="Bound">L</a> <a id="2717" href="Categories.Diagram.Limit.html#741" class="Record Operator">⇔</a> <a id="2719" href="Categories.Diagram.Limit.html#770" class="Generalizable">K</a> <a id="2721" class="Symbol">→</a> <a id="2723" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a>
<a id="2729" href="Categories.Diagram.Limit.html#2671" class="Function">transport-by-iso-cone</a> <a id="2751" href="Categories.Diagram.Limit.html#2751" class="Bound">L</a> <a id="2753" href="Categories.Diagram.Limit.html#2753" class="Bound">L⇿K</a> <a id="2757" class="Symbol">=</a> <a id="2759" class="Keyword">record</a>
  <a id="2768" class="Symbol">{</a> <a id="2770" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a> <a id="2779" class="Symbol">=</a> <a id="2781" href="Categories.Object.Terminal.html#1150" class="Function">T.transport-by-iso</a> <a id="2800" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="2806" class="Symbol">(</a><a id="2807" href="Categories.Diagram.Limit.html#991" class="Field">terminal</a> <a id="2816" href="Categories.Diagram.Limit.html#2751" class="Bound">L</a><a id="2817" class="Symbol">)</a> <a id="2819" href="Categories.Diagram.Limit.html#2753" class="Bound">L⇿K</a>
  <a id="2825" class="Symbol">}</a>

<a id="transport-by-iso"></a><a id="2828" href="Categories.Diagram.Limit.html#2828" class="Function">transport-by-iso</a> <a id="2845" class="Symbol">:</a> <a id="2847" class="Symbol">(</a><a id="2848" href="Categories.Diagram.Limit.html#2848" class="Bound">L</a> <a id="2850" class="Symbol">:</a> <a id="2852" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a><a id="2857" class="Symbol">)</a> <a id="2859" class="Symbol">→</a> <a id="2861" href="Categories.Diagram.Limit.html#1158" class="Function">apex</a> <a id="2866" href="Categories.Diagram.Limit.html#2848" class="Bound">L</a> <a id="2868" href="Categories.Morphism.html#980" class="Record Operator">≅</a> <a id="2870" href="Categories.Diagram.Limit.html#805" class="Generalizable">X</a> <a id="2872" class="Symbol">→</a> <a id="2874" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a>
<a id="2880" href="Categories.Diagram.Limit.html#2828" class="Function">transport-by-iso</a> <a id="2897" href="Categories.Diagram.Limit.html#2897" class="Bound">L</a> <a id="2899" href="Categories.Diagram.Limit.html#2899" class="Bound">L≅X</a> <a id="2903" class="Symbol">=</a> <a id="2905" href="Categories.Diagram.Limit.html#2671" class="Function">transport-by-iso-cone</a> <a id="2927" href="Categories.Diagram.Limit.html#2897" class="Bound">L</a> <a id="2929" class="Symbol">(</a><a id="2930" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a> <a id="2936" href="Categories.Diagram.Limit.html#2947" class="Function">p</a><a id="2937" class="Symbol">)</a>
  <a id="2941" class="Keyword">where</a> <a id="2947" href="Categories.Diagram.Limit.html#2947" class="Function">p</a> <a id="2949" class="Symbol">=</a> <a id="2951" href="Categories.Category.Construction.Cones.html#1525" class="Function">cone-resp-iso</a> <a id="2965" class="Symbol">(</a><a id="2966" href="Categories.Diagram.Limit.html#1097" class="Function">limit</a> <a id="2972" href="Categories.Diagram.Limit.html#2897" class="Bound">L</a><a id="2973" class="Symbol">)</a> <a id="2975" href="Categories.Diagram.Limit.html#2899" class="Bound">L≅X</a>

<a id="2980" class="Keyword">module</a> <a id="2987" href="Categories.Diagram.Limit.html#2987" class="Module">_</a> <a id="2989" class="Symbol">(</a><a id="2990" href="Categories.Diagram.Limit.html#2990" class="Bound">X</a> <a id="2992" class="Symbol">:</a> <a id="2994" href="Categories.Category.Core.html#492" class="Function">Obj</a><a id="2997" class="Symbol">)</a> <a id="2999" class="Symbol">(</a><a id="3000" href="Categories.Diagram.Limit.html#3000" class="Bound">apex₁</a> <a id="3006" class="Symbol">:</a> <a id="3008" href="Categories.Diagram.Cone.html#358" class="Record">Apex</a> <a id="3013" href="Categories.Diagram.Limit.html#2990" class="Bound">X</a><a id="3014" class="Symbol">)</a> <a id="3016" class="Symbol">(</a><a id="3017" href="Categories.Diagram.Limit.html#3017" class="Bound">apex₂</a> <a id="3023" class="Symbol">:</a> <a id="3025" href="Categories.Diagram.Cone.html#358" class="Record">Apex</a> <a id="3030" href="Categories.Diagram.Limit.html#2990" class="Bound">X</a><a id="3031" class="Symbol">)</a> <a id="3033" class="Symbol">(</a><a id="3034" href="Categories.Diagram.Limit.html#3034" class="Bound">L</a> <a id="3036" class="Symbol">:</a> <a id="3038" href="Categories.Diagram.Limit.html#934" class="Record">Limit</a><a id="3043" class="Symbol">)</a> <a id="3045" class="Keyword">where</a>
  <a id="3053" class="Keyword">private</a>
    <a id="3065" class="Keyword">module</a> <a id="apex₁"></a><a id="3072" href="Categories.Diagram.Limit.html#3072" class="Module">apex₁</a> <a id="3078" class="Symbol">=</a> <a id="3080" href="Categories.Diagram.Cone.html#358" class="Module">Apex</a> <a id="3085" href="Categories.Diagram.Limit.html#3000" class="Bound">apex₁</a>
    <a id="3095" class="Keyword">module</a> <a id="apex₂"></a><a id="3102" href="Categories.Diagram.Limit.html#3102" class="Module">apex₂</a> <a id="3108" class="Symbol">=</a> <a id="3110" href="Categories.Diagram.Cone.html#358" class="Module">Apex</a> <a id="3115" href="Categories.Diagram.Limit.html#3017" class="Bound">apex₂</a>
    <a id="3125" class="Keyword">module</a> <a id="L"></a><a id="3132" href="Categories.Diagram.Limit.html#3132" class="Module">L</a> <a id="3134" class="Symbol">=</a> <a id="3136" href="Categories.Diagram.Limit.html#934" class="Module">Limit</a> <a id="3142" href="Categories.Diagram.Limit.html#3034" class="Bound">L</a>

    <a id="3149" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a> <a id="3152" class="Symbol">:</a> <a id="3154" href="Categories.Diagram.Cone.html#526" class="Record">Cone</a>
    <a id="3163" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a> <a id="3166" class="Symbol">=</a> <a id="3168" class="Keyword">record</a> <a id="3175" class="Symbol">{</a> <a id="3177" href="Categories.Diagram.Cone.html#592" class="Field">apex</a> <a id="3182" class="Symbol">=</a> <a id="3184" href="Categories.Diagram.Limit.html#3000" class="Bound">apex₁</a> <a id="3190" class="Symbol">}</a>
    <a id="3196" class="Keyword">module</a> <a id="K₁"></a><a id="3203" href="Categories.Diagram.Limit.html#3203" class="Module">K₁</a> <a id="3206" class="Symbol">=</a> <a id="3208" href="Categories.Diagram.Cone.html#526" class="Module">Cone</a> <a id="3213" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a>

    <a id="3221" href="Categories.Diagram.Limit.html#3221" class="Function">K₂</a> <a id="3224" class="Symbol">:</a> <a id="3226" href="Categories.Diagram.Cone.html#526" class="Record">Cone</a>
    <a id="3235" href="Categories.Diagram.Limit.html#3221" class="Function">K₂</a> <a id="3238" class="Symbol">=</a> <a id="3240" class="Keyword">record</a> <a id="3247" class="Symbol">{</a> <a id="3249" href="Categories.Diagram.Cone.html#592" class="Field">apex</a> <a id="3254" class="Symbol">=</a> <a id="3256" href="Categories.Diagram.Limit.html#3017" class="Bound">apex₂</a> <a id="3262" class="Symbol">}</a>
    <a id="3268" class="Keyword">module</a> <a id="K₂"></a><a id="3275" href="Categories.Diagram.Limit.html#3275" class="Module">K₂</a> <a id="3278" class="Symbol">=</a> <a id="3280" href="Categories.Diagram.Cone.html#526" class="Module">Cone</a> <a id="3285" href="Categories.Diagram.Limit.html#3221" class="Function">K₂</a>

  <a id="3291" href="Categories.Diagram.Limit.html#3291" class="Function">ψ-≈⇒rep-≈</a> <a id="3301" class="Symbol">:</a> <a id="3303" class="Symbol">(∀</a> <a id="3306" href="Categories.Diagram.Limit.html#3306" class="Bound">A</a> <a id="3308" class="Symbol">→</a> <a id="3310" href="Categories.Diagram.Cone.html#415" class="Function">apex₁.ψ</a> <a id="3318" href="Categories.Diagram.Limit.html#3306" class="Bound">A</a> <a id="3320" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="3322" href="Categories.Diagram.Cone.html#415" class="Function">apex₂.ψ</a> <a id="3330" href="Categories.Diagram.Limit.html#3306" class="Bound">A</a><a id="3331" class="Symbol">)</a> <a id="3333" class="Symbol">→</a> <a id="3335" href="Categories.Diagram.Limit.html#1271" class="Function">L.rep</a> <a id="3341" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a> <a id="3344" href="Categories.Category.Core.html#528" class="Function Operator">≈</a> <a id="3346" href="Categories.Diagram.Limit.html#1271" class="Function">L.rep</a> <a id="3352" href="Categories.Diagram.Limit.html#3221" class="Function">K₂</a>
  <a id="3357" href="Categories.Diagram.Limit.html#3291" class="Function">ψ-≈⇒rep-≈</a> <a id="3367" href="Categories.Diagram.Limit.html#3367" class="Bound">∀eq</a> <a id="3371" class="Symbol">=</a> <a id="3373" class="Symbol">(</a><a id="3374" href="Categories.Object.Terminal.html#604" class="Function">L.terminal.!-unique</a> <a id="3394" href="Categories.Diagram.Limit.html#3573" class="Function">K₁⇒limit</a><a id="3402" class="Symbol">)</a> <a id="3404" href="Categories.Category.Core.html#2639" class="Function Operator">○</a> <a id="3406" href="Categories.Category.Core.html#1078" class="Function">identityʳ</a>
    <a id="3420" class="Keyword">where</a> <a id="3426" href="Categories.Diagram.Limit.html#3426" class="Function">K₁⇒K₂</a> <a id="3432" class="Symbol">:</a> <a id="3434" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a> <a id="3437" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="3439" href="Categories.Diagram.Limit.html#3221" class="Function">K₂</a>
          <a id="3452" href="Categories.Diagram.Limit.html#3426" class="Function">K₁⇒K₂</a> <a id="3458" class="Symbol">=</a> <a id="3460" class="Keyword">record</a>
            <a id="3479" class="Symbol">{</a> <a id="3481" href="Categories.Diagram.Cone.html#717" class="Field">arr</a>     <a id="3489" class="Symbol">=</a> <a id="3491" href="Categories.Category.Core.html#563" class="Function">id</a>
            <a id="3506" class="Symbol">;</a> <a id="3508" href="Categories.Diagram.Cone.html#742" class="Field">commute</a> <a id="3516" class="Symbol">=</a> <a id="3518" class="Symbol">λ</a> <a id="3520" class="Symbol">{</a><a id="3521" href="Categories.Diagram.Limit.html#3521" class="Bound">X</a><a id="3522" class="Symbol">}</a> <a id="3524" class="Symbol">→</a> <a id="3526" href="Categories.Category.Core.html#1078" class="Function">identityʳ</a> <a id="3536" href="Categories.Category.Core.html#2639" class="Function Operator">○</a> <a id="3538" href="Categories.Category.Core.html#2583" class="Function">⟺</a> <a id="3540" class="Symbol">(</a><a id="3541" href="Categories.Diagram.Limit.html#3367" class="Bound">∀eq</a> <a id="3545" href="Categories.Diagram.Limit.html#3521" class="Bound">X</a><a id="3546" class="Symbol">)</a>
            <a id="3560" class="Symbol">}</a>

          <a id="3573" href="Categories.Diagram.Limit.html#3573" class="Function">K₁⇒limit</a> <a id="3582" class="Symbol">:</a> <a id="3584" href="Categories.Diagram.Limit.html#3149" class="Function">K₁</a> <a id="3587" href="Categories.Diagram.Limit.html#507" class="Record Operator">⇨</a> <a id="3589" href="Categories.Diagram.Limit.html#1097" class="Function">L.limit</a>
          <a id="3607" href="Categories.Diagram.Limit.html#3573" class="Function">K₁⇒limit</a> <a id="3616" class="Symbol">=</a> <a id="3618" href="Categories.Category.Construction.Cones.html#632" class="Function">Cones</a> <a id="3624" href="Categories.Category.html#701" class="Function Operator">[</a> <a id="3626" href="Categories.Object.Terminal.html#576" class="Function">L.terminal.!</a> <a id="3639" href="Categories.Category.html#701" class="Function Operator">∘</a> <a id="3641" href="Categories.Diagram.Limit.html#3426" class="Function">K₁⇒K₂</a> <a id="3647" href="Categories.Category.html#701" class="Function Operator">]</a>
</pre></body></html>
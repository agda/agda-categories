<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Category.Construction.Arrow</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>
<a id="36" class="Keyword">open</a> <a id="41" class="Keyword">import</a> <a id="48" href="Categories.Category.html" class="Module">Categories.Category</a> <a id="68" class="Keyword">using</a> <a id="74" class="Symbol">(</a><a id="75" href="Categories.Category.Core.html#442" class="Record">Category</a><a id="83" class="Symbol">;</a> <a id="85" class="Keyword">module</a> <a id="92" href="Categories.Category.html#824" class="Module">Definitions</a><a id="103" class="Symbol">)</a>

<a id="106" class="Comment">-- Definition of the Arrow Category of a Category C</a>
<a id="158" class="Keyword">module</a> <a id="165" href="Categories.Category.Construction.Arrow.html" class="Module">Categories.Category.Construction.Arrow</a> <a id="204" class="Symbol">{</a><a id="205" href="Categories.Category.Construction.Arrow.html#205" class="Bound">o</a> <a id="207" href="Categories.Category.Construction.Arrow.html#207" class="Bound">ℓ</a> <a id="209" href="Categories.Category.Construction.Arrow.html#209" class="Bound">e</a><a id="210" class="Symbol">}</a> <a id="212" class="Symbol">(</a><a id="213" href="Categories.Category.Construction.Arrow.html#213" class="Bound">C</a> <a id="215" class="Symbol">:</a> <a id="217" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="226" href="Categories.Category.Construction.Arrow.html#205" class="Bound">o</a> <a id="228" href="Categories.Category.Construction.Arrow.html#207" class="Bound">ℓ</a> <a id="230" href="Categories.Category.Construction.Arrow.html#209" class="Bound">e</a><a id="231" class="Symbol">)</a> <a id="233" class="Keyword">where</a>

<a id="240" class="Keyword">open</a> <a id="245" class="Keyword">import</a> <a id="252" href="Level.html" class="Module">Level</a>
<a id="258" class="Keyword">open</a> <a id="263" class="Keyword">import</a> <a id="270" href="Data.Product.html" class="Module">Data.Product</a> <a id="283" class="Keyword">using</a> <a id="289" class="Symbol">(</a><a id="290" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="293" class="Symbol">;</a> <a id="295" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="298" class="Symbol">;</a> <a id="300" href="Data.Product.Base.html#2173" class="Function">map</a><a id="303" class="Symbol">;</a> <a id="305" href="Data.Product.Base.html#2747" class="Function">zip</a><a id="308" class="Symbol">)</a>
<a id="310" class="Keyword">open</a> <a id="315" class="Keyword">import</a> <a id="322" href="Function.Base.html" class="Module">Function.Base</a> <a id="336" class="Keyword">using</a> <a id="342" class="Symbol">(</a><a id="343" href="Function.Base.html#1993" class="Function Operator">_$_</a><a id="346" class="Symbol">)</a>
<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="381" class="Keyword">using</a> <a id="387" class="Symbol">(</a><a id="388" href="Relation.Binary.Core.html#896" class="Function">Rel</a><a id="391" class="Symbol">)</a>

<a id="394" class="Keyword">import</a> <a id="401" href="Categories.Morphism.html" class="Module">Categories.Morphism</a> <a id="421" class="Symbol">as</a> <a id="424" class="Module">M</a>
<a id="426" class="Keyword">open</a> <a id="431" href="Categories.Morphism.html" class="Module">M</a> <a id="433" href="Categories.Category.Construction.Arrow.html#213" class="Bound">C</a>
<a id="435" class="Keyword">open</a> <a id="440" class="Keyword">import</a> <a id="447" href="Categories.Morphism.Reasoning.html" class="Module">Categories.Morphism.Reasoning</a> <a id="477" href="Categories.Category.Construction.Arrow.html#213" class="Bound">C</a>

<a id="480" class="Keyword">open</a> <a id="485" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="494" href="Categories.Category.Construction.Arrow.html#213" class="Bound">C</a>
<a id="496" class="Keyword">open</a> <a id="501" href="Categories.Category.html#824" class="Module">Definitions</a> <a id="513" href="Categories.Category.Construction.Arrow.html#213" class="Bound">C</a>
<a id="515" class="Keyword">open</a> <a id="520" href="Categories.Category.Core.html#2462" class="Module">HomReasoning</a>

<a id="534" class="Keyword">private</a>
  <a id="544" class="Keyword">variable</a>
    <a id="557" href="Categories.Category.Construction.Arrow.html#557" class="Generalizable">A</a> <a id="559" href="Categories.Category.Construction.Arrow.html#559" class="Generalizable">B</a> <a id="561" href="Categories.Category.Construction.Arrow.html#561" class="Generalizable">D</a> <a id="563" href="Categories.Category.Construction.Arrow.html#563" class="Generalizable">E</a> <a id="565" class="Symbol">:</a> <a id="567" href="Categories.Category.Core.html#559" class="Field">Obj</a>

<a id="572" class="Keyword">record</a> <a id="Morphism"></a><a id="579" href="Categories.Category.Construction.Arrow.html#579" class="Record">Morphism</a> <a id="588" class="Symbol">:</a> <a id="590" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="594" class="Symbol">(</a><a id="595" href="Categories.Category.Construction.Arrow.html#205" class="Bound">o</a> <a id="597" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="599" href="Categories.Category.Construction.Arrow.html#207" class="Bound">ℓ</a><a id="600" class="Symbol">)</a> <a id="602" class="Keyword">where</a>
  <a id="610" class="Keyword">constructor</a> <a id="mor"></a><a id="622" href="Categories.Category.Construction.Arrow.html#622" class="InductiveConstructor">mor</a>
  <a id="628" class="Keyword">field</a>
    <a id="638" class="Symbol">{</a><a id="Morphism.dom"></a><a id="639" href="Categories.Category.Construction.Arrow.html#639" class="Field">dom</a><a id="642" class="Symbol">}</a> <a id="644" class="Symbol">:</a> <a id="646" href="Categories.Category.Core.html#559" class="Field">Obj</a>
    <a id="654" class="Symbol">{</a><a id="Morphism.cod"></a><a id="655" href="Categories.Category.Construction.Arrow.html#655" class="Field">cod</a><a id="658" class="Symbol">}</a> <a id="660" class="Symbol">:</a> <a id="662" href="Categories.Category.Core.html#559" class="Field">Obj</a>
    <a id="Morphism.arr"></a><a id="670" href="Categories.Category.Construction.Arrow.html#670" class="Field">arr</a>   <a id="676" class="Symbol">:</a> <a id="678" href="Categories.Category.Construction.Arrow.html#639" class="Field">dom</a> <a id="682" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="684" href="Categories.Category.Construction.Arrow.html#655" class="Field">cod</a>

<a id="689" class="Keyword">record</a> <a id="Morphism⇒"></a><a id="696" href="Categories.Category.Construction.Arrow.html#696" class="Record">Morphism⇒</a> <a id="706" class="Symbol">(</a><a id="707" href="Categories.Category.Construction.Arrow.html#707" class="Bound">f</a> <a id="709" href="Categories.Category.Construction.Arrow.html#709" class="Bound">g</a> <a id="711" class="Symbol">:</a> <a id="713" href="Categories.Category.Construction.Arrow.html#579" class="Record">Morphism</a><a id="721" class="Symbol">)</a> <a id="723" class="Symbol">:</a> <a id="725" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="729" class="Symbol">(</a><a id="730" href="Categories.Category.Construction.Arrow.html#207" class="Bound">ℓ</a> <a id="732" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="734" href="Categories.Category.Construction.Arrow.html#209" class="Bound">e</a><a id="735" class="Symbol">)</a> <a id="737" class="Keyword">where</a>
  <a id="745" class="Keyword">constructor</a> <a id="mor⇒"></a><a id="757" href="Categories.Category.Construction.Arrow.html#757" class="InductiveConstructor">mor⇒</a>
  <a id="764" class="Keyword">private</a>
    <a id="776" class="Keyword">module</a> <a id="Morphism⇒.f"></a><a id="783" href="Categories.Category.Construction.Arrow.html#783" class="Module">f</a> <a id="785" class="Symbol">=</a> <a id="787" href="Categories.Category.Construction.Arrow.html#579" class="Module">Morphism</a> <a id="796" href="Categories.Category.Construction.Arrow.html#707" class="Bound">f</a>
    <a id="802" class="Keyword">module</a> <a id="Morphism⇒.g"></a><a id="809" href="Categories.Category.Construction.Arrow.html#809" class="Module">g</a> <a id="811" class="Symbol">=</a> <a id="813" href="Categories.Category.Construction.Arrow.html#579" class="Module">Morphism</a> <a id="822" href="Categories.Category.Construction.Arrow.html#709" class="Bound">g</a>

  <a id="827" class="Keyword">field</a>
    <a id="837" class="Symbol">{</a><a id="Morphism⇒.dom⇒"></a><a id="838" href="Categories.Category.Construction.Arrow.html#838" class="Field">dom⇒</a><a id="842" class="Symbol">}</a> <a id="844" class="Symbol">:</a> <a id="846" href="Categories.Category.Construction.Arrow.html#639" class="Function">f.dom</a> <a id="852" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="854" href="Categories.Category.Construction.Arrow.html#639" class="Function">g.dom</a>
    <a id="864" class="Symbol">{</a><a id="Morphism⇒.cod⇒"></a><a id="865" href="Categories.Category.Construction.Arrow.html#865" class="Field">cod⇒</a><a id="869" class="Symbol">}</a> <a id="871" class="Symbol">:</a> <a id="873" href="Categories.Category.Construction.Arrow.html#655" class="Function">f.cod</a> <a id="879" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="881" href="Categories.Category.Construction.Arrow.html#655" class="Function">g.cod</a>
    <a id="Morphism⇒.square"></a><a id="891" href="Categories.Category.Construction.Arrow.html#891" class="Field">square</a> <a id="898" class="Symbol">:</a> <a id="900" href="Categories.Category.html#884" class="Function">CommutativeSquare</a> <a id="918" href="Categories.Category.Construction.Arrow.html#670" class="Function">f.arr</a> <a id="924" href="Categories.Category.Construction.Arrow.html#838" class="Field">dom⇒</a> <a id="929" href="Categories.Category.Construction.Arrow.html#865" class="Field">cod⇒</a> <a id="934" href="Categories.Category.Construction.Arrow.html#670" class="Function">g.arr</a>

<a id="Arrow"></a><a id="941" href="Categories.Category.Construction.Arrow.html#941" class="Function">Arrow</a> <a id="947" class="Symbol">:</a> <a id="949" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="958" class="Symbol">_</a> <a id="960" class="Symbol">_</a> <a id="962" class="Symbol">_</a>
<a id="964" href="Categories.Category.Construction.Arrow.html#941" class="Function">Arrow</a> <a id="970" class="Symbol">=</a> <a id="972" class="Keyword">record</a>
  <a id="981" class="Symbol">{</a> <a id="983" href="Categories.Category.Core.html#559" class="Field">Obj</a>       <a id="993" class="Symbol">=</a> <a id="995" href="Categories.Category.Construction.Arrow.html#579" class="Record">Morphism</a>
  <a id="1006" class="Symbol">;</a> <a id="1008" href="Categories.Category.Core.html#575" class="Field Operator">_⇒_</a>       <a id="1018" class="Symbol">=</a> <a id="1020" href="Categories.Category.Construction.Arrow.html#696" class="Record">Morphism⇒</a>
  <a id="1032" class="Symbol">;</a> <a id="1034" href="Categories.Category.Core.html#595" class="Field Operator">_≈_</a>       <a id="1044" class="Symbol">=</a> <a id="1046" class="Symbol">λ</a> <a id="1048" href="Categories.Category.Construction.Arrow.html#1048" class="Bound">f</a> <a id="1050" href="Categories.Category.Construction.Arrow.html#1050" class="Bound">g</a> <a id="1052" class="Symbol">→</a> <a id="1054" href="Categories.Category.Construction.Arrow.html#838" class="Field">dom⇒</a> <a id="1059" href="Categories.Category.Construction.Arrow.html#1048" class="Bound">f</a> <a id="1061" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="1063" href="Categories.Category.Construction.Arrow.html#838" class="Field">dom⇒</a> <a id="1068" href="Categories.Category.Construction.Arrow.html#1050" class="Bound">g</a> <a id="1070" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="1072" href="Categories.Category.Construction.Arrow.html#865" class="Field">cod⇒</a> <a id="1077" href="Categories.Category.Construction.Arrow.html#1048" class="Bound">f</a> <a id="1079" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="1081" href="Categories.Category.Construction.Arrow.html#865" class="Field">cod⇒</a> <a id="1086" href="Categories.Category.Construction.Arrow.html#1050" class="Bound">g</a>
  <a id="1090" class="Symbol">;</a> <a id="1092" href="Categories.Category.Core.html#630" class="Field">id</a>        <a id="1102" class="Symbol">=</a> <a id="1104" href="Categories.Category.Construction.Arrow.html#757" class="InductiveConstructor">mor⇒</a> <a id="1109" href="Function.Base.html#1993" class="Function Operator">$</a> <a id="1111" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1121" href="Categories.Category.Core.html#3061" class="Function Operator">○</a> <a id="1123" href="Categories.Category.Core.html#3005" class="Function">⟺</a> <a id="1125" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a>
  <a id="1137" class="Symbol">;</a> <a id="1139" href="Categories.Category.Core.html#656" class="Field Operator">_∘_</a>       <a id="1149" class="Symbol">=</a> <a id="1151" class="Symbol">λ</a> <a id="1153" href="Categories.Category.Construction.Arrow.html#1153" class="Bound">m₁</a> <a id="1156" href="Categories.Category.Construction.Arrow.html#1156" class="Bound">m₂</a> <a id="1159" class="Symbol">→</a> <a id="1161" href="Categories.Category.Construction.Arrow.html#757" class="InductiveConstructor">mor⇒</a> <a id="1166" href="Function.Base.html#1993" class="Function Operator">$</a> <a id="1168" href="Categories.Morphism.Reasoning.Core.html#5355" class="Function">glue</a> <a id="1173" class="Symbol">(</a><a id="1174" href="Categories.Category.Construction.Arrow.html#891" class="Field">square</a> <a id="1181" href="Categories.Category.Construction.Arrow.html#1153" class="Bound">m₁</a><a id="1183" class="Symbol">)</a> <a id="1185" class="Symbol">(</a><a id="1186" href="Categories.Category.Construction.Arrow.html#891" class="Field">square</a> <a id="1193" href="Categories.Category.Construction.Arrow.html#1156" class="Bound">m₂</a><a id="1195" class="Symbol">)</a>
  <a id="1199" class="Symbol">;</a> <a id="1201" href="Categories.Category.Core.html#715" class="Field">assoc</a>     <a id="1211" class="Symbol">=</a> <a id="1213" href="Categories.Category.Core.html#715" class="Field">assoc</a> <a id="1219" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1221" href="Categories.Category.Core.html#715" class="Field">assoc</a>
  <a id="1229" class="Symbol">;</a> <a id="1231" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a> <a id="1241" class="Symbol">=</a> <a id="1243" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a> <a id="1253" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1255" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a>
  <a id="1267" class="Symbol">;</a> <a id="1269" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1279" class="Symbol">=</a> <a id="1281" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1291" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1293" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a>
  <a id="1305" class="Symbol">;</a> <a id="1307" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1317" class="Symbol">=</a> <a id="1319" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1329" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1331" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a>
  <a id="1343" class="Symbol">;</a> <a id="1345" href="Categories.Category.Core.html#1339" class="Field">identity²</a> <a id="1355" class="Symbol">=</a> <a id="1357" href="Categories.Category.Core.html#1339" class="Field">identity²</a> <a id="1367" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1369" href="Categories.Category.Core.html#1339" class="Field">identity²</a>
  <a id="1381" class="Symbol">;</a> <a id="1383" href="Categories.Category.Core.html#1384" class="Field">equiv</a>     <a id="1393" class="Symbol">=</a> <a id="1395" class="Keyword">record</a>
    <a id="1406" class="Symbol">{</a> <a id="1408" href="Relation.Binary.Structures.html#1641" class="Field">refl</a>  <a id="1414" class="Symbol">=</a> <a id="1416" href="Relation.Binary.Structures.html#1641" class="Function">refl</a> <a id="1421" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1423" href="Relation.Binary.Structures.html#1641" class="Function">refl</a>
    <a id="1432" class="Symbol">;</a> <a id="1434" href="Relation.Binary.Structures.html#1667" class="Field">sym</a>   <a id="1440" class="Symbol">=</a> <a id="1442" href="Data.Product.Base.html#2173" class="Function">map</a> <a id="1446" href="Relation.Binary.Structures.html#1667" class="Function">sym</a> <a id="1450" href="Relation.Binary.Structures.html#1667" class="Function">sym</a>
    <a id="1458" class="Symbol">;</a> <a id="1460" href="Relation.Binary.Structures.html#1693" class="Field">trans</a> <a id="1466" class="Symbol">=</a> <a id="1468" href="Data.Product.Base.html#2747" class="Function">zip</a> <a id="1472" href="Relation.Binary.Structures.html#1693" class="Function">trans</a> <a id="1478" href="Relation.Binary.Structures.html#1693" class="Function">trans</a>
    <a id="1488" class="Symbol">}</a>
  <a id="1492" class="Symbol">;</a> <a id="1494" href="Categories.Category.Core.html#1438" class="Field">∘-resp-≈</a>  <a id="1504" class="Symbol">=</a> <a id="1506" href="Data.Product.Base.html#2747" class="Function">zip</a> <a id="1510" href="Categories.Category.Core.html#1438" class="Field">∘-resp-≈</a> <a id="1519" href="Categories.Category.Core.html#1438" class="Field">∘-resp-≈</a>
  <a id="1530" class="Symbol">}</a>
  <a id="1534" class="Keyword">where</a>
  <a id="1542" class="Keyword">open</a> <a id="1547" href="Categories.Category.Construction.Arrow.html#696" class="Module">Morphism⇒</a>
  <a id="1559" class="Keyword">open</a> <a id="1564" href="Categories.Category.Core.html#1530" class="Module">Equiv</a>

<a id="1571" class="Keyword">private</a>
  <a id="1581" class="Keyword">module</a> <a id="MM"></a><a id="1588" href="Categories.Category.Construction.Arrow.html#1588" class="Module">MM</a> <a id="1591" class="Symbol">=</a> <a id="1593" href="Categories.Morphism.html" class="Module">M</a> <a id="1595" href="Categories.Category.Construction.Arrow.html#941" class="Function">Arrow</a>

<a id="1602" class="Keyword">module</a> <a id="1609" href="Categories.Category.Construction.Arrow.html#1609" class="Module">_</a> <a id="1611" class="Keyword">where</a>
  <a id="1619" class="Keyword">open</a> <a id="1624" href="Categories.Morphism.html#1957" class="Module Operator">_≅_</a>
  <a id="1630" class="Keyword">open</a> <a id="1635" href="Categories.Category.Core.html#1530" class="Module">Equiv</a>

  <a id="1644" href="Categories.Category.Construction.Arrow.html#1644" class="Function">lift-iso</a> <a id="1653" class="Symbol">:</a> <a id="1655" class="Symbol">∀</a> <a id="1657" class="Symbol">{</a><a id="1658" href="Categories.Category.Construction.Arrow.html#1658" class="Bound">f</a> <a id="1660" href="Categories.Category.Construction.Arrow.html#1660" class="Bound">h</a><a id="1661" class="Symbol">}</a> <a id="1663" class="Symbol">→</a>
               <a id="1680" class="Symbol">(</a><a id="1681" href="Categories.Category.Construction.Arrow.html#1681" class="Bound">iso₁</a> <a id="1686" class="Symbol">:</a> <a id="1688" href="Categories.Category.Construction.Arrow.html#557" class="Generalizable">A</a> <a id="1690" href="Categories.Morphism.html#1957" class="Record Operator">≅</a> <a id="1692" href="Categories.Category.Construction.Arrow.html#561" class="Generalizable">D</a><a id="1693" class="Symbol">)</a> <a id="1695" class="Symbol">→</a> <a id="1697" class="Symbol">(</a><a id="1698" href="Categories.Category.Construction.Arrow.html#1698" class="Bound">iso₂</a> <a id="1703" class="Symbol">:</a> <a id="1705" href="Categories.Category.Construction.Arrow.html#559" class="Generalizable">B</a> <a id="1707" href="Categories.Morphism.html#1957" class="Record Operator">≅</a> <a id="1709" href="Categories.Category.Construction.Arrow.html#563" class="Generalizable">E</a><a id="1710" class="Symbol">)</a> <a id="1712" class="Symbol">→</a>
               <a id="1729" href="Categories.Category.html#884" class="Function">CommutativeSquare</a> <a id="1747" href="Categories.Category.Construction.Arrow.html#1658" class="Bound">f</a> <a id="1749" class="Symbol">(</a><a id="1750" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="1755" href="Categories.Category.Construction.Arrow.html#1681" class="Bound">iso₁</a><a id="1759" class="Symbol">)</a> <a id="1761" class="Symbol">(</a><a id="1762" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="1767" href="Categories.Category.Construction.Arrow.html#1698" class="Bound">iso₂</a><a id="1771" class="Symbol">)</a> <a id="1773" href="Categories.Category.Construction.Arrow.html#1660" class="Bound">h</a> <a id="1775" class="Symbol">→</a>
               <a id="1792" class="Keyword">record</a> <a id="1799" class="Symbol">{</a> <a id="1801" href="Categories.Category.Construction.Arrow.html#670" class="Field">arr</a> <a id="1805" class="Symbol">=</a> <a id="1807" href="Categories.Category.Construction.Arrow.html#1658" class="Bound">f</a> <a id="1809" class="Symbol">}</a> <a id="1811" href="Categories.Morphism.html#1957" class="Record Operator">MM.≅</a> <a id="1816" class="Keyword">record</a> <a id="1823" class="Symbol">{</a> <a id="1825" href="Categories.Category.Construction.Arrow.html#670" class="Field">arr</a> <a id="1829" class="Symbol">=</a> <a id="1831" href="Categories.Category.Construction.Arrow.html#1660" class="Bound">h</a> <a id="1833" class="Symbol">}</a>
  <a id="1837" href="Categories.Category.Construction.Arrow.html#1644" class="Function">lift-iso</a> <a id="1846" class="Symbol">{</a><a id="1847" class="Argument">f</a> <a id="1849" class="Symbol">=</a> <a id="1851" href="Categories.Category.Construction.Arrow.html#1851" class="Bound">f</a><a id="1852" class="Symbol">}</a> <a id="1854" class="Symbol">{</a><a id="1855" class="Argument">h</a> <a id="1857" class="Symbol">=</a> <a id="1859" href="Categories.Category.Construction.Arrow.html#1859" class="Bound">h</a><a id="1860" class="Symbol">}</a> <a id="1862" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="1867" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="1872" href="Categories.Category.Construction.Arrow.html#1872" class="Bound">sq</a> <a id="1875" class="Symbol">=</a> <a id="1877" class="Keyword">record</a>
    <a id="1888" class="Symbol">{</a> <a id="1890" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="1895" class="Symbol">=</a> <a id="1897" class="Keyword">record</a> <a id="1904" class="Symbol">{</a> <a id="1906" href="Categories.Category.Construction.Arrow.html#891" class="Field">square</a> <a id="1913" class="Symbol">=</a> <a id="1915" href="Categories.Category.Construction.Arrow.html#1872" class="Bound">sq</a> <a id="1918" class="Symbol">}</a>
    <a id="1924" class="Symbol">;</a> <a id="1926" href="Categories.Morphism.html#2022" class="Field">to</a>   <a id="1931" class="Symbol">=</a> <a id="1933" class="Keyword">record</a> <a id="1940" class="Symbol">{</a> <a id="1942" href="Categories.Category.Construction.Arrow.html#891" class="Field">square</a> <a id="1949" class="Symbol">=</a> <a id="1951" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
      <a id="1963" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="1966" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="1971" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1973" href="Categories.Category.Construction.Arrow.html#1859" class="Bound">h</a>                           <a id="2001" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2004" href="Categories.Morphism.Reasoning.Core.html#3964" class="Function">introʳ</a> <a id="2011" class="Symbol">(</a><a id="2012" href="Categories.Morphism.html#1612" class="Function">isoʳ</a> <a id="2017" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a><a id="2021" class="Symbol">)</a> <a id="2023" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2031" class="Symbol">(</a><a id="2032" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2035" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2040" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2042" href="Categories.Category.Construction.Arrow.html#1859" class="Bound">h</a><a id="2043" class="Symbol">)</a> <a id="2045" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2047" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="2052" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="2057" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2059" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2062" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a>   <a id="2069" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2072" href="Categories.Category.Core.html#715" class="Field">assoc</a> <a id="2078" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2086" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2089" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2094" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2096" href="Categories.Category.Construction.Arrow.html#1859" class="Bound">h</a> <a id="2098" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2100" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="2105" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="2110" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2112" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2115" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a>     <a id="2124" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="2128" href="Relation.Binary.Structures.html#1641" class="Function">refl</a> <a id="2133" href="Categories.Category.Core.html#2634" class="Function Operator">⟩∘⟨</a> <a id="2137" href="Categories.Morphism.Reasoning.Core.html#3550" class="Function">pushˡ</a> <a id="2143" href="Categories.Category.Construction.Arrow.html#1872" class="Bound">sq</a> <a id="2146" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
      <a id="2154" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2157" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2162" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2164" class="Symbol">(</a><a id="2165" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="2170" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2175" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2177" href="Categories.Category.Construction.Arrow.html#1851" class="Bound">f</a><a id="2178" class="Symbol">)</a> <a id="2180" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2182" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2185" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a>   <a id="2192" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2195" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a> <a id="2205" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2213" class="Symbol">(</a><a id="2214" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2217" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2222" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2224" class="Symbol">(</a><a id="2225" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="2230" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a> <a id="2235" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2237" href="Categories.Category.Construction.Arrow.html#1851" class="Bound">f</a><a id="2238" class="Symbol">))</a> <a id="2241" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2243" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2246" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="2251" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2254" href="Categories.Morphism.Reasoning.Core.html#8385" class="Function">cancelˡ</a> <a id="2262" class="Symbol">(</a><a id="2263" href="Categories.Morphism.html#1586" class="Function">isoˡ</a> <a id="2268" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a><a id="2272" class="Symbol">)</a> <a id="2274" href="Categories.Category.Core.html#2634" class="Function Operator">⟩∘⟨</a> <a id="2278" href="Relation.Binary.Structures.html#1641" class="Function">refl</a> <a id="2283" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2291" href="Categories.Category.Construction.Arrow.html#1851" class="Bound">f</a> <a id="2293" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2295" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2298" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a>                           <a id="2329" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a> <a id="2331" class="Symbol">}</a>
    <a id="2337" class="Symbol">;</a> <a id="2339" href="Categories.Morphism.html#2039" class="Field">iso</a>  <a id="2344" class="Symbol">=</a> <a id="2346" class="Keyword">record</a>
      <a id="2359" class="Symbol">{</a> <a id="2361" href="Categories.Morphism.html#1586" class="Field">isoˡ</a> <a id="2366" class="Symbol">=</a> <a id="2368" href="Categories.Morphism.html#1586" class="Function">isoˡ</a> <a id="2373" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="2378" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2380" href="Categories.Morphism.html#1586" class="Function">isoˡ</a> <a id="2385" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a>
      <a id="2396" class="Symbol">;</a> <a id="2398" href="Categories.Morphism.html#1612" class="Field">isoʳ</a> <a id="2403" class="Symbol">=</a> <a id="2405" href="Categories.Morphism.html#1612" class="Function">isoʳ</a> <a id="2410" href="Categories.Category.Construction.Arrow.html#1862" class="Bound">iso₁</a> <a id="2415" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2417" href="Categories.Morphism.html#1612" class="Function">isoʳ</a> <a id="2422" href="Categories.Category.Construction.Arrow.html#1867" class="Bound">iso₂</a>
      <a id="2433" class="Symbol">}</a>
    <a id="2439" class="Symbol">}</a>
</pre></body></html>
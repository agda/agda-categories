<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Adjoint.Instance.Slice</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Pragma">--without-K</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Keyword">open</a> <a id="42" class="Keyword">import</a> <a id="49" href="Categories.Category.html" class="Module">Categories.Category</a> <a id="69" class="Keyword">using</a> <a id="75" class="Symbol">(</a><a id="76" href="Categories.Category.Core.html#442" class="Record">Category</a><a id="84" class="Symbol">)</a>

<a id="87" class="Keyword">module</a> <a id="94" href="Categories.Adjoint.Instance.Slice.html" class="Module">Categories.Adjoint.Instance.Slice</a> <a id="128" class="Symbol">{</a><a id="129" href="Categories.Adjoint.Instance.Slice.html#129" class="Bound">o</a> <a id="131" href="Categories.Adjoint.Instance.Slice.html#131" class="Bound">ℓ</a> <a id="133" href="Categories.Adjoint.Instance.Slice.html#133" class="Bound">e</a><a id="134" class="Symbol">}</a> <a id="136" class="Symbol">(</a><a id="137" href="Categories.Adjoint.Instance.Slice.html#137" class="Bound">C</a> <a id="139" class="Symbol">:</a> <a id="141" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="150" href="Categories.Adjoint.Instance.Slice.html#129" class="Bound">o</a> <a id="152" href="Categories.Adjoint.Instance.Slice.html#131" class="Bound">ℓ</a> <a id="154" href="Categories.Adjoint.Instance.Slice.html#133" class="Bound">e</a><a id="155" class="Symbol">)</a> <a id="157" class="Keyword">where</a>

<a id="164" class="Keyword">open</a> <a id="169" class="Keyword">import</a> <a id="176" href="Categories.Adjoint.html" class="Module">Categories.Adjoint</a> <a id="195" class="Keyword">using</a> <a id="201" class="Symbol">(</a><a id="202" href="Categories.Adjoint.html#7818" class="Function Operator">_⊣_</a><a id="205" class="Symbol">)</a>
<a id="207" class="Keyword">open</a> <a id="212" class="Keyword">import</a> <a id="219" href="Categories.Category.Slice.html" class="Module">Categories.Category.Slice</a> <a id="245" href="Categories.Adjoint.Instance.Slice.html#137" class="Bound">C</a> <a id="247" class="Keyword">using</a> <a id="253" class="Symbol">(</a><a id="254" href="Categories.Category.Slice.html#450" class="Record">SliceObj</a><a id="262" class="Symbol">;</a> <a id="264" href="Categories.Category.Slice.html#613" class="Record">Slice⇒</a><a id="270" class="Symbol">;</a> <a id="272" href="Categories.Category.Slice.html#683" class="InductiveConstructor">slicearr</a><a id="280" class="Symbol">)</a>
<a id="282" class="Keyword">open</a> <a id="287" class="Keyword">import</a> <a id="294" href="Categories.Functor.Slice.html" class="Module">Categories.Functor.Slice</a> <a id="319" href="Categories.Adjoint.Instance.Slice.html#137" class="Bound">C</a> <a id="321" class="Keyword">using</a> <a id="327" class="Symbol">(</a><a id="328" href="Categories.Functor.Slice.html#1180" class="Function">Forgetful</a><a id="337" class="Symbol">;</a> <a id="339" href="Categories.Functor.Slice.html#3291" class="Function">Free</a><a id="343" class="Symbol">)</a>
<a id="345" class="Keyword">open</a> <a id="350" class="Keyword">import</a> <a id="357" href="Categories.NaturalTransformation.html" class="Module">Categories.NaturalTransformation</a> <a id="390" class="Keyword">using</a> <a id="396" class="Symbol">(</a><a id="397" href="Categories.NaturalTransformation.Core.html#1750" class="Function">ntHelper</a><a id="405" class="Symbol">)</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Categories.Object.Product.html" class="Module">Categories.Object.Product</a> <a id="445" href="Categories.Adjoint.Instance.Slice.html#137" class="Bound">C</a>

<a id="448" class="Keyword">open</a> <a id="453" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="462" href="Categories.Adjoint.Instance.Slice.html#137" class="Bound">C</a>
<a id="464" class="Keyword">open</a> <a id="469" href="Categories.Category.Core.html#2462" class="Module">HomReasoning</a>

<a id="483" class="Keyword">open</a> <a id="488" href="Categories.Category.Slice.html#450" class="Module">SliceObj</a>
<a id="497" class="Keyword">open</a> <a id="502" href="Categories.Category.Slice.html#613" class="Module">Slice⇒</a>

<a id="510" class="Keyword">module</a> <a id="517" href="Categories.Adjoint.Instance.Slice.html#517" class="Module">_</a> <a id="519" class="Symbol">{</a><a id="520" href="Categories.Adjoint.Instance.Slice.html#520" class="Bound">A</a> <a id="522" class="Symbol">:</a> <a id="524" href="Categories.Category.Core.html#559" class="Field">Obj</a><a id="527" class="Symbol">}</a> <a id="529" class="Symbol">(</a><a id="530" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="538" class="Symbol">:</a> <a id="540" class="Symbol">∀</a> <a id="542" class="Symbol">{</a><a id="543" href="Categories.Adjoint.Instance.Slice.html#543" class="Bound">X</a><a id="544" class="Symbol">}</a> <a id="546" class="Symbol">→</a> <a id="548" href="Categories.Object.Product.Core.html#435" class="Record">Product</a> <a id="556" href="Categories.Adjoint.Instance.Slice.html#520" class="Bound">A</a> <a id="558" href="Categories.Adjoint.Instance.Slice.html#543" class="Bound">X</a><a id="559" class="Symbol">)</a> <a id="561" class="Keyword">where</a>

  <a id="570" class="Keyword">private</a>
    <a id="582" class="Keyword">module</a> <a id="589" href="Categories.Adjoint.Instance.Slice.html#589" class="Module">product</a> <a id="597" class="Symbol">{</a><a id="598" href="Categories.Adjoint.Instance.Slice.html#598" class="Bound">X</a><a id="599" class="Symbol">}</a> <a id="601" class="Symbol">=</a> <a id="603" href="Categories.Object.Product.Core.html#435" class="Module">Product</a> <a id="611" class="Symbol">(</a><a id="612" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="620" class="Symbol">{</a><a id="621" href="Categories.Adjoint.Instance.Slice.html#598" class="Bound">X</a><a id="622" class="Symbol">})</a>
    <a id="629" class="Keyword">open</a> <a id="634" href="Categories.Adjoint.Instance.Slice.html#589" class="Module">product</a>

  <a id="645" href="Categories.Adjoint.Instance.Slice.html#645" class="Function">Forgetful⊣Free</a> <a id="660" class="Symbol">:</a> <a id="662" href="Categories.Functor.Slice.html#1180" class="Function">Forgetful</a> <a id="672" href="Categories.Adjoint.html#7818" class="Function Operator">⊣</a> <a id="674" href="Categories.Functor.Slice.html#3291" class="Function">Free</a> <a id="679" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a>
  <a id="689" href="Categories.Adjoint.Instance.Slice.html#645" class="Function">Forgetful⊣Free</a> <a id="704" class="Symbol">=</a> <a id="706" class="Keyword">record</a>
    <a id="717" class="Symbol">{</a> <a id="719" href="Categories.Adjoint.html#1473" class="Field">unit</a> <a id="724" class="Symbol">=</a> <a id="726" href="Categories.NaturalTransformation.Core.html#1750" class="Function">ntHelper</a> <a id="735" class="Keyword">record</a>
      <a id="748" class="Symbol">{</a> <a id="750" href="Categories.NaturalTransformation.Core.html#1637" class="Field">η</a> <a id="752" class="Symbol">=</a> <a id="754" class="Symbol">λ</a> <a id="756" href="Categories.Adjoint.Instance.Slice.html#756" class="Bound">_</a> <a id="758" class="Symbol">→</a> <a id="760" href="Categories.Category.Slice.html#683" class="InductiveConstructor">slicearr</a> <a id="769" href="Categories.Object.Product.Core.html#603" class="Function">project₁</a>
      <a id="784" class="Symbol">;</a> <a id="786" href="Categories.NaturalTransformation.Core.html#1681" class="Field">commute</a> <a id="794" class="Symbol">=</a> <a id="796" class="Symbol">λ</a> <a id="798" class="Symbol">{</a><a id="799" href="Categories.Adjoint.Instance.Slice.html#799" class="Bound">X</a><a id="800" class="Symbol">}</a> <a id="802" class="Symbol">{</a><a id="803" href="Categories.Adjoint.Instance.Slice.html#803" class="Bound">Y</a><a id="804" class="Symbol">}</a> <a id="806" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="808" class="Symbol">→</a> <a id="810" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
        <a id="824" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="826" href="Categories.Category.Slice.html#538" class="Field">arr</a> <a id="830" href="Categories.Adjoint.Instance.Slice.html#803" class="Bound">Y</a> <a id="832" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="834" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="837" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a> <a id="839" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="841" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="843" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a>                            <a id="872" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="875" href="Categories.Object.Product.Core.html#1014" class="Function">∘-distribʳ-⟨⟩</a> <a id="889" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="899" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="901" href="Categories.Category.Slice.html#538" class="Field">arr</a> <a id="905" href="Categories.Adjoint.Instance.Slice.html#803" class="Bound">Y</a> <a id="907" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="909" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="911" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="913" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="915" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="918" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="920" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="922" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="924" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a>                      <a id="947" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="950" href="Categories.Object.Product.Core.html#854" class="Function">⟨⟩-cong₂</a> <a id="959" class="Symbol">(</a><a id="960" href="Categories.Category.Slice.html#786" class="Field">△</a> <a id="962" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a><a id="963" class="Symbol">)</a> <a id="965" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="975" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="985" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="987" href="Categories.Category.Slice.html#538" class="Field">arr</a> <a id="991" href="Categories.Adjoint.Instance.Slice.html#799" class="Bound">X</a> <a id="993" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="995" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="997" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="999" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a>                                 <a id="1033" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="1037" href="Categories.Object.Product.Core.html#854" class="Function">⟨⟩-cong₂</a> <a id="1046" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1056" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1066" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
        <a id="1076" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="1078" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1081" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1083" href="Categories.Category.Slice.html#538" class="Field">arr</a> <a id="1087" href="Categories.Adjoint.Instance.Slice.html#799" class="Bound">X</a> <a id="1089" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="1091" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="1093" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="1095" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1097" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1100" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a>                       <a id="1124" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="1128" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">[</a> <a id="1130" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1138" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">⇒</a> <a id="1140" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1148" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">]×∘⟨⟩</a> <a id="1154" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
        <a id="1164" href="Categories.Object.Product.Morphisms.html#838" class="Function Operator">[</a> <a id="1166" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1174" href="Categories.Object.Product.Morphisms.html#838" class="Function Operator">⇒</a> <a id="1176" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1184" href="Categories.Object.Product.Morphisms.html#838" class="Function Operator">]</a> <a id="1186" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1189" href="Categories.Object.Product.Morphisms.html#838" class="Function Operator">×</a> <a id="1191" href="Categories.Category.Slice.html#767" class="Field">h</a> <a id="1193" href="Categories.Adjoint.Instance.Slice.html#806" class="Bound">f</a> <a id="1195" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1197" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="1199" href="Categories.Category.Slice.html#538" class="Field">arr</a> <a id="1203" href="Categories.Adjoint.Instance.Slice.html#799" class="Bound">X</a> <a id="1205" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="1207" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1210" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a> <a id="1212" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
      <a id="1220" class="Symbol">}</a>
    <a id="1226" class="Symbol">;</a> <a id="1228" href="Categories.Adjoint.html#1521" class="Field">counit</a> <a id="1235" class="Symbol">=</a> <a id="1237" href="Categories.NaturalTransformation.Core.html#1750" class="Function">ntHelper</a> <a id="1246" class="Keyword">record</a>
      <a id="1259" class="Symbol">{</a> <a id="1261" href="Categories.NaturalTransformation.Core.html#1637" class="Field">η</a> <a id="1263" class="Symbol">=</a> <a id="1265" class="Symbol">λ</a> <a id="1267" href="Categories.Adjoint.Instance.Slice.html#1267" class="Bound">_</a> <a id="1269" class="Symbol">→</a> <a id="1271" href="Categories.Object.Product.Core.html#546" class="Function">π₂</a>
      <a id="1280" class="Symbol">;</a> <a id="1282" href="Categories.NaturalTransformation.Core.html#1681" class="Field">commute</a> <a id="1290" class="Symbol">=</a> <a id="1292" class="Symbol">λ</a> <a id="1294" href="Categories.Adjoint.Instance.Slice.html#1294" class="Bound">_</a> <a id="1296" class="Symbol">→</a> <a id="1298" href="Categories.Object.Product.Core.html#637" class="Function">project₂</a>
      <a id="1313" class="Symbol">}</a>
    <a id="1319" class="Symbol">;</a> <a id="1321" href="Categories.Adjoint.html#1669" class="Field">zig</a> <a id="1325" class="Symbol">=</a> <a id="1327" href="Categories.Object.Product.Core.html#637" class="Function">project₂</a>
    <a id="1340" class="Symbol">;</a> <a id="1342" href="Categories.Adjoint.html#1742" class="Field">zag</a> <a id="1346" class="Symbol">=</a> <a id="1348" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
      <a id="1360" href="Categories.Object.Product.Morphisms.html#4398" class="Function Operator">[</a> <a id="1362" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1370" href="Categories.Object.Product.Morphisms.html#4398" class="Function Operator">⇒</a> <a id="1372" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1380" href="Categories.Object.Product.Morphisms.html#4398" class="Function Operator">]id×</a> <a id="1385" href="Categories.Object.Product.Core.html#546" class="Function">π₂</a> <a id="1388" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1390" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="1392" href="Categories.Object.Product.Core.html#526" class="Function">π₁</a> <a id="1395" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="1397" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1400" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a> <a id="1402" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1405" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">[</a> <a id="1407" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1415" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">⇒</a> <a id="1417" href="Categories.Adjoint.Instance.Slice.html#530" class="Bound">product</a> <a id="1425" href="Categories.Object.Product.Morphisms.html#1982" class="Function Operator">]×∘⟨⟩</a> <a id="1431" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="1439" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="1441" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1444" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1446" href="Categories.Object.Product.Core.html#526" class="Function">π₁</a> <a id="1449" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="1451" href="Categories.Object.Product.Core.html#546" class="Function">π₂</a> <a id="1454" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1456" href="Categories.Category.Core.html#630" class="Field">id</a> <a id="1459" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a>                     <a id="1481" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1484" href="Categories.Object.Product.Core.html#854" class="Function">⟨⟩-cong₂</a> <a id="1493" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1503" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1513" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="1521" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟨</a> <a id="1523" href="Categories.Object.Product.Core.html#526" class="Function">π₁</a> <a id="1526" href="Categories.Object.Product.Core.html#566" class="Function Operator">,</a> <a id="1528" href="Categories.Object.Product.Core.html#546" class="Function">π₂</a> <a id="1531" href="Categories.Object.Product.Core.html#566" class="Function Operator">⟩</a>                               <a id="1563" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1566" href="Categories.Object.Product.Core.html#797" class="Function">η</a> <a id="1568" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="1576" href="Categories.Category.Core.html#630" class="Field">id</a>                                        <a id="1618" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
    <a id="1624" class="Symbol">}</a>
</pre></body></html>
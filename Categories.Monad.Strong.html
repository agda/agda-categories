<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Monad.Strong</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Comment">-- Define Strong Monad; use the Wikipedia definition</a>
<a id="90" class="Comment">-- https://en.wikipedia.org/wiki/Strong_monad</a>
<a id="136" class="Comment">-- At the nLab, https://ncatlab.org/nlab/show/strong+monad</a>
<a id="195" class="Comment">-- there are two further definitions; the 2-categorical version is too complicated</a>
<a id="278" class="Comment">-- and the Moggi definition is a special case of the one here</a>

<a id="341" class="Keyword">module</a> <a id="348" href="Categories.Monad.Strong.html" class="Module">Categories.Monad.Strong</a>  <a id="373" class="Keyword">where</a>

<a id="380" class="Keyword">open</a> <a id="385" class="Keyword">import</a> <a id="392" href="Level.html" class="Module">Level</a>
<a id="398" class="Keyword">open</a> <a id="403" class="Keyword">import</a> <a id="410" href="Data.Product.html" class="Module">Data.Product</a> <a id="423" class="Keyword">using</a> <a id="429" class="Symbol">(</a><a id="430" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="433" class="Symbol">)</a>

<a id="436" class="Keyword">open</a> <a id="441" class="Keyword">import</a> <a id="448" href="Categories.Category.html" class="Module">Categories.Category</a>
<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Categories.Functor.html" class="Module">Categories.Functor</a> <a id="499" class="Keyword">renaming</a> <a id="508" class="Symbol">(</a><a id="509" href="Categories.Functor.html#349" class="Function">id</a> <a id="512" class="Symbol">to</a> <a id="515" class="Function">idF</a><a id="518" class="Symbol">)</a>
<a id="520" class="Keyword">open</a> <a id="525" class="Keyword">import</a> <a id="532" href="Categories.Category.Monoidal.html" class="Module">Categories.Category.Monoidal</a>
<a id="561" class="Keyword">open</a> <a id="566" class="Keyword">import</a> <a id="573" href="Categories.Category.Product.html" class="Module">Categories.Category.Product</a>
<a id="601" class="Keyword">open</a> <a id="606" class="Keyword">import</a> <a id="613" href="Categories.NaturalTransformation.html" class="Module">Categories.NaturalTransformation</a> <a id="646" class="Keyword">hiding</a> <a id="653" class="Symbol">(</a><a id="654" href="Categories.NaturalTransformation.Core.html#2132" class="Function">id</a><a id="656" class="Symbol">)</a>
<a id="658" class="Keyword">open</a> <a id="663" class="Keyword">import</a> <a id="670" href="Categories.Monad.html" class="Module">Categories.Monad</a> <a id="687" class="Keyword">hiding</a> <a id="694" class="Symbol">(</a><a id="695" href="Categories.Monad.html#1093" class="Function">id</a><a id="697" class="Symbol">)</a>

<a id="700" class="Keyword">private</a>
  <a id="710" class="Keyword">variable</a>
    <a id="723" href="Categories.Monad.Strong.html#723" class="Generalizable">o</a> <a id="725" href="Categories.Monad.Strong.html#725" class="Generalizable">ℓ</a> <a id="727" href="Categories.Monad.Strong.html#727" class="Generalizable">e</a> <a id="729" class="Symbol">:</a> <a id="731" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="738" class="Comment">-- (left) strength on a monad</a>

<a id="769" class="Keyword">record</a> <a id="Strength"></a><a id="776" href="Categories.Monad.Strong.html#776" class="Record">Strength</a> <a id="785" class="Symbol">{</a><a id="786" href="Categories.Monad.Strong.html#786" class="Bound">C</a> <a id="788" class="Symbol">:</a> <a id="790" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="799" href="Categories.Monad.Strong.html#723" class="Generalizable">o</a> <a id="801" href="Categories.Monad.Strong.html#725" class="Generalizable">ℓ</a> <a id="803" href="Categories.Monad.Strong.html#727" class="Generalizable">e</a><a id="804" class="Symbol">}</a> <a id="806" class="Symbol">(</a><a id="807" href="Categories.Monad.Strong.html#807" class="Bound">V</a> <a id="809" class="Symbol">:</a> <a id="811" href="Categories.Category.Monoidal.Core.html#1287" class="Record">Monoidal</a> <a id="820" href="Categories.Monad.Strong.html#786" class="Bound">C</a><a id="821" class="Symbol">)</a> <a id="823" class="Symbol">(</a><a id="824" href="Categories.Monad.Strong.html#824" class="Bound">M</a> <a id="826" class="Symbol">:</a> <a id="828" href="Categories.Monad.html#454" class="Record">Monad</a> <a id="834" href="Categories.Monad.Strong.html#786" class="Bound">C</a><a id="835" class="Symbol">)</a> <a id="837" class="Symbol">:</a> <a id="839" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="843" class="Symbol">(</a><a id="844" href="Categories.Monad.Strong.html#799" class="Bound">o</a> <a id="846" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="848" href="Categories.Monad.Strong.html#801" class="Bound">ℓ</a> <a id="850" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="852" href="Categories.Monad.Strong.html#803" class="Bound">e</a><a id="853" class="Symbol">)</a> <a id="855" class="Keyword">where</a>
  <a id="863" class="Keyword">open</a> <a id="868" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="877" href="Categories.Monad.Strong.html#786" class="Bound">C</a>
  <a id="881" class="Keyword">open</a> <a id="886" href="Categories.Category.Monoidal.Core.html#1287" class="Module">Monoidal</a> <a id="895" href="Categories.Monad.Strong.html#807" class="Bound">V</a>
  <a id="899" class="Keyword">private</a>
    <a id="911" class="Keyword">module</a> <a id="Strength.M"></a><a id="918" href="Categories.Monad.Strong.html#918" class="Module">M</a> <a id="920" class="Symbol">=</a> <a id="922" href="Categories.Monad.html#454" class="Module">Monad</a> <a id="928" href="Categories.Monad.Strong.html#824" class="Bound">M</a>
  <a id="932" class="Keyword">open</a> <a id="937" href="Categories.Monad.Strong.html#918" class="Module">M</a> <a id="939" class="Keyword">using</a> <a id="945" class="Symbol">(</a><a id="946" href="Categories.Monad.html#525" class="Function">F</a><a id="947" class="Symbol">)</a>
  <a id="951" class="Keyword">open</a> <a id="956" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="978" href="Categories.Monad.html#547" class="Function">M.η</a> <a id="982" class="Keyword">using</a> <a id="988" class="Symbol">(</a><a id="989" href="Categories.NaturalTransformation.Core.html#783" class="Field">η</a><a id="990" class="Symbol">)</a>
  <a id="994" class="Keyword">open</a> <a id="999" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="1021" href="Categories.Monad.html#583" class="Function">M.μ</a> <a id="1025" class="Keyword">renaming</a> <a id="1034" class="Symbol">(</a><a id="1035" href="Categories.NaturalTransformation.Core.html#783" class="Field">η</a> <a id="1037" class="Symbol">to</a> <a id="1040" class="Field">μ</a><a id="1041" class="Symbol">)</a>
  <a id="1045" class="Keyword">open</a> <a id="1050" href="Categories.Functor.Core.html#248" class="Module">Functor</a> <a id="1058" href="Categories.Monad.html#525" class="Function">F</a>
  <a id="1062" class="Keyword">field</a>
    <a id="Strength.strengthen"></a><a id="1072" href="Categories.Monad.Strong.html#1072" class="Field">strengthen</a> <a id="1083" class="Symbol">:</a> <a id="1085" href="Categories.NaturalTransformation.Core.html#466" class="Record">NaturalTransformation</a> <a id="1107" class="Symbol">(</a><a id="1108" href="Categories.Category.Monoidal.Core.html#1355" class="Function">⊗</a> <a id="1110" href="Categories.Functor.html#747" class="Function Operator">∘F</a> <a id="1113" class="Symbol">(</a><a id="1114" href="Categories.Monad.Strong.html#515" class="Function">idF</a> <a id="1118" href="Categories.Category.Product.html#1962" class="Function Operator">⁂</a> <a id="1120" href="Categories.Monad.html#525" class="Function">F</a><a id="1121" class="Symbol">))</a> <a id="1124" class="Symbol">(</a><a id="1125" href="Categories.Monad.html#525" class="Function">F</a> <a id="1127" href="Categories.Functor.html#747" class="Function Operator">∘F</a> <a id="1130" href="Categories.Category.Monoidal.Core.html#1355" class="Function">⊗</a><a id="1131" class="Symbol">)</a>

  <a id="1136" class="Keyword">module</a> <a id="Strength.strengthen"></a><a id="1143" href="Categories.Monad.Strong.html#1143" class="Module">strengthen</a> <a id="1154" class="Symbol">=</a> <a id="1156" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="1178" href="Categories.Monad.Strong.html#1072" class="Field">strengthen</a>
  <a id="1191" class="Keyword">private</a>
    <a id="1203" class="Keyword">module</a> <a id="Strength.t"></a><a id="1210" href="Categories.Monad.Strong.html#1210" class="Module">t</a> <a id="1212" class="Symbol">=</a> <a id="1214" href="Categories.Monad.Strong.html#1143" class="Module">strengthen</a>

  <a id="1228" class="Keyword">field</a>
    <a id="1238" class="Comment">-- strengthening with 1 is irrelevant</a>
    <a id="Strength.identityˡ"></a><a id="1280" href="Categories.Monad.Strong.html#1280" class="Field">identityˡ</a> <a id="1290" class="Symbol">:</a> <a id="1292" class="Symbol">{</a><a id="1293" href="Categories.Monad.Strong.html#1293" class="Bound">A</a> <a id="1295" class="Symbol">:</a> <a id="1297" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="1300" class="Symbol">}</a> <a id="1302" class="Symbol">→</a> <a id="1304" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="1307" class="Symbol">(</a><a id="1308" href="Categories.Morphism.html#2005" class="Function">unitorˡ.from</a><a id="1320" class="Symbol">)</a> <a id="1322" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1324" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1328" class="Symbol">(</a><a id="1329" href="Categories.Category.Monoidal.Core.html#1380" class="Function">unit</a> <a id="1334" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1336" href="Categories.Monad.Strong.html#1293" class="Bound">A</a><a id="1337" class="Symbol">)</a> <a id="1339" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1341" href="Categories.Morphism.html#2005" class="Function">unitorˡ.from</a>
    <a id="1358" class="Comment">-- commutes with unit (of monad)</a>
    <a id="Strength.η-comm"></a><a id="1395" href="Categories.Monad.Strong.html#1395" class="Field">η-comm</a> <a id="1402" class="Symbol">:</a> <a id="1404" class="Symbol">{</a><a id="1405" href="Categories.Monad.Strong.html#1405" class="Bound">A</a> <a id="1407" href="Categories.Monad.Strong.html#1407" class="Bound">B</a> <a id="1409" class="Symbol">:</a> <a id="1411" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="1414" class="Symbol">}</a> <a id="1416" class="Symbol">→</a> <a id="1418" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1422" class="Symbol">(</a><a id="1423" href="Categories.Monad.Strong.html#1405" class="Bound">A</a> <a id="1425" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1427" href="Categories.Monad.Strong.html#1407" class="Bound">B</a><a id="1428" class="Symbol">)</a> <a id="1430" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1432" class="Symbol">(</a><a id="1433" href="Categories.Category.Core.html#630" class="Function">id</a> <a id="1436" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1439" href="Categories.NaturalTransformation.Core.html#783" class="Function">η</a> <a id="1441" href="Categories.Monad.Strong.html#1407" class="Bound">B</a><a id="1442" class="Symbol">)</a> <a id="1444" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1446" href="Categories.NaturalTransformation.Core.html#783" class="Function">η</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Categories.Monad.Strong.html#1405" class="Bound">A</a> <a id="1451" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="1454" href="Categories.Monad.Strong.html#1407" class="Bound">B</a><a id="1455" class="Symbol">)</a>
    <a id="1461" class="Comment">-- strength commutes with multiplication</a>
    <a id="Strength.μ-η-comm"></a><a id="1506" href="Categories.Monad.Strong.html#1506" class="Field">μ-η-comm</a> <a id="1515" class="Symbol">:</a> <a id="1517" class="Symbol">{</a><a id="1518" href="Categories.Monad.Strong.html#1518" class="Bound">A</a> <a id="1520" href="Categories.Monad.Strong.html#1520" class="Bound">B</a> <a id="1522" class="Symbol">:</a> <a id="1524" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="1527" class="Symbol">}</a> <a id="1529" class="Symbol">→</a> <a id="1531" href="Categories.Monad.Strong.html#1040" class="Function">μ</a> <a id="1533" class="Symbol">(</a><a id="1534" href="Categories.Monad.Strong.html#1518" class="Bound">A</a> <a id="1536" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="1539" href="Categories.Monad.Strong.html#1520" class="Bound">B</a><a id="1540" class="Symbol">)</a> <a id="1542" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1544" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="1547" class="Symbol">(</a><a id="1548" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1552" class="Symbol">(</a><a id="1553" href="Categories.Monad.Strong.html#1518" class="Bound">A</a> <a id="1555" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1557" href="Categories.Monad.Strong.html#1520" class="Bound">B</a><a id="1558" class="Symbol">))</a> <a id="1561" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1563" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1567" class="Symbol">(</a><a id="1568" href="Categories.Monad.Strong.html#1518" class="Bound">A</a> <a id="1570" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1572" href="Categories.Functor.Core.html#432" class="Function">F₀</a> <a id="1575" href="Categories.Monad.Strong.html#1520" class="Bound">B</a><a id="1576" class="Symbol">)</a>
      <a id="1584" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1586" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1590" class="Symbol">(</a><a id="1591" href="Categories.Monad.Strong.html#1518" class="Bound">A</a> <a id="1593" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1595" href="Categories.Monad.Strong.html#1520" class="Bound">B</a><a id="1596" class="Symbol">)</a> <a id="1598" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1600" class="Symbol">(</a><a id="1601" href="Categories.Category.Core.html#630" class="Function">id</a> <a id="1604" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1607" href="Categories.Monad.Strong.html#1040" class="Function">μ</a> <a id="1609" href="Categories.Monad.Strong.html#1520" class="Bound">B</a><a id="1610" class="Symbol">)</a>
    <a id="1616" class="Comment">-- consecutive applications of strength commute (i.e. strength is associative)</a>
    <a id="Strength.strength-assoc"></a><a id="1699" href="Categories.Monad.Strong.html#1699" class="Field">strength-assoc</a> <a id="1714" class="Symbol">:</a>  <a id="1717" class="Symbol">{</a><a id="1718" href="Categories.Monad.Strong.html#1718" class="Bound">A</a> <a id="1720" href="Categories.Monad.Strong.html#1720" class="Bound">B</a> <a id="1722" href="Categories.Monad.Strong.html#1722" class="Bound">C</a> <a id="1724" class="Symbol">:</a> <a id="1726" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="1729" class="Symbol">}</a> <a id="1731" class="Symbol">→</a> <a id="1733" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="1736" href="Categories.Morphism.html#2005" class="Function">associator.from</a> <a id="1752" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1754" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1758" class="Symbol">(</a><a id="1759" href="Categories.Monad.Strong.html#1718" class="Bound">A</a> <a id="1761" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="1764" href="Categories.Monad.Strong.html#1720" class="Bound">B</a> <a id="1766" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1768" href="Categories.Monad.Strong.html#1722" class="Bound">C</a><a id="1769" class="Symbol">)</a>
      <a id="1777" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1779" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1783" class="Symbol">(</a><a id="1784" href="Categories.Monad.Strong.html#1718" class="Bound">A</a> <a id="1786" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1788" href="Categories.Monad.Strong.html#1720" class="Bound">B</a> <a id="1790" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="1793" href="Categories.Monad.Strong.html#1722" class="Bound">C</a><a id="1794" class="Symbol">)</a> <a id="1796" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1798" class="Symbol">(</a><a id="1799" href="Categories.Category.Core.html#630" class="Function">id</a> <a id="1802" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="1805" href="Categories.NaturalTransformation.Core.html#783" class="Function">t.η</a> <a id="1809" class="Symbol">(</a><a id="1810" href="Categories.Monad.Strong.html#1720" class="Bound">B</a> <a id="1812" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1814" href="Categories.Monad.Strong.html#1722" class="Bound">C</a><a id="1815" class="Symbol">))</a> <a id="1818" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1820" href="Categories.Morphism.html#2005" class="Function">associator.from</a>

<a id="1837" class="Keyword">record</a> <a id="StrongMonad"></a><a id="1844" href="Categories.Monad.Strong.html#1844" class="Record">StrongMonad</a> <a id="1856" class="Symbol">{</a><a id="1857" href="Categories.Monad.Strong.html#1857" class="Bound">C</a> <a id="1859" class="Symbol">:</a> <a id="1861" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="1870" href="Categories.Monad.Strong.html#723" class="Generalizable">o</a> <a id="1872" href="Categories.Monad.Strong.html#725" class="Generalizable">ℓ</a> <a id="1874" href="Categories.Monad.Strong.html#727" class="Generalizable">e</a><a id="1875" class="Symbol">}</a> <a id="1877" class="Symbol">(</a><a id="1878" href="Categories.Monad.Strong.html#1878" class="Bound">V</a> <a id="1880" class="Symbol">:</a> <a id="1882" href="Categories.Category.Monoidal.Core.html#1287" class="Record">Monoidal</a> <a id="1891" href="Categories.Monad.Strong.html#1857" class="Bound">C</a><a id="1892" class="Symbol">)</a> <a id="1894" class="Symbol">:</a> <a id="1896" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1900" class="Symbol">(</a><a id="1901" href="Categories.Monad.Strong.html#1870" class="Bound">o</a> <a id="1903" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1905" href="Categories.Monad.Strong.html#1872" class="Bound">ℓ</a> <a id="1907" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1909" href="Categories.Monad.Strong.html#1874" class="Bound">e</a><a id="1910" class="Symbol">)</a> <a id="1912" class="Keyword">where</a>
  <a id="1920" class="Keyword">field</a>
    <a id="StrongMonad.M"></a><a id="1930" href="Categories.Monad.Strong.html#1930" class="Field">M</a>        <a id="1939" class="Symbol">:</a> <a id="1941" href="Categories.Monad.html#454" class="Record">Monad</a> <a id="1947" href="Categories.Monad.Strong.html#1857" class="Bound">C</a>
    <a id="StrongMonad.strength"></a><a id="1953" href="Categories.Monad.Strong.html#1953" class="Field">strength</a> <a id="1962" class="Symbol">:</a> <a id="1964" href="Categories.Monad.Strong.html#776" class="Record">Strength</a> <a id="1973" href="Categories.Monad.Strong.html#1878" class="Bound">V</a> <a id="1975" href="Categories.Monad.Strong.html#1930" class="Field">M</a>

  <a id="1980" class="Keyword">module</a> <a id="StrongMonad.M"></a><a id="1987" href="Categories.Monad.Strong.html#1987" class="Module">M</a> <a id="1989" class="Symbol">=</a> <a id="1991" href="Categories.Monad.html#454" class="Module">Monad</a> <a id="1997" href="Categories.Monad.Strong.html#1930" class="Field">M</a>
  <a id="2001" class="Keyword">open</a> <a id="2006" href="Categories.Monad.Strong.html#776" class="Module">Strength</a> <a id="2015" href="Categories.Monad.Strong.html#1953" class="Field">strength</a> <a id="2024" class="Keyword">public</a>

<a id="2032" class="Comment">-- right strength</a>

<a id="2051" class="Keyword">record</a> <a id="RightStrength"></a><a id="2058" href="Categories.Monad.Strong.html#2058" class="Record">RightStrength</a> <a id="2072" class="Symbol">{</a><a id="2073" href="Categories.Monad.Strong.html#2073" class="Bound">C</a> <a id="2075" class="Symbol">:</a> <a id="2077" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="2086" href="Categories.Monad.Strong.html#723" class="Generalizable">o</a> <a id="2088" href="Categories.Monad.Strong.html#725" class="Generalizable">ℓ</a> <a id="2090" href="Categories.Monad.Strong.html#727" class="Generalizable">e</a><a id="2091" class="Symbol">}</a> <a id="2093" class="Symbol">(</a><a id="2094" href="Categories.Monad.Strong.html#2094" class="Bound">V</a> <a id="2096" class="Symbol">:</a> <a id="2098" href="Categories.Category.Monoidal.Core.html#1287" class="Record">Monoidal</a> <a id="2107" href="Categories.Monad.Strong.html#2073" class="Bound">C</a><a id="2108" class="Symbol">)</a> <a id="2110" class="Symbol">(</a><a id="2111" href="Categories.Monad.Strong.html#2111" class="Bound">M</a> <a id="2113" class="Symbol">:</a> <a id="2115" href="Categories.Monad.html#454" class="Record">Monad</a> <a id="2121" href="Categories.Monad.Strong.html#2073" class="Bound">C</a><a id="2122" class="Symbol">)</a> <a id="2124" class="Symbol">:</a> <a id="2126" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2130" class="Symbol">(</a><a id="2131" href="Categories.Monad.Strong.html#2086" class="Bound">o</a> <a id="2133" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2135" href="Categories.Monad.Strong.html#2088" class="Bound">ℓ</a> <a id="2137" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2139" href="Categories.Monad.Strong.html#2090" class="Bound">e</a><a id="2140" class="Symbol">)</a> <a id="2142" class="Keyword">where</a>
  <a id="2150" class="Keyword">open</a> <a id="2155" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="2164" href="Categories.Monad.Strong.html#2073" class="Bound">C</a>
  <a id="2168" class="Keyword">open</a> <a id="2173" href="Categories.Category.Monoidal.Core.html#1287" class="Module">Monoidal</a> <a id="2182" href="Categories.Monad.Strong.html#2094" class="Bound">V</a>
  <a id="2186" class="Keyword">private</a>
    <a id="2198" class="Keyword">module</a> <a id="RightStrength.M"></a><a id="2205" href="Categories.Monad.Strong.html#2205" class="Module">M</a> <a id="2207" class="Symbol">=</a> <a id="2209" href="Categories.Monad.html#454" class="Module">Monad</a> <a id="2215" href="Categories.Monad.Strong.html#2111" class="Bound">M</a>
  <a id="2219" class="Keyword">open</a> <a id="2224" href="Categories.Monad.Strong.html#2205" class="Module">M</a> <a id="2226" class="Keyword">using</a> <a id="2232" class="Symbol">(</a><a id="2233" href="Categories.Monad.html#525" class="Function">F</a><a id="2234" class="Symbol">)</a>
  <a id="2238" class="Keyword">open</a> <a id="2243" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="2265" href="Categories.Monad.html#547" class="Function">M.η</a> <a id="2269" class="Keyword">using</a> <a id="2275" class="Symbol">(</a><a id="2276" href="Categories.NaturalTransformation.Core.html#783" class="Field">η</a><a id="2277" class="Symbol">)</a>
  <a id="2281" class="Keyword">open</a> <a id="2286" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="2308" href="Categories.Monad.html#583" class="Function">M.μ</a> <a id="2312" class="Keyword">renaming</a> <a id="2321" class="Symbol">(</a><a id="2322" href="Categories.NaturalTransformation.Core.html#783" class="Field">η</a> <a id="2324" class="Symbol">to</a> <a id="2327" class="Field">μ</a><a id="2328" class="Symbol">)</a>
  <a id="2332" class="Keyword">open</a> <a id="2337" href="Categories.Functor.Core.html#248" class="Module">Functor</a> <a id="2345" href="Categories.Monad.html#525" class="Function">F</a>
  <a id="2349" class="Keyword">field</a>
    <a id="RightStrength.strengthen"></a><a id="2359" href="Categories.Monad.Strong.html#2359" class="Field">strengthen</a> <a id="2370" class="Symbol">:</a> <a id="2372" href="Categories.NaturalTransformation.Core.html#466" class="Record">NaturalTransformation</a> <a id="2394" class="Symbol">(</a><a id="2395" href="Categories.Category.Monoidal.Core.html#1355" class="Function">⊗</a> <a id="2397" href="Categories.Functor.html#747" class="Function Operator">∘F</a> <a id="2400" class="Symbol">(</a><a id="2401" href="Categories.Monad.html#525" class="Function">F</a> <a id="2403" href="Categories.Category.Product.html#1962" class="Function Operator">⁂</a> <a id="2405" href="Categories.Monad.Strong.html#515" class="Function">idF</a><a id="2408" class="Symbol">))</a> <a id="2411" class="Symbol">(</a><a id="2412" href="Categories.Monad.html#525" class="Function">F</a> <a id="2414" href="Categories.Functor.html#747" class="Function Operator">∘F</a> <a id="2417" href="Categories.Category.Monoidal.Core.html#1355" class="Function">⊗</a><a id="2418" class="Symbol">)</a>

  <a id="2423" class="Keyword">module</a> <a id="RightStrength.strengthen"></a><a id="2430" href="Categories.Monad.Strong.html#2430" class="Module">strengthen</a> <a id="2441" class="Symbol">=</a> <a id="2443" href="Categories.NaturalTransformation.Core.html#466" class="Module">NaturalTransformation</a> <a id="2465" href="Categories.Monad.Strong.html#2359" class="Field">strengthen</a>
  <a id="2478" class="Keyword">private</a>
    <a id="2490" class="Keyword">module</a> <a id="RightStrength.u"></a><a id="2497" href="Categories.Monad.Strong.html#2497" class="Module">u</a> <a id="2499" class="Symbol">=</a> <a id="2501" href="Categories.Monad.Strong.html#2430" class="Module">strengthen</a>

  <a id="2515" class="Keyword">field</a>
    <a id="2525" class="Comment">-- strengthening with 1 is irrelevant</a>
    <a id="RightStrength.identityˡ"></a><a id="2567" href="Categories.Monad.Strong.html#2567" class="Field">identityˡ</a> <a id="2577" class="Symbol">:</a> <a id="2579" class="Symbol">{</a><a id="2580" href="Categories.Monad.Strong.html#2580" class="Bound">A</a> <a id="2582" class="Symbol">:</a> <a id="2584" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="2587" class="Symbol">}</a> <a id="2589" class="Symbol">→</a> <a id="2591" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="2594" class="Symbol">(</a><a id="2595" href="Categories.Morphism.html#2005" class="Function">unitorʳ.from</a><a id="2607" class="Symbol">)</a> <a id="2609" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2611" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="2615" class="Symbol">(</a><a id="2616" href="Categories.Monad.Strong.html#2580" class="Bound">A</a> <a id="2618" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2620" href="Categories.Category.Monoidal.Core.html#1380" class="Function">unit</a><a id="2624" class="Symbol">)</a> <a id="2626" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2628" href="Categories.Morphism.html#2005" class="Function">unitorʳ.from</a>
    <a id="2645" class="Comment">-- commutes with unit (of monad)</a>
    <a id="RightStrength.η-comm"></a><a id="2682" href="Categories.Monad.Strong.html#2682" class="Field">η-comm</a> <a id="2689" class="Symbol">:</a> <a id="2691" class="Symbol">{</a><a id="2692" href="Categories.Monad.Strong.html#2692" class="Bound">A</a> <a id="2694" href="Categories.Monad.Strong.html#2694" class="Bound">B</a> <a id="2696" class="Symbol">:</a> <a id="2698" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="2701" class="Symbol">}</a> <a id="2703" class="Symbol">→</a> <a id="2705" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="2709" class="Symbol">(</a><a id="2710" href="Categories.Monad.Strong.html#2692" class="Bound">A</a> <a id="2712" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2714" href="Categories.Monad.Strong.html#2694" class="Bound">B</a><a id="2715" class="Symbol">)</a> <a id="2717" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2719" class="Symbol">(</a><a id="2720" href="Categories.NaturalTransformation.Core.html#783" class="Function">η</a> <a id="2722" href="Categories.Monad.Strong.html#2692" class="Bound">A</a> <a id="2724" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="2727" href="Categories.Category.Core.html#630" class="Function">id</a><a id="2729" class="Symbol">)</a> <a id="2731" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2733" href="Categories.NaturalTransformation.Core.html#783" class="Function">η</a> <a id="2735" class="Symbol">(</a><a id="2736" href="Categories.Monad.Strong.html#2692" class="Bound">A</a> <a id="2738" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="2741" href="Categories.Monad.Strong.html#2694" class="Bound">B</a><a id="2742" class="Symbol">)</a>
    <a id="2748" class="Comment">-- strength commutes with multiplication</a>
    <a id="RightStrength.μ-η-comm"></a><a id="2793" href="Categories.Monad.Strong.html#2793" class="Field">μ-η-comm</a> <a id="2802" class="Symbol">:</a> <a id="2804" class="Symbol">{</a><a id="2805" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2807" href="Categories.Monad.Strong.html#2807" class="Bound">B</a> <a id="2809" class="Symbol">:</a> <a id="2811" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="2814" class="Symbol">}</a> <a id="2816" class="Symbol">→</a> <a id="2818" href="Categories.Monad.Strong.html#2327" class="Function">μ</a> <a id="2820" class="Symbol">(</a><a id="2821" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2823" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="2826" href="Categories.Monad.Strong.html#2807" class="Bound">B</a><a id="2827" class="Symbol">)</a> <a id="2829" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2831" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="2834" class="Symbol">(</a><a id="2835" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="2839" class="Symbol">(</a><a id="2840" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2842" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2844" href="Categories.Monad.Strong.html#2807" class="Bound">B</a><a id="2845" class="Symbol">))</a> <a id="2848" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2850" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="2854" class="Symbol">(</a><a id="2855" href="Categories.Functor.Core.html#432" class="Function">F₀</a> <a id="2858" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2860" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2862" href="Categories.Monad.Strong.html#2807" class="Bound">B</a><a id="2863" class="Symbol">)</a>
      <a id="2871" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2873" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="2877" class="Symbol">(</a><a id="2878" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2880" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2882" href="Categories.Monad.Strong.html#2807" class="Bound">B</a><a id="2883" class="Symbol">)</a> <a id="2885" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2887" class="Symbol">(</a><a id="2888" href="Categories.Monad.Strong.html#2327" class="Function">μ</a> <a id="2890" href="Categories.Monad.Strong.html#2805" class="Bound">A</a> <a id="2892" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="2895" href="Categories.Category.Core.html#630" class="Function">id</a><a id="2897" class="Symbol">)</a>
    <a id="2903" class="Comment">-- consecutive applications of strength commute (i.e. strength is associative)</a>
    <a id="RightStrength.strength-assoc"></a><a id="2986" href="Categories.Monad.Strong.html#2986" class="Field">strength-assoc</a> <a id="3001" class="Symbol">:</a>  <a id="3004" class="Symbol">{</a><a id="3005" href="Categories.Monad.Strong.html#3005" class="Bound">A</a> <a id="3007" href="Categories.Monad.Strong.html#3007" class="Bound">B</a> <a id="3009" href="Categories.Monad.Strong.html#3009" class="Bound">C</a> <a id="3011" class="Symbol">:</a> <a id="3013" href="Categories.Category.Core.html#559" class="Function">Obj</a><a id="3016" class="Symbol">}</a> <a id="3018" class="Symbol">→</a> <a id="3020" href="Categories.Functor.Core.html#455" class="Function">F₁</a> <a id="3023" href="Categories.Morphism.html#2022" class="Function">associator.to</a> <a id="3037" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="3039" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="3043" class="Symbol">(</a><a id="3044" href="Categories.Monad.Strong.html#3005" class="Bound">A</a> <a id="3046" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3048" href="Categories.Monad.Strong.html#3007" class="Bound">B</a> <a id="3050" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="3053" href="Categories.Monad.Strong.html#3009" class="Bound">C</a><a id="3054" class="Symbol">)</a>
      <a id="3062" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="3064" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="3068" class="Symbol">(</a><a id="3069" href="Categories.Monad.Strong.html#3005" class="Bound">A</a> <a id="3071" href="Categories.Category.Monoidal.Core.html#1435" class="Function Operator">⊗₀</a> <a id="3074" href="Categories.Monad.Strong.html#3007" class="Bound">B</a> <a id="3076" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3078" href="Categories.Monad.Strong.html#3009" class="Bound">C</a><a id="3079" class="Symbol">)</a> <a id="3081" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="3083" class="Symbol">(</a><a id="3084" href="Categories.NaturalTransformation.Core.html#783" class="Function">u.η</a> <a id="3088" class="Symbol">(</a><a id="3089" href="Categories.Monad.Strong.html#3005" class="Bound">A</a> <a id="3091" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3093" href="Categories.Monad.Strong.html#3007" class="Bound">B</a><a id="3094" class="Symbol">)</a> <a id="3096" href="Categories.Category.Monoidal.Core.html#1536" class="Function Operator">⊗₁</a> <a id="3099" href="Categories.Category.Core.html#630" class="Function">id</a><a id="3101" class="Symbol">)</a> <a id="3103" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="3105" href="Categories.Morphism.html#2022" class="Function">associator.to</a>

<a id="3120" class="Keyword">record</a> <a id="RightStrongMonad"></a><a id="3127" href="Categories.Monad.Strong.html#3127" class="Record">RightStrongMonad</a> <a id="3144" class="Symbol">{</a><a id="3145" href="Categories.Monad.Strong.html#3145" class="Bound">C</a> <a id="3147" class="Symbol">:</a> <a id="3149" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="3158" href="Categories.Monad.Strong.html#723" class="Generalizable">o</a> <a id="3160" href="Categories.Monad.Strong.html#725" class="Generalizable">ℓ</a> <a id="3162" href="Categories.Monad.Strong.html#727" class="Generalizable">e</a><a id="3163" class="Symbol">}</a> <a id="3165" class="Symbol">(</a><a id="3166" href="Categories.Monad.Strong.html#3166" class="Bound">V</a> <a id="3168" class="Symbol">:</a> <a id="3170" href="Categories.Category.Monoidal.Core.html#1287" class="Record">Monoidal</a> <a id="3179" href="Categories.Monad.Strong.html#3145" class="Bound">C</a><a id="3180" class="Symbol">)</a> <a id="3182" class="Symbol">:</a> <a id="3184" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="3188" class="Symbol">(</a><a id="3189" href="Categories.Monad.Strong.html#3158" class="Bound">o</a> <a id="3191" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3193" href="Categories.Monad.Strong.html#3160" class="Bound">ℓ</a> <a id="3195" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3197" href="Categories.Monad.Strong.html#3162" class="Bound">e</a><a id="3198" class="Symbol">)</a> <a id="3200" class="Keyword">where</a>
  <a id="3208" class="Keyword">field</a>
    <a id="RightStrongMonad.M"></a><a id="3218" href="Categories.Monad.Strong.html#3218" class="Field">M</a>        <a id="3227" class="Symbol">:</a> <a id="3229" href="Categories.Monad.html#454" class="Record">Monad</a> <a id="3235" href="Categories.Monad.Strong.html#3145" class="Bound">C</a>
    <a id="RightStrongMonad.rightStrength"></a><a id="3241" href="Categories.Monad.Strong.html#3241" class="Field">rightStrength</a> <a id="3255" class="Symbol">:</a> <a id="3257" href="Categories.Monad.Strong.html#2058" class="Record">RightStrength</a> <a id="3271" href="Categories.Monad.Strong.html#3166" class="Bound">V</a> <a id="3273" href="Categories.Monad.Strong.html#3218" class="Field">M</a>

  <a id="3278" class="Keyword">module</a> <a id="RightStrongMonad.M"></a><a id="3285" href="Categories.Monad.Strong.html#3285" class="Module">M</a> <a id="3287" class="Symbol">=</a> <a id="3289" href="Categories.Monad.html#454" class="Module">Monad</a> <a id="3295" href="Categories.Monad.Strong.html#3218" class="Field">M</a>
  <a id="3299" class="Keyword">open</a> <a id="3304" href="Categories.Monad.Strong.html#2058" class="Module">RightStrength</a> <a id="3318" href="Categories.Monad.Strong.html#3241" class="Field">rightStrength</a> <a id="3332" class="Keyword">public</a>
</pre></body></html>
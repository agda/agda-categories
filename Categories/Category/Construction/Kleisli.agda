{-# OPTIONS --without-K --safe #-}
module Categories.Category.Construction.Kleisli where

open import Level

open import Categories.Category
open import Categories.Functor using (Functor; module Functor)
open import Categories.NaturalTransformation hiding (id)
open import Categories.Monad

private
  variable
    o â„“ e : Level

Kleisli : {ğ’ : Category o â„“ e} â†’ Monad ğ’ â†’ Category o â„“ e
Kleisli {ğ’ = ğ’} M = record
  { Obj       = Obj
  ; _â‡’_       = Î» A B â†’ (A â‡’ Fâ‚€ B)
  ; _â‰ˆ_       = _â‰ˆ_
  ; _âˆ˜_       = Î» f g â†’ (Î¼.Î· _ âˆ˜ Fâ‚ f) âˆ˜ g
  ; id        = Î·.Î· _
  ; assoc     = assocâ€²
  ; sym-assoc = Equiv.sym assocâ€²
  ; identityË¡ = identityË¡â€²
  ; identityÊ³ = identityÊ³â€²
  ; equiv     = equiv
  ; âˆ˜-resp-â‰ˆ  = Î» fâ‰ˆh gâ‰ˆi â†’ âˆ˜-resp-â‰ˆ (âˆ˜-resp-â‰ˆÊ³ (F-resp-â‰ˆ fâ‰ˆh)) gâ‰ˆi
  }
  where
  module M = Monad M
  open M using (Î¼; Î·; F)
  open Functor F
  open Category ğ’
  open Equiv
  open HomReasoning

  -- shorthands to make the proofs nicer
  Fâ‰ˆ = F-resp-â‰ˆ

  assocâ€² : âˆ€ {A B C D} {f : A â‡’ Fâ‚€ B} {g : B â‡’ Fâ‚€ C} {h : C â‡’ Fâ‚€ D}
          â†’ (Î¼.Î· D âˆ˜ (Fâ‚ ((Î¼.Î· D âˆ˜ Fâ‚ h) âˆ˜ g))) âˆ˜ f â‰ˆ (Î¼.Î· D âˆ˜ Fâ‚ h) âˆ˜ ((Î¼.Î· C âˆ˜ Fâ‚ g) âˆ˜ f)
  assocâ€² {A} {B} {C} {D} {f} {g} {h} =
      begin
        (Î¼.Î· D âˆ˜ Fâ‚ ((Î¼.Î· D âˆ˜ Fâ‚ h) âˆ˜ g)) âˆ˜ f            â‰ˆâŸ¨ assoc âŸ©
        Î¼.Î· D âˆ˜ (Fâ‚ ((Î¼.Î· D âˆ˜ Fâ‚ h) âˆ˜ g) âˆ˜ f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (Fâ‰ˆ assoc âŸ©âˆ˜âŸ¨refl ) âŸ©
        Î¼.Î· D âˆ˜ (Fâ‚ (Î¼.Î· D âˆ˜ (Fâ‚ h âˆ˜ g)) âˆ˜ f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (homomorphism âŸ©âˆ˜âŸ¨refl) âŸ©
        Î¼.Î· D âˆ˜ ((Fâ‚ (Î¼.Î· D) âˆ˜ Fâ‚ (Fâ‚ h âˆ˜ g)) âˆ˜ f)       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc â—‹ âŸº assoc âŸ©
        (Î¼.Î· D âˆ˜ Fâ‚ (Î¼.Î· D)) âˆ˜ (Fâ‚ (Fâ‚ h âˆ˜ g) âˆ˜ f)       â‰ˆâŸ¨ (reflâŸ©âˆ˜âŸ¨ âŸº identityÊ³ â—‹ M.assoc) âŸ©âˆ˜âŸ¨refl â—‹ assoc âŸ©
        Î¼.Î· D âˆ˜ (Î¼.Î· (Fâ‚€ D) âˆ˜ (Fâ‚ (Fâ‚ h âˆ˜ g) âˆ˜ f))       â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ assoc âŸ©
        Î¼.Î· D âˆ˜ ((Î¼.Î· (Fâ‚€ D) âˆ˜ Fâ‚ (Fâ‚ h âˆ˜ g)) âˆ˜ f)       â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ ( (reflâŸ©âˆ˜âŸ¨ homomorphism) âŸ©âˆ˜âŸ¨refl) âŸ©
        Î¼.Î· D âˆ˜ ((Î¼.Î· (Fâ‚€ D) âˆ˜ (Fâ‚ (Fâ‚ h) âˆ˜ Fâ‚ g)) âˆ˜ f)  â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ (assoc âŸ©âˆ˜âŸ¨refl) âŸ©
        Î¼.Î· D âˆ˜ (((Î¼.Î· (Fâ‚€ D) âˆ˜ Fâ‚ (Fâ‚ h)) âˆ˜ Fâ‚ g) âˆ˜ f)  â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ ((Î¼.commute h âŸ©âˆ˜âŸ¨refl) âŸ©âˆ˜âŸ¨refl) âŸ©
        Î¼.Î· D âˆ˜ (((Fâ‚ h âˆ˜ Î¼.Î· C) âˆ˜ Fâ‚ g) âˆ˜ f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (assoc âŸ©âˆ˜âŸ¨refl) âŸ©
        Î¼.Î· D âˆ˜ ((Fâ‚ h âˆ˜ (Î¼.Î· C âˆ˜ Fâ‚ g)) âˆ˜ f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ assoc â—‹ âŸº assoc âŸ©
        (Î¼.Î· D âˆ˜ Fâ‚ h) âˆ˜ ((Î¼.Î· C âˆ˜ Fâ‚ g) âˆ˜ f)
      âˆ

  identityË¡â€² : âˆ€ {A B} {f : A â‡’ Fâ‚€ B} â†’ (Î¼.Î· B âˆ˜ Fâ‚ (Î·.Î· B)) âˆ˜ f â‰ˆ f
  identityË¡â€² {A} {B} {f} =
      begin
        (Î¼.Î· B âˆ˜ Fâ‚ (Î·.Î· B)) âˆ˜ f      â‰ˆË˜âŸ¨  (reflâŸ©âˆ˜âŸ¨ identityÊ³) âŸ©âˆ˜âŸ¨refl   âŸ©
        (Î¼.Î· B âˆ˜ Fâ‚ (Î·.Î· B) âˆ˜ id) âˆ˜ f â‰ˆâŸ¨ M.identityË¡ âŸ©âˆ˜âŸ¨refl âŸ©
        id âˆ˜ f                        â‰ˆâŸ¨ identityË¡ âŸ©
        f
      âˆ

  identityÊ³â€² : âˆ€ {A B} {f : A â‡’ Fâ‚€ B} â†’ (Î¼.Î· B âˆ˜ Fâ‚ f) âˆ˜ Î·.Î· A â‰ˆ f
  identityÊ³â€² {A} {B} {f} =
      begin
        (Î¼.Î· B âˆ˜ Fâ‚ f) âˆ˜ Î·.Î· A      â‰ˆâŸ¨ assoc âŸ©
        Î¼.Î· B âˆ˜ (Fâ‚ f âˆ˜ Î·.Î· A)      â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ Î·.commute f âŸ©
        Î¼.Î· B âˆ˜ (Î·.Î· (Fâ‚€ B) âˆ˜ f)    â‰ˆË˜âŸ¨ assoc âŸ©
        (Î¼.Î· B âˆ˜ Î·.Î· (Fâ‚€ B)) âˆ˜ f    â‰ˆâŸ¨ (reflâŸ©âˆ˜âŸ¨ âŸº identityÊ³ â—‹  M.identityÊ³) âŸ©âˆ˜âŸ¨refl âŸ©
        id âˆ˜ f                      â‰ˆâŸ¨ identityË¡ âŸ©
        f
      âˆ

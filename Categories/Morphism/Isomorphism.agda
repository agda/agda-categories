{-# OPTIONS --without-K --safe #-}
open import Categories.Category

-- Properties of isomorphisms

module Categories.Morphism.Isomorphism {o â„“ e} (ğ’ : Category o â„“ e) where

import Categories.Category.Construction.Core as Core
open import Categories.Category.Groupoid using (IsGroupoid)
import Categories.Category.Groupoid.Properties as GroupoidProps
import Categories.Morphism as Morphism
import Categories.Morphism.Properties as MorphismProps
import Categories.Morphism.IsoEquiv as IsoEquiv
import Categories.Morphism.Reasoning as MR

open Core ğ’ using (Core; Core-isGroupoid; CoreGroupoid)
open Morphism ğ’
open MorphismProps ğ’
open IsoEquiv ğ’ using (_â‰ƒ_; âŒ_âŒŸ)

open Category ğ’

private
  module MCore where
    open IsGroupoid    Core-isGroupoid public
    open MorphismProps Core public
    open Morphism      Core public
    open GroupoidProps CoreGroupoid public

  variable
    A B C D E F : Obj

open MCore using () renaming (_âˆ˜_ to _âˆ˜áµ¢_) public

CommutativeIso = IsGroupoid.CommutativeSquare Core-isGroupoid

module _ where
  open _â‰…_

  -- projecting isomorphism commutations to morphism commutations

  project-triangle : {g : A â‰… B} {f : C â‰… A} {h : C â‰… B} â†’ g âˆ˜áµ¢ f â‰ƒ h â†’ from g âˆ˜ from f â‰ˆ from h
  project-triangle = _â‰ƒ_.from-â‰ˆ

  project-square : {g : A â‰… B} {f : C â‰… A} {i : D â‰… B} {h : C â‰… D} â†’ g âˆ˜áµ¢ f â‰ƒ i âˆ˜áµ¢ h â†’ from g âˆ˜ from f â‰ˆ from i âˆ˜ from h
  project-square = _â‰ƒ_.from-â‰ˆ

  -- direct lifting from morphism commutations to isomorphism commutations

  lift-triangle : {f : A â‰… B} {g : C â‰… A} {h : C â‰… B} â†’ from f âˆ˜ from g â‰ˆ from h â†’ f âˆ˜áµ¢ g â‰ƒ h
  lift-triangle = âŒ_âŒŸ

  lift-square : {f : A â‰… B} {g : C â‰… A} {h : D â‰… B} {i : C â‰… D} â†’ from f âˆ˜ from g â‰ˆ from h âˆ˜ from i â†’ f âˆ˜áµ¢ g â‰ƒ h âˆ˜áµ¢ i
  lift-square = âŒ_âŒŸ

  lift-pentagon : {f : A â‰… B} {g : C â‰… A} {h : D â‰… C} {i : E â‰… B} {j : D â‰… E} â†’
                   from f âˆ˜ from g âˆ˜ from h â‰ˆ from i âˆ˜ from j â†’ f âˆ˜áµ¢ g âˆ˜áµ¢ h â‰ƒ i âˆ˜áµ¢ j
  lift-pentagon = âŒ_âŒŸ

  open MR Core
  open MCore using (_â»Â¹)
  open MCore.HomReasoning
  open MR.GroupoidR _ Core-isGroupoid

  squaresÃ—â‰ƒâ‡’â‰ƒ : {f fâ€² : A â‰… B} {g : A â‰… C} {h : B â‰… D} {i iâ€² : C â‰… D} â†’
                CommutativeIso f g h i â†’ CommutativeIso fâ€² g h iâ€² â†’ i â‰ƒ iâ€² â†’ f â‰ƒ fâ€²
  squaresÃ—â‰ƒâ‡’â‰ƒ sqâ‚ sqâ‚‚ eq = MCore.isosÃ—â‰ˆâ‡’â‰ˆ eq helperâ‚ (MCore.â‰….sym helperâ‚‚) sqâ‚ sqâ‚‚
    where
      helperâ‚ = record { iso = MCore.iso }
      helperâ‚‚ = record { iso = MCore.iso }

  -- imagine a triangle prism, if all the sides and the top face commute, the bottom face commute.
  triangle-prism : {iâ€² : A â‰… B} {fâ€² : C â‰… A} {hâ€² : C â‰… B} {i : D â‰… E} {j : D â‰… A}
    {k : E â‰… B} {f : F â‰… D} {g : F â‰… C} {h : F â‰… E} â†’
    iâ€² âˆ˜áµ¢ fâ€² â‰ƒ hâ€² â†’
    CommutativeIso i j k iâ€² â†’ CommutativeIso f g j fâ€² â†’ CommutativeIso h g k hâ€² â†’
    i âˆ˜áµ¢ f â‰ƒ h
  triangle-prism {iâ€² = iâ€²} {fâ€²} {_} {i} {_} {k} {f} {g} {_} eq sqâ‚ sqâ‚‚ sqâ‚ƒ =
    squaresÃ—â‰ƒâ‡’â‰ƒ glued sqâ‚ƒ eq
    where
      glued : CommutativeIso (i âˆ˜áµ¢ f) g k (iâ€² âˆ˜áµ¢ fâ€²)
      glued = sym (glue (sym sqâ‚) (sym sqâ‚‚))

  elim-triangleË¡ : {f : A â‰… B} {g : C â‰… A} {h : D â‰… C} {i : D â‰… B} {j : D â‰… A} â†’
                   f âˆ˜áµ¢ g âˆ˜áµ¢ h â‰ƒ i â†’ f âˆ˜áµ¢ j â‰ƒ i â†’ g âˆ˜áµ¢ h â‰ƒ j
  elim-triangleË¡ perim tri = MCore.mono _ _ (perim â—‹ âŸº tri)

  elim-triangleË¡â€² : {f : A â‰… B} {g : C â‰… A} {h : D â‰… C} {i : D â‰… B} {j : C â‰… B} â†’
                    f âˆ˜áµ¢ g âˆ˜áµ¢ h â‰ƒ i â†’ j âˆ˜áµ¢ h â‰ƒ i â†’ f âˆ˜áµ¢ g â‰ƒ j
  elim-triangleË¡â€² {f = f} {g} {h} {i} {j} perim tri = MCore.epi _ _ ( begin
    (f âˆ˜áµ¢ g) âˆ˜áµ¢ h â‰ˆâŸ¨ MCore.assoc âŸ©
    f âˆ˜áµ¢ g âˆ˜áµ¢ h   â‰ˆâŸ¨ perim âŸ©
    i             â‰ˆË˜âŸ¨ tri âŸ©
    j âˆ˜áµ¢ h        âˆ )

  cut-squareÊ³ : {g : A â‰… B} {f : A â‰… C} {h : B â‰… D} {i : C â‰… D} {j : B â‰… C} â†’
                CommutativeIso g f h i â†’ i âˆ˜áµ¢ j â‰ƒ h â†’ j âˆ˜áµ¢ g â‰ƒ f
  cut-squareÊ³ {g = g} {f = f} {h = h} {i = i} {j = j} sq tri = begin
    j âˆ˜áµ¢ g            â‰ˆâŸ¨ switch-fromtoË¡â€² {f = i} {h = j} {k = h} tri âŸ©âˆ˜âŸ¨ refl âŸ©
    (i â»Â¹ âˆ˜áµ¢ h) âˆ˜áµ¢ g  â‰ˆâŸ¨ MCore.assoc âŸ©
    i â»Â¹ âˆ˜áµ¢ h âˆ˜áµ¢ g    â‰ˆË˜âŸ¨ switch-fromtoË¡â€² {f = i} {h = f} {k = h âˆ˜áµ¢ g} (sym sq) âŸ©
    f                 âˆ

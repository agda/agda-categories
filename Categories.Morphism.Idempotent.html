<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Morphism.Idempotent</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>

<a id="37" class="Keyword">open</a> <a id="42" class="Keyword">import</a> <a id="49" href="Categories.Category.Core.html" class="Module">Categories.Category.Core</a>

<a id="75" class="Comment">-- Idempotents and Split Idempotents</a>
<a id="112" class="Keyword">module</a> <a id="119" href="Categories.Morphism.Idempotent.html" class="Module">Categories.Morphism.Idempotent</a> <a id="150" class="Symbol">{</a><a id="151" href="Categories.Morphism.Idempotent.html#151" class="Bound">o</a> <a id="153" href="Categories.Morphism.Idempotent.html#153" class="Bound">ℓ</a> <a id="155" href="Categories.Morphism.Idempotent.html#155" class="Bound">e</a><a id="156" class="Symbol">}</a> <a id="158" class="Symbol">(</a><a id="159" href="Categories.Morphism.Idempotent.html#159" class="Bound">𝒞</a> <a id="161" class="Symbol">:</a> <a id="163" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="172" href="Categories.Morphism.Idempotent.html#151" class="Bound">o</a> <a id="174" href="Categories.Morphism.Idempotent.html#153" class="Bound">ℓ</a> <a id="176" href="Categories.Morphism.Idempotent.html#155" class="Bound">e</a><a id="177" class="Symbol">)</a> <a id="179" class="Keyword">where</a>

<a id="186" class="Keyword">open</a> <a id="191" class="Keyword">import</a> <a id="198" href="Level.html" class="Module">Level</a>

<a id="205" class="Keyword">open</a> <a id="210" class="Keyword">import</a> <a id="217" href="Categories.Morphism.html" class="Module">Categories.Morphism</a> <a id="237" href="Categories.Morphism.Idempotent.html#159" class="Bound">𝒞</a>
<a id="239" class="Keyword">open</a> <a id="244" class="Keyword">import</a> <a id="251" href="Categories.Morphism.Reasoning.html" class="Module">Categories.Morphism.Reasoning</a> <a id="281" href="Categories.Morphism.Idempotent.html#159" class="Bound">𝒞</a>

<a id="284" class="Keyword">open</a> <a id="289" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="298" href="Categories.Morphism.Idempotent.html#159" class="Bound">𝒞</a>
<a id="300" class="Keyword">open</a> <a id="305" href="Categories.Category.Core.html#2462" class="Module">HomReasoning</a>

<a id="319" class="Keyword">record</a> <a id="Idempotent"></a><a id="326" href="Categories.Morphism.Idempotent.html#326" class="Record">Idempotent</a> <a id="337" class="Symbol">(</a><a id="338" href="Categories.Morphism.Idempotent.html#338" class="Bound">A</a> <a id="340" class="Symbol">:</a> <a id="342" href="Categories.Category.Core.html#559" class="Field">Obj</a><a id="345" class="Symbol">)</a> <a id="347" class="Symbol">:</a> <a id="349" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="353" class="Symbol">(</a><a id="354" href="Categories.Morphism.Idempotent.html#153" class="Bound">ℓ</a> <a id="356" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="358" href="Categories.Morphism.Idempotent.html#155" class="Bound">e</a><a id="359" class="Symbol">)</a> <a id="361" class="Keyword">where</a>
  <a id="369" class="Keyword">field</a>
    <a id="Idempotent.idem"></a><a id="379" href="Categories.Morphism.Idempotent.html#379" class="Field">idem</a>       <a id="390" class="Symbol">:</a> <a id="392" href="Categories.Morphism.Idempotent.html#338" class="Bound">A</a> <a id="394" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="396" href="Categories.Morphism.Idempotent.html#338" class="Bound">A</a>
    <a id="Idempotent.idempotent"></a><a id="402" href="Categories.Morphism.Idempotent.html#402" class="Field">idempotent</a> <a id="413" class="Symbol">:</a> <a id="415" href="Categories.Morphism.Idempotent.html#379" class="Field">idem</a> <a id="420" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="422" href="Categories.Morphism.Idempotent.html#379" class="Field">idem</a> <a id="427" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="429" href="Categories.Morphism.Idempotent.html#379" class="Field">idem</a>

<a id="435" class="Keyword">record</a> <a id="IsSplitIdempotent"></a><a id="442" href="Categories.Morphism.Idempotent.html#442" class="Record">IsSplitIdempotent</a> <a id="460" class="Symbol">{</a><a id="461" href="Categories.Morphism.Idempotent.html#461" class="Bound">A</a> <a id="463" class="Symbol">:</a> <a id="465" href="Categories.Category.Core.html#559" class="Field">Obj</a><a id="468" class="Symbol">}</a> <a id="470" class="Symbol">(</a><a id="471" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="473" class="Symbol">:</a> <a id="475" href="Categories.Morphism.Idempotent.html#461" class="Bound">A</a> <a id="477" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="479" href="Categories.Morphism.Idempotent.html#461" class="Bound">A</a><a id="480" class="Symbol">)</a> <a id="482" class="Symbol">:</a> <a id="484" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="488" class="Symbol">(</a><a id="489" href="Categories.Morphism.Idempotent.html#151" class="Bound">o</a> <a id="491" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="493" href="Categories.Morphism.Idempotent.html#153" class="Bound">ℓ</a> <a id="495" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="497" href="Categories.Morphism.Idempotent.html#155" class="Bound">e</a><a id="498" class="Symbol">)</a> <a id="500" class="Keyword">where</a>
  <a id="508" class="Keyword">field</a>
    <a id="518" class="Symbol">{</a><a id="IsSplitIdempotent.obj"></a><a id="519" href="Categories.Morphism.Idempotent.html#519" class="Field">obj</a><a id="522" class="Symbol">}</a>    <a id="527" class="Symbol">:</a> <a id="529" href="Categories.Category.Core.html#559" class="Field">Obj</a>
    <a id="IsSplitIdempotent.retract"></a><a id="537" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a>  <a id="546" class="Symbol">:</a> <a id="548" href="Categories.Morphism.Idempotent.html#461" class="Bound">A</a> <a id="550" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="552" href="Categories.Morphism.Idempotent.html#519" class="Field">obj</a>
    <a id="IsSplitIdempotent.section"></a><a id="560" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a>  <a id="569" class="Symbol">:</a> <a id="571" href="Categories.Morphism.Idempotent.html#519" class="Field">obj</a> <a id="575" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="577" href="Categories.Morphism.Idempotent.html#461" class="Bound">A</a>
    <a id="IsSplitIdempotent.retracts"></a><a id="583" href="Categories.Morphism.Idempotent.html#583" class="Field">retracts</a> <a id="592" class="Symbol">:</a> <a id="594" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="602" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="604" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="612" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="614" href="Categories.Category.Core.html#630" class="Field">id</a>
    <a id="IsSplitIdempotent.splits"></a><a id="621" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a>   <a id="630" class="Symbol">:</a> <a id="632" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="640" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="642" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="650" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="652" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a>

  <a id="IsSplitIdempotent.retract-absorb"></a><a id="657" href="Categories.Morphism.Idempotent.html#657" class="Function">retract-absorb</a> <a id="672" class="Symbol">:</a> <a id="674" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="682" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="684" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="686" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="688" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a>
  <a id="698" href="Categories.Morphism.Idempotent.html#657" class="Function">retract-absorb</a> <a id="713" class="Symbol">=</a> <a id="715" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
    <a id="725" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="733" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="735" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a>                 <a id="753" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="757" href="Categories.Category.Core.html#2734" class="Function Operator">refl⟩∘⟨</a> <a id="765" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a> <a id="772" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
    <a id="778" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="786" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="788" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="796" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="798" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a> <a id="806" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="809" href="Categories.Morphism.Reasoning.Core.html#7916" class="Function">cancelˡ</a> <a id="817" href="Categories.Morphism.Idempotent.html#583" class="Field">retracts</a> <a id="826" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
    <a id="832" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a>                     <a id="860" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

  <a id="IsSplitIdempotent.section-absorb"></a><a id="865" href="Categories.Morphism.Idempotent.html#865" class="Function">section-absorb</a> <a id="880" class="Symbol">:</a> <a id="882" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="884" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="886" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="894" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="896" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a>
  <a id="906" href="Categories.Morphism.Idempotent.html#865" class="Function">section-absorb</a> <a id="921" class="Symbol">=</a> <a id="923" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
    <a id="933" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="935" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="937" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a>                   <a id="963" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="967" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a> <a id="974" href="Categories.Category.Core.html#2837" class="Function Operator">⟩∘⟨refl</a> <a id="982" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
    <a id="988" class="Symbol">(</a><a id="989" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="997" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="999" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a><a id="1006" class="Symbol">)</a> <a id="1008" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1010" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="1018" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1021" href="Categories.Morphism.Reasoning.Core.html#7730" class="Function">cancelʳ</a> <a id="1029" href="Categories.Morphism.Idempotent.html#583" class="Field">retracts</a> <a id="1038" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
    <a id="1044" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a>                       <a id="1074" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

  <a id="IsSplitIdempotent.idempotent"></a><a id="1079" href="Categories.Morphism.Idempotent.html#1079" class="Function">idempotent</a> <a id="1090" class="Symbol">:</a> <a id="1092" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="1094" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1096" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="1098" href="Categories.Category.Core.html#595" class="Field Operator">≈</a> <a id="1100" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a>
  <a id="1104" href="Categories.Morphism.Idempotent.html#1079" class="Function">idempotent</a> <a id="1115" class="Symbol">=</a> <a id="1117" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
    <a id="1127" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a> <a id="1129" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1131" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a>                                     <a id="1169" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">≈˘⟨</a> <a id="1173" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a> <a id="1180" href="Categories.Category.Core.html#2634" class="Function Operator">⟩∘⟨</a> <a id="1184" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a> <a id="1191" href="Relation.Binary.Reasoning.Syntax.html#7462" class="Function">⟩</a>
    <a id="1197" class="Symbol">(</a><a id="1198" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="1206" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1208" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a><a id="1215" class="Symbol">)</a> <a id="1217" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1219" class="Symbol">(</a><a id="1220" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="1228" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1230" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a><a id="1237" class="Symbol">)</a> <a id="1239" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1242" href="Categories.Morphism.Reasoning.Core.html#8102" class="Function">cancelInner</a> <a id="1254" href="Categories.Morphism.Idempotent.html#583" class="Field">retracts</a> <a id="1263" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
    <a id="1269" href="Categories.Morphism.Idempotent.html#560" class="Field">section</a> <a id="1277" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="1279" href="Categories.Morphism.Idempotent.html#537" class="Field">retract</a>                         <a id="1311" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1314" href="Categories.Morphism.Idempotent.html#621" class="Field">splits</a> <a id="1321" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
    <a id="1327" href="Categories.Morphism.Idempotent.html#471" class="Bound">i</a>                                         <a id="1369" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

<a id="1372" class="Keyword">record</a> <a id="SplitIdempotent"></a><a id="1379" href="Categories.Morphism.Idempotent.html#1379" class="Record">SplitIdempotent</a> <a id="1395" class="Symbol">(</a><a id="1396" href="Categories.Morphism.Idempotent.html#1396" class="Bound">A</a> <a id="1398" class="Symbol">:</a> <a id="1400" href="Categories.Category.Core.html#559" class="Field">Obj</a><a id="1403" class="Symbol">)</a> <a id="1405" class="Symbol">:</a> <a id="1407" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1411" class="Symbol">(</a><a id="1412" href="Categories.Morphism.Idempotent.html#151" class="Bound">o</a> <a id="1414" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1416" href="Categories.Morphism.Idempotent.html#153" class="Bound">ℓ</a> <a id="1418" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1420" href="Categories.Morphism.Idempotent.html#155" class="Bound">e</a><a id="1421" class="Symbol">)</a> <a id="1423" class="Keyword">where</a>
  <a id="1431" class="Keyword">field</a>
    <a id="SplitIdempotent.idem"></a><a id="1441" href="Categories.Morphism.Idempotent.html#1441" class="Field">idem</a> <a id="1446" class="Symbol">:</a> <a id="1448" href="Categories.Morphism.Idempotent.html#1396" class="Bound">A</a> <a id="1450" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="1452" href="Categories.Morphism.Idempotent.html#1396" class="Bound">A</a>
    <a id="SplitIdempotent.isSplitIdempotent"></a><a id="1458" href="Categories.Morphism.Idempotent.html#1458" class="Field">isSplitIdempotent</a> <a id="1476" class="Symbol">:</a> <a id="1478" href="Categories.Morphism.Idempotent.html#442" class="Record">IsSplitIdempotent</a> <a id="1496" href="Categories.Morphism.Idempotent.html#1441" class="Field">idem</a>

  <a id="1504" class="Keyword">open</a> <a id="1509" href="Categories.Morphism.Idempotent.html#442" class="Module">IsSplitIdempotent</a> <a id="1527" href="Categories.Morphism.Idempotent.html#1458" class="Field">isSplitIdempotent</a> <a id="1545" class="Keyword">public</a>

<a id="1553" class="Comment">-- All split idempotents are idempotent</a>
<a id="SplitIdempotent⇒Idempotent"></a><a id="1593" href="Categories.Morphism.Idempotent.html#1593" class="Function">SplitIdempotent⇒Idempotent</a> <a id="1620" class="Symbol">:</a> <a id="1622" class="Symbol">∀</a> <a id="1624" class="Symbol">{</a><a id="1625" href="Categories.Morphism.Idempotent.html#1625" class="Bound">A</a><a id="1626" class="Symbol">}</a> <a id="1628" class="Symbol">→</a> <a id="1630" href="Categories.Morphism.Idempotent.html#1379" class="Record">SplitIdempotent</a> <a id="1646" href="Categories.Morphism.Idempotent.html#1625" class="Bound">A</a> <a id="1648" class="Symbol">→</a> <a id="1650" href="Categories.Morphism.Idempotent.html#326" class="Record">Idempotent</a> <a id="1661" href="Categories.Morphism.Idempotent.html#1625" class="Bound">A</a>
<a id="1663" href="Categories.Morphism.Idempotent.html#1593" class="Function">SplitIdempotent⇒Idempotent</a> <a id="1690" href="Categories.Morphism.Idempotent.html#1690" class="Bound">Split</a> <a id="1696" class="Symbol">=</a> <a id="1698" class="Keyword">record</a> <a id="1705" class="Symbol">{</a> <a id="1707" href="Categories.Morphism.Idempotent.html#1734" class="Module">Split</a> <a id="1713" class="Symbol">}</a>
  <a id="1717" class="Keyword">where</a>
    <a id="1727" class="Keyword">module</a> <a id="1734" href="Categories.Morphism.Idempotent.html#1734" class="Module">Split</a> <a id="1740" class="Symbol">=</a> <a id="1742" href="Categories.Morphism.Idempotent.html#1379" class="Module">SplitIdempotent</a> <a id="1758" href="Categories.Morphism.Idempotent.html#1690" class="Bound">Split</a>

<a id="1765" class="Keyword">module</a> <a id="1772" href="Categories.Morphism.Idempotent.html#1772" class="Module">_</a> <a id="1774" class="Symbol">{</a><a id="1775" href="Categories.Morphism.Idempotent.html#1775" class="Bound">A</a><a id="1776" class="Symbol">}</a> <a id="1778" class="Symbol">{</a><a id="1779" href="Categories.Morphism.Idempotent.html#1779" class="Bound">f</a> <a id="1781" class="Symbol">:</a> <a id="1783" href="Categories.Morphism.Idempotent.html#1775" class="Bound">A</a> <a id="1785" href="Categories.Category.Core.html#575" class="Field Operator">⇒</a> <a id="1787" href="Categories.Morphism.Idempotent.html#1775" class="Bound">A</a><a id="1788" class="Symbol">}</a> <a id="1790" class="Symbol">(</a><a id="1791" href="Categories.Morphism.Idempotent.html#1791" class="Bound">S</a> <a id="1793" href="Categories.Morphism.Idempotent.html#1793" class="Bound">T</a> <a id="1795" class="Symbol">:</a> <a id="1797" href="Categories.Morphism.Idempotent.html#442" class="Record">IsSplitIdempotent</a> <a id="1815" href="Categories.Morphism.Idempotent.html#1779" class="Bound">f</a><a id="1816" class="Symbol">)</a> <a id="1818" class="Keyword">where</a>
  <a id="1826" class="Keyword">private</a>
    <a id="1838" class="Keyword">module</a> <a id="1845" href="Categories.Morphism.Idempotent.html#1845" class="Module">S</a> <a id="1847" class="Symbol">=</a> <a id="1849" href="Categories.Morphism.Idempotent.html#442" class="Module">IsSplitIdempotent</a> <a id="1867" href="Categories.Morphism.Idempotent.html#1791" class="Bound">S</a>
    <a id="1873" class="Keyword">module</a> <a id="1880" href="Categories.Morphism.Idempotent.html#1880" class="Module">T</a> <a id="1882" class="Symbol">=</a> <a id="1884" href="Categories.Morphism.Idempotent.html#442" class="Module">IsSplitIdempotent</a> <a id="1902" href="Categories.Morphism.Idempotent.html#1793" class="Bound">T</a>

  <a id="1907" href="Categories.Morphism.Idempotent.html#1907" class="Function">split-idempotent-unique</a> <a id="1931" class="Symbol">:</a> <a id="1933" href="Categories.Morphism.Idempotent.html#519" class="Function">S.obj</a> <a id="1939" href="Categories.Morphism.html#1957" class="Record Operator">≅</a> <a id="1941" href="Categories.Morphism.Idempotent.html#519" class="Field">T.obj</a>
  <a id="1949" href="Categories.Morphism.Idempotent.html#1907" class="Function">split-idempotent-unique</a> <a id="1973" class="Symbol">=</a> <a id="1975" class="Keyword">record</a>
    <a id="1986" class="Symbol">{</a> <a id="1988" href="Categories.Morphism.html#2005" class="Field">from</a> <a id="1993" class="Symbol">=</a> <a id="1995" href="Categories.Morphism.Idempotent.html#537" class="Field">T.retract</a> <a id="2005" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2007" href="Categories.Morphism.Idempotent.html#560" class="Function">S.section</a>
    <a id="2021" class="Symbol">;</a> <a id="2023" href="Categories.Morphism.html#2022" class="Field">to</a> <a id="2026" class="Symbol">=</a> <a id="2028" href="Categories.Morphism.Idempotent.html#537" class="Function">S.retract</a> <a id="2038" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2040" href="Categories.Morphism.Idempotent.html#560" class="Field">T.section</a>
    <a id="2054" class="Symbol">;</a> <a id="2056" href="Categories.Morphism.html#2039" class="Field">iso</a> <a id="2060" class="Symbol">=</a> <a id="2062" class="Keyword">record</a>
      <a id="2075" class="Symbol">{</a> <a id="2077" href="Categories.Morphism.html#1586" class="Field">isoˡ</a> <a id="2082" class="Symbol">=</a> <a id="2084" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
        <a id="2098" class="Symbol">(</a><a id="2099" href="Categories.Morphism.Idempotent.html#537" class="Function">S.retract</a> <a id="2109" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2111" href="Categories.Morphism.Idempotent.html#560" class="Field">T.section</a><a id="2120" class="Symbol">)</a> <a id="2122" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2124" class="Symbol">(</a><a id="2125" href="Categories.Morphism.Idempotent.html#537" class="Field">T.retract</a> <a id="2135" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2137" href="Categories.Morphism.Idempotent.html#560" class="Function">S.section</a><a id="2146" class="Symbol">)</a> <a id="2148" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2151" href="Categories.Morphism.Reasoning.Core.html#8322" class="Function">center</a> <a id="2158" href="Categories.Morphism.Idempotent.html#621" class="Field">T.splits</a> <a id="2167" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2177" href="Categories.Morphism.Idempotent.html#537" class="Function">S.retract</a> <a id="2187" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2189" href="Categories.Morphism.Idempotent.html#1779" class="Bound">f</a> <a id="2191" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2193" href="Categories.Morphism.Idempotent.html#560" class="Function">S.section</a>                         <a id="2227" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2230" href="Categories.Morphism.Reasoning.Core.html#3113" class="Function">pullˡ</a> <a id="2236" href="Categories.Morphism.Idempotent.html#657" class="Function">S.retract-absorb</a> <a id="2253" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2263" href="Categories.Morphism.Idempotent.html#537" class="Function">S.retract</a> <a id="2273" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2275" href="Categories.Morphism.Idempotent.html#560" class="Function">S.section</a>                             <a id="2313" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2316" href="Categories.Morphism.Idempotent.html#583" class="Function">S.retracts</a> <a id="2327" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2337" href="Categories.Category.Core.html#630" class="Field">id</a>                                                <a id="2387" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
      <a id="2395" class="Symbol">;</a> <a id="2397" href="Categories.Morphism.html#1612" class="Field">isoʳ</a> <a id="2402" class="Symbol">=</a> <a id="2404" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
        <a id="2418" class="Symbol">(</a><a id="2419" href="Categories.Morphism.Idempotent.html#537" class="Field">T.retract</a> <a id="2429" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2431" href="Categories.Morphism.Idempotent.html#560" class="Function">S.section</a><a id="2440" class="Symbol">)</a> <a id="2442" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2444" class="Symbol">(</a><a id="2445" href="Categories.Morphism.Idempotent.html#537" class="Function">S.retract</a> <a id="2455" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2457" href="Categories.Morphism.Idempotent.html#560" class="Field">T.section</a><a id="2466" class="Symbol">)</a> <a id="2468" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2471" href="Categories.Morphism.Reasoning.Core.html#8322" class="Function">center</a> <a id="2478" href="Categories.Morphism.Idempotent.html#621" class="Function">S.splits</a> <a id="2487" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2497" href="Categories.Morphism.Idempotent.html#537" class="Field">T.retract</a> <a id="2507" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2509" href="Categories.Morphism.Idempotent.html#1779" class="Bound">f</a> <a id="2511" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2513" href="Categories.Morphism.Idempotent.html#560" class="Field">T.section</a>                         <a id="2547" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2550" href="Categories.Morphism.Reasoning.Core.html#3113" class="Function">pullˡ</a> <a id="2556" href="Categories.Morphism.Idempotent.html#657" class="Function">T.retract-absorb</a> <a id="2573" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2583" href="Categories.Morphism.Idempotent.html#537" class="Field">T.retract</a> <a id="2593" href="Categories.Category.Core.html#656" class="Field Operator">∘</a> <a id="2595" href="Categories.Morphism.Idempotent.html#560" class="Field">T.section</a>                             <a id="2633" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2636" href="Categories.Morphism.Idempotent.html#583" class="Field">T.retracts</a> <a id="2647" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
        <a id="2657" href="Categories.Category.Core.html#630" class="Field">id</a>                                                <a id="2707" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
      <a id="2715" class="Symbol">}</a>
    <a id="2721" class="Symbol">}</a>
</pre></body></html>
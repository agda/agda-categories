<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Category.Construction.KanComplex</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>


<a id="38" class="Keyword">open</a> <a id="43" class="Keyword">import</a> <a id="50" href="Level.html" class="Module">Level</a>

<a id="57" class="Comment">-- Kan Complexes</a>
<a id="74" class="Comment">--</a>
<a id="77" class="Comment">-- These are technically &quot;Algebraic&quot; Kan Complexes, as they come with a choice of fillers</a>
<a id="167" class="Comment">-- However, this notion is far easier than the more geometric flavor,</a>
<a id="237" class="Comment">-- as we can sidestep questions about choice.</a>

<a id="284" class="Keyword">module</a> <a id="291" href="Categories.Category.Construction.KanComplex.html" class="Module">Categories.Category.Construction.KanComplex</a> <a id="335" class="Symbol">(</a><a id="336" href="Categories.Category.Construction.KanComplex.html#336" class="Bound">o</a> <a id="338" href="Categories.Category.Construction.KanComplex.html#338" class="Bound">ℓ</a> <a id="340" class="Symbol">:</a> <a id="342" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="347" class="Symbol">)</a> <a id="349" class="Keyword">where</a>

<a id="356" class="Keyword">open</a> <a id="361" class="Keyword">import</a> <a id="368" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="377" class="Keyword">using</a> <a id="383" class="Symbol">(</a><a id="384" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="385" class="Symbol">)</a>
<a id="387" class="Keyword">open</a> <a id="392" class="Keyword">import</a> <a id="399" href="Data.Fin.html" class="Module">Data.Fin</a> <a id="408" class="Keyword">using</a> <a id="414" class="Symbol">(</a><a id="415" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a><a id="418" class="Symbol">;</a> <a id="420" href="Data.Fin.Base.html#3077" class="Function">inject₁</a><a id="427" class="Symbol">)</a>

<a id="430" class="Keyword">open</a> <a id="435" class="Keyword">import</a> <a id="442" href="Categories.Category.html" class="Module">Categories.Category</a> <a id="462" class="Keyword">using</a> <a id="468" class="Symbol">(</a><a id="469" href="Categories.Category.Core.html#442" class="Record">Category</a><a id="477" class="Symbol">)</a>

<a id="480" class="Keyword">open</a> <a id="485" class="Keyword">import</a> <a id="492" href="Categories.Category.Instance.SimplicialSet.html" class="Module">Categories.Category.Instance.SimplicialSet</a> <a id="535" class="Keyword">using</a> <a id="541" class="Symbol">(</a><a id="542" href="Categories.Category.Instance.SimplicialSet.html#251" class="Function">SimplicialSet</a><a id="555" class="Symbol">)</a>
<a id="557" class="Keyword">open</a> <a id="562" class="Keyword">import</a> <a id="569" href="Categories.Category.Instance.SimplicialSet.Properties.html" class="Module">Categories.Category.Instance.SimplicialSet.Properties</a> <a id="623" href="Categories.Category.Construction.KanComplex.html#336" class="Bound">o</a> <a id="625" href="Categories.Category.Construction.KanComplex.html#338" class="Bound">ℓ</a> <a id="627" class="Keyword">using</a> <a id="633" class="Symbol">(</a><a id="634" href="Categories.Category.Instance.SimplicialSet.Properties.html#1028" class="Function">ΔSet</a><a id="638" class="Symbol">;</a> <a id="640" href="Categories.Category.Instance.SimplicialSet.Properties.html#1124" class="Function Operator">Δ[_]</a><a id="644" class="Symbol">;</a> <a id="646" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[_,_]</a><a id="652" class="Symbol">;</a> <a id="654" href="Categories.Category.Instance.SimplicialSet.Properties.html#5391" class="Function">Λ-inj</a><a id="659" class="Symbol">)</a>

<a id="662" class="Keyword">open</a> <a id="667" href="Categories.Category.Core.html#442" class="Module">Category</a> <a id="676" class="Symbol">(</a><a id="677" href="Categories.Category.Instance.SimplicialSet.html#251" class="Function">SimplicialSet</a> <a id="691" href="Categories.Category.Construction.KanComplex.html#336" class="Bound">o</a> <a id="693" href="Categories.Category.Construction.KanComplex.html#338" class="Bound">ℓ</a><a id="694" class="Symbol">)</a>

<a id="697" class="Comment">-- A Kan complex is a simplicial set where every k-horn has a filler.</a>
<a id="767" class="Keyword">record</a> <a id="IsKanComplex"></a><a id="774" href="Categories.Category.Construction.KanComplex.html#774" class="Record">IsKanComplex</a> <a id="787" class="Symbol">(</a><a id="788" href="Categories.Category.Construction.KanComplex.html#788" class="Bound">X</a> <a id="790" class="Symbol">:</a> <a id="792" href="Categories.Category.Instance.SimplicialSet.Properties.html#1028" class="Function">ΔSet</a><a id="796" class="Symbol">)</a> <a id="798" class="Symbol">:</a> <a id="800" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="804" class="Symbol">(</a><a id="805" href="Categories.Category.Construction.KanComplex.html#336" class="Bound">o</a> <a id="807" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="809" href="Categories.Category.Construction.KanComplex.html#338" class="Bound">ℓ</a><a id="810" class="Symbol">)</a> <a id="812" class="Keyword">where</a>
  <a id="820" class="Keyword">field</a>
    <a id="IsKanComplex.filler"></a><a id="830" href="Categories.Category.Construction.KanComplex.html#830" class="Field">filler</a>      <a id="842" class="Symbol">:</a> <a id="844" class="Symbol">∀</a> <a id="846" class="Symbol">{</a><a id="847" href="Categories.Category.Construction.KanComplex.html#847" class="Bound">n</a><a id="848" class="Symbol">}</a> <a id="850" class="Symbol">{</a><a id="851" href="Categories.Category.Construction.KanComplex.html#851" class="Bound">k</a><a id="852" class="Symbol">}</a> <a id="854" class="Symbol">→</a> <a id="856" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="859" href="Categories.Category.Construction.KanComplex.html#847" class="Bound">n</a> <a id="861" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="863" href="Categories.Category.Construction.KanComplex.html#851" class="Bound">k</a> <a id="865" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="867" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="869" href="Categories.Category.Construction.KanComplex.html#788" class="Bound">X</a> <a id="871" class="Symbol">→</a> <a id="873" href="Categories.Category.Instance.SimplicialSet.Properties.html#1124" class="Function Operator">Δ[</a> <a id="876" href="Categories.Category.Construction.KanComplex.html#847" class="Bound">n</a> <a id="878" href="Categories.Category.Instance.SimplicialSet.Properties.html#1124" class="Function Operator">]</a> <a id="880" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="882" href="Categories.Category.Construction.KanComplex.html#788" class="Bound">X</a>
    <a id="IsKanComplex.filler-cong"></a><a id="888" href="Categories.Category.Construction.KanComplex.html#888" class="Field">filler-cong</a> <a id="900" class="Symbol">:</a> <a id="902" class="Symbol">∀</a> <a id="904" class="Symbol">{</a><a id="905" href="Categories.Category.Construction.KanComplex.html#905" class="Bound">n</a><a id="906" class="Symbol">}</a> <a id="908" class="Symbol">{</a><a id="909" href="Categories.Category.Construction.KanComplex.html#909" class="Bound">k</a><a id="910" class="Symbol">}</a> <a id="912" class="Symbol">→</a> <a id="914" class="Symbol">{</a><a id="915" href="Categories.Category.Construction.KanComplex.html#915" class="Bound">f</a> <a id="917" href="Categories.Category.Construction.KanComplex.html#917" class="Bound">g</a> <a id="919" class="Symbol">:</a> <a id="921" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="924" href="Categories.Category.Construction.KanComplex.html#905" class="Bound">n</a> <a id="926" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="928" href="Categories.Category.Construction.KanComplex.html#909" class="Bound">k</a> <a id="930" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="932" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="934" href="Categories.Category.Construction.KanComplex.html#788" class="Bound">X</a><a id="935" class="Symbol">}</a> <a id="937" class="Symbol">→</a> <a id="939" href="Categories.Category.Construction.KanComplex.html#915" class="Bound">f</a> <a id="941" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="943" href="Categories.Category.Construction.KanComplex.html#917" class="Bound">g</a> <a id="945" class="Symbol">→</a> <a id="947" href="Categories.Category.Construction.KanComplex.html#830" class="Field">filler</a> <a id="954" class="Symbol">{</a><a id="955" href="Categories.Category.Construction.KanComplex.html#905" class="Bound">n</a><a id="956" class="Symbol">}</a> <a id="958" href="Categories.Category.Construction.KanComplex.html#915" class="Bound">f</a> <a id="960" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="962" href="Categories.Category.Construction.KanComplex.html#830" class="Field">filler</a> <a id="969" href="Categories.Category.Construction.KanComplex.html#917" class="Bound">g</a>
    <a id="IsKanComplex.is-filler"></a><a id="975" href="Categories.Category.Construction.KanComplex.html#975" class="Field">is-filler</a>   <a id="987" class="Symbol">:</a> <a id="989" class="Symbol">∀</a> <a id="991" class="Symbol">{</a><a id="992" href="Categories.Category.Construction.KanComplex.html#992" class="Bound">n</a><a id="993" class="Symbol">}</a> <a id="995" class="Symbol">{</a><a id="996" href="Categories.Category.Construction.KanComplex.html#996" class="Bound">k</a><a id="997" class="Symbol">}</a> <a id="999" class="Symbol">→</a> <a id="1001" class="Symbol">(</a><a id="1002" href="Categories.Category.Construction.KanComplex.html#1002" class="Bound">f</a> <a id="1004" class="Symbol">:</a> <a id="1006" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="1009" href="Categories.Category.Construction.KanComplex.html#992" class="Bound">n</a> <a id="1011" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="1013" href="Categories.Category.Construction.KanComplex.html#996" class="Bound">k</a> <a id="1015" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="1017" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="1019" href="Categories.Category.Construction.KanComplex.html#788" class="Bound">X</a><a id="1020" class="Symbol">)</a> <a id="1022" class="Symbol">→</a> <a id="1024" href="Categories.Category.Construction.KanComplex.html#830" class="Field">filler</a> <a id="1031" href="Categories.Category.Construction.KanComplex.html#1002" class="Bound">f</a> <a id="1033" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="1035" href="Categories.Category.Instance.SimplicialSet.Properties.html#5391" class="Function">Λ-inj</a> <a id="1041" href="Categories.Category.Construction.KanComplex.html#996" class="Bound">k</a> <a id="1043" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="1045" href="Categories.Category.Construction.KanComplex.html#1002" class="Bound">f</a>

<a id="1048" class="Comment">-- &#39;inner k&#39; will embed &#39;k : Fin n&#39; into the &quot;inner&quot; portion of &#39;Fin (n + 2)&#39;</a>
<a id="1126" class="Comment">-- Visually, it looks a little something like:</a>
<a id="1173" class="Comment">-- </a>
<a id="1177" class="Comment">--   * * *</a>
<a id="1188" class="Comment">--   | | | </a>
<a id="1200" class="Comment">--   v v v</a>
<a id="1211" class="Comment">-- * * * * *</a>
<a id="1224" class="Comment">-- </a>
<a id="1228" class="Comment">-- Note that this is set up in such a way that we can normalize</a>
<a id="1292" class="Comment">-- as far as possible without pattern matching on &#39;i&#39; in proofs.</a>
<a id="inner"></a><a id="1357" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="1363" class="Symbol">:</a> <a id="1365" class="Symbol">∀</a> <a id="1367" class="Symbol">{</a><a id="1368" href="Categories.Category.Construction.KanComplex.html#1368" class="Bound">n</a><a id="1369" class="Symbol">}</a> <a id="1371" class="Symbol">→</a> <a id="1373" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a> <a id="1377" href="Categories.Category.Construction.KanComplex.html#1368" class="Bound">n</a> <a id="1379" class="Symbol">→</a> <a id="1381" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a> <a id="1385" class="Symbol">(</a><a id="1386" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="1392" class="Symbol">(</a><a id="1393" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="1399" href="Categories.Category.Construction.KanComplex.html#1368" class="Bound">n</a><a id="1400" class="Symbol">))</a>
<a id="1403" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="1409" href="Categories.Category.Construction.KanComplex.html#1409" class="Bound">i</a> <a id="1411" class="Symbol">=</a> <a id="1413" href="Data.Fin.Base.html#1197" class="InductiveConstructor">Fin.suc</a> <a id="1421" class="Symbol">(</a><a id="1422" href="Data.Fin.Base.html#3077" class="Function">inject₁</a> <a id="1430" href="Categories.Category.Construction.KanComplex.html#1409" class="Bound">i</a><a id="1431" class="Symbol">)</a>

<a id="1434" class="Comment">-- A Weak Kan complex is similar to a Kan Complex, but where only &quot;inner horns&quot; have fillers.</a>
<a id="1528" class="Comment">--</a>
<a id="1531" class="Comment">-- The indexing here is tricky, but it lets us avoid extra proof conditions that the horn is an inner horn.</a>
<a id="1639" class="Comment">-- The basic idea is that if we want an n-dimensional inner horn, then we only want to allow the faces {1,2,3...n-1}</a>
<a id="1756" class="Comment">-- to be missing. We could do this by requiring proofs that the missing face is greater than 0 and less than n, but</a>
<a id="1872" class="Comment">-- this makes working with the definition _extremely_ difficult.</a>
<a id="1937" class="Comment">--</a>
<a id="1940" class="Comment">-- To avoid this, we only allow an missing face index that ranges from 0 to n-2, and then embed that index</a>
<a id="2047" class="Comment">-- into the full range of face indexes via &#39;inner&#39;. This does require us to shift our indexes around a bit.</a>
<a id="2155" class="Comment">-- To make this indexing more obvious, we use the suggestively named variable &#39;n-2&#39;.</a>
<a id="2240" class="Keyword">record</a> <a id="IsWeakKanComplex"></a><a id="2247" href="Categories.Category.Construction.KanComplex.html#2247" class="Record">IsWeakKanComplex</a> <a id="2264" class="Symbol">(</a><a id="2265" href="Categories.Category.Construction.KanComplex.html#2265" class="Bound">X</a> <a id="2267" class="Symbol">:</a> <a id="2269" href="Categories.Category.Instance.SimplicialSet.Properties.html#1028" class="Function">ΔSet</a><a id="2273" class="Symbol">)</a> <a id="2275" class="Symbol">:</a> <a id="2277" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2281" class="Symbol">(</a><a id="2282" href="Categories.Category.Construction.KanComplex.html#336" class="Bound">o</a> <a id="2284" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2286" href="Categories.Category.Construction.KanComplex.html#338" class="Bound">ℓ</a><a id="2287" class="Symbol">)</a> <a id="2289" class="Keyword">where</a>
  <a id="2297" class="Keyword">field</a>
    <a id="IsWeakKanComplex.filler"></a><a id="2307" href="Categories.Category.Construction.KanComplex.html#2307" class="Field">filler</a>      <a id="2319" class="Symbol">:</a> <a id="2321" class="Symbol">∀</a> <a id="2323" class="Symbol">{</a><a id="2324" href="Categories.Category.Construction.KanComplex.html#2324" class="Bound">n-2</a><a id="2327" class="Symbol">}</a> <a id="2329" class="Symbol">{</a><a id="2330" href="Categories.Category.Construction.KanComplex.html#2330" class="Bound">k</a> <a id="2332" class="Symbol">:</a> <a id="2334" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a> <a id="2338" href="Categories.Category.Construction.KanComplex.html#2324" class="Bound">n-2</a><a id="2341" class="Symbol">}</a> <a id="2343" class="Symbol">→</a> <a id="2345" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="2348" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2354" class="Symbol">(</a><a id="2355" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2361" href="Categories.Category.Construction.KanComplex.html#2324" class="Bound">n-2</a><a id="2364" class="Symbol">)</a> <a id="2366" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="2368" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="2374" href="Categories.Category.Construction.KanComplex.html#2330" class="Bound">k</a> <a id="2376" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="2378" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="2380" href="Categories.Category.Construction.KanComplex.html#2265" class="Bound">X</a> <a id="2382" class="Symbol">→</a> <a id="2384" href="Categories.Category.Instance.SimplicialSet.Properties.html#1124" class="Function Operator">Δ[</a> <a id="2387" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2393" class="Symbol">(</a><a id="2394" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2400" href="Categories.Category.Construction.KanComplex.html#2324" class="Bound">n-2</a><a id="2403" class="Symbol">)</a> <a id="2405" href="Categories.Category.Instance.SimplicialSet.Properties.html#1124" class="Function Operator">]</a> <a id="2407" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="2409" href="Categories.Category.Construction.KanComplex.html#2265" class="Bound">X</a>
    <a id="IsWeakKanComplex.filler-cong"></a><a id="2415" href="Categories.Category.Construction.KanComplex.html#2415" class="Field">filler-cong</a> <a id="2427" class="Symbol">:</a> <a id="2429" class="Symbol">∀</a> <a id="2431" class="Symbol">{</a><a id="2432" href="Categories.Category.Construction.KanComplex.html#2432" class="Bound">n-2</a><a id="2435" class="Symbol">}</a> <a id="2437" class="Symbol">{</a><a id="2438" href="Categories.Category.Construction.KanComplex.html#2438" class="Bound">k</a> <a id="2440" class="Symbol">:</a> <a id="2442" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a> <a id="2446" href="Categories.Category.Construction.KanComplex.html#2432" class="Bound">n-2</a><a id="2449" class="Symbol">}</a> <a id="2451" class="Symbol">→</a> <a id="2453" class="Symbol">{</a><a id="2454" href="Categories.Category.Construction.KanComplex.html#2454" class="Bound">f</a> <a id="2456" href="Categories.Category.Construction.KanComplex.html#2456" class="Bound">g</a> <a id="2458" class="Symbol">:</a> <a id="2460" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="2463" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2469" class="Symbol">(</a><a id="2470" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2476" href="Categories.Category.Construction.KanComplex.html#2432" class="Bound">n-2</a><a id="2479" class="Symbol">)</a> <a id="2481" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="2483" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="2489" href="Categories.Category.Construction.KanComplex.html#2438" class="Bound">k</a> <a id="2491" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="2493" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="2495" href="Categories.Category.Construction.KanComplex.html#2265" class="Bound">X</a><a id="2496" class="Symbol">}</a> <a id="2498" class="Symbol">→</a> <a id="2500" href="Categories.Category.Construction.KanComplex.html#2454" class="Bound">f</a> <a id="2502" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2504" href="Categories.Category.Construction.KanComplex.html#2456" class="Bound">g</a> <a id="2506" class="Symbol">→</a> <a id="2508" href="Categories.Category.Construction.KanComplex.html#2307" class="Field">filler</a> <a id="2515" href="Categories.Category.Construction.KanComplex.html#2454" class="Bound">f</a> <a id="2517" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2519" href="Categories.Category.Construction.KanComplex.html#2307" class="Field">filler</a> <a id="2526" href="Categories.Category.Construction.KanComplex.html#2456" class="Bound">g</a>
    <a id="IsWeakKanComplex.is-filler"></a><a id="2532" href="Categories.Category.Construction.KanComplex.html#2532" class="Field">is-filler</a>   <a id="2544" class="Symbol">:</a> <a id="2546" class="Symbol">∀</a> <a id="2548" class="Symbol">{</a><a id="2549" href="Categories.Category.Construction.KanComplex.html#2549" class="Bound">n-2</a><a id="2552" class="Symbol">}</a> <a id="2554" class="Symbol">{</a><a id="2555" href="Categories.Category.Construction.KanComplex.html#2555" class="Bound">k</a> <a id="2557" class="Symbol">:</a> <a id="2559" href="Data.Fin.Base.html#1154" class="Datatype">Fin</a> <a id="2563" href="Categories.Category.Construction.KanComplex.html#2549" class="Bound">n-2</a><a id="2566" class="Symbol">}</a> <a id="2568" class="Symbol">→</a> <a id="2570" class="Symbol">(</a><a id="2571" href="Categories.Category.Construction.KanComplex.html#2571" class="Bound">f</a> <a id="2573" class="Symbol">:</a> <a id="2575" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">Λ[</a> <a id="2578" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2584" class="Symbol">(</a><a id="2585" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">ℕ.suc</a> <a id="2591" href="Categories.Category.Construction.KanComplex.html#2549" class="Bound">n-2</a><a id="2594" class="Symbol">)</a> <a id="2596" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">,</a> <a id="2598" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="2604" href="Categories.Category.Construction.KanComplex.html#2555" class="Bound">k</a> <a id="2606" href="Categories.Category.Instance.SimplicialSet.Properties.html#3827" class="Function Operator">]</a> <a id="2608" href="Categories.Category.Core.html#575" class="Function Operator">⇒</a> <a id="2610" href="Categories.Category.Construction.KanComplex.html#2265" class="Bound">X</a><a id="2611" class="Symbol">)</a> <a id="2613" class="Symbol">→</a> <a id="2615" href="Categories.Category.Construction.KanComplex.html#2307" class="Field">filler</a> <a id="2622" href="Categories.Category.Construction.KanComplex.html#2571" class="Bound">f</a> <a id="2624" href="Categories.Category.Core.html#656" class="Function Operator">∘</a> <a id="2626" href="Categories.Category.Instance.SimplicialSet.Properties.html#5391" class="Function">Λ-inj</a> <a id="2632" class="Symbol">(</a><a id="2633" href="Categories.Category.Construction.KanComplex.html#1357" class="Function">inner</a> <a id="2639" href="Categories.Category.Construction.KanComplex.html#2555" class="Bound">k</a><a id="2640" class="Symbol">)</a> <a id="2642" href="Categories.Category.Core.html#595" class="Function Operator">≈</a> <a id="2644" href="Categories.Category.Construction.KanComplex.html#2571" class="Bound">f</a>

<a id="KanComplex⇒WeakKanComplex"></a><a id="2647" href="Categories.Category.Construction.KanComplex.html#2647" class="Function">KanComplex⇒WeakKanComplex</a> <a id="2673" class="Symbol">:</a> <a id="2675" class="Symbol">∀</a> <a id="2677" class="Symbol">{</a><a id="2678" href="Categories.Category.Construction.KanComplex.html#2678" class="Bound">X</a><a id="2679" class="Symbol">}</a> <a id="2681" class="Symbol">→</a> <a id="2683" href="Categories.Category.Construction.KanComplex.html#774" class="Record">IsKanComplex</a> <a id="2696" href="Categories.Category.Construction.KanComplex.html#2678" class="Bound">X</a> <a id="2698" class="Symbol">→</a> <a id="2700" href="Categories.Category.Construction.KanComplex.html#2247" class="Record">IsWeakKanComplex</a> <a id="2717" href="Categories.Category.Construction.KanComplex.html#2678" class="Bound">X</a>
<a id="2719" href="Categories.Category.Construction.KanComplex.html#2647" class="Function">KanComplex⇒WeakKanComplex</a> <a id="2745" href="Categories.Category.Construction.KanComplex.html#2745" class="Bound">complex</a> <a id="2753" class="Symbol">=</a> <a id="2755" class="Keyword">record</a> <a id="2762" class="Symbol">{</a> <a id="2764" href="Categories.Category.Construction.KanComplex.html#2764" class="Module">IsKanComplex</a> <a id="2777" href="Categories.Category.Construction.KanComplex.html#2745" class="Bound">complex</a> <a id="2785" class="Symbol">}</a>
</pre></body></html>
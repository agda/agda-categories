{-# OPTIONS --without-K --safe #-}

module Categories.Category.Extensive where

-- https://ncatlab.org/nlab/show/extensive+category

open import Level
open import Function using (_$_)

open import Categories.Category.Core using (Category)
open import Categories.Diagram.Pullback using (Pullback; IsPullback; up-to-iso)
open import Categories.Diagram.Pullback.Properties using (module IsoPb)
open import Categories.Category.Cocartesian using (Cocartesian)
open import Categories.Object.Coproduct using (IsCoproduct)
open import Categories.Morphism
import Categories.Morphism.Reasoning as MR

record Extensive {o â„“ e : Level} (ğ’ : Category o â„“ e) : Set (suc (o âŠ” â„“ âŠ” e)) where
  open Category ğ’
  open Pullback using (pâ‚)

  field
    cocartesian : Cocartesian ğ’

  module CC = Cocartesian cocartesian
  open CC using (_+_; iâ‚; iâ‚‚; Â¡)

  field
    pullbackâ‚ : {A B C : Obj} (f : A â‡’ B + C) â†’ Pullback ğ’ f iâ‚
    pullbackâ‚‚ : {A B C : Obj} (f : A â‡’ B + C) â†’ Pullback ğ’ f iâ‚‚
    pullback-of-cp-is-cp : {A B C : Obj} (f : A â‡’ B + C) â†’ IsCoproduct ğ’ (pâ‚ (pullbackâ‚ f)) (pâ‚ (pullbackâ‚‚ f))

    pullbackâ‚-is-mono : âˆ€ {A B : Obj} â†’ Mono ğ’ (iâ‚ {A = A}{B = B})
    pullbackâ‚‚-is-mono : âˆ€ {A B : Obj} â†’ Mono ğ’ (iâ‚‚ {A = A}{B = B})

    disjoint : âˆ€ {A B : Obj} â†’ IsPullback ğ’ Â¡ Â¡ (iâ‚ {A = A}{B = B}) iâ‚‚

  -- a version with non-canonical pullbacks
  module _ {A B C : Obj} {f : A â‡’ B + C} (pbâ‚ : Pullback ğ’ f iâ‚) (pbâ‚‚ : Pullback ğ’ f iâ‚‚) where
      private
        open IsCoproduct (pullback-of-cp-is-cp f)
        open HomReasoning; open MR ğ’

        open IsoPb ğ’ (pullbackâ‚ f) pbâ‚ renaming (Pâ‚€â‡’Pâ‚ to pbâ‚-to-can; pâ‚-â‰ˆ to pâ‚-â‰ˆâ‚)
        open IsoPb ğ’ (pullbackâ‚‚ f) pbâ‚‚ renaming (Pâ‚€â‡’Pâ‚ to pbâ‚‚-to-can; pâ‚-â‰ˆ to pâ‚-â‰ˆâ‚‚)

        can-to-pbâ‚ = _â‰…_.from $ up-to-iso ğ’ pbâ‚ (pullbackâ‚ f)
        can-to-pbâ‚‚ = _â‰…_.from $ up-to-iso ğ’ pbâ‚‚ (pullbackâ‚‚ f)

      pullback-of-cp-is-cp' : IsCoproduct ğ’ (pâ‚ pbâ‚) (pâ‚ pbâ‚‚)

      IsCoproduct.[_,_] pullback-of-cp-is-cp' g h  = [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ]
      IsCoproduct.injectâ‚ pullback-of-cp-is-cp' {_}{g}{h}  = begin
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ pâ‚ pbâ‚                               â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ cancelÊ³ (Iso.isoË¡ $ _â‰…_.iso $ up-to-iso ğ’ pbâ‚ (pullbackâ‚ f)) âŸ©
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ (pâ‚ pbâ‚ âˆ˜ pbâ‚-to-can) âˆ˜ can-to-pbâ‚    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pâ‚-â‰ˆâ‚ âŸ©âˆ˜âŸ¨refl âŸ©
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ pâ‚ (pullbackâ‚ f) âˆ˜ can-to-pbâ‚         â‰ˆâŸ¨ pullË¡ injectâ‚ âŸ©
         (g âˆ˜ pbâ‚-to-can) âˆ˜ can-to-pbâ‚                                               â‰ˆâŸ¨ cancelÊ³ (Iso.isoË¡ $ _â‰…_.iso $ up-to-iso ğ’ pbâ‚ (pullbackâ‚ f)) âŸ©
         g                                                                           âˆ

      IsCoproduct.injectâ‚‚ pullback-of-cp-is-cp' {_}{g}{h} = begin
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ pâ‚ pbâ‚‚                               â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ cancelÊ³ (Iso.isoË¡ $ _â‰…_.iso $ up-to-iso ğ’ pbâ‚‚ (pullbackâ‚‚ f)) âŸ©
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ (pâ‚ pbâ‚‚ âˆ˜ pbâ‚‚-to-can) âˆ˜ can-to-pbâ‚‚    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ pâ‚-â‰ˆâ‚‚ âŸ©âˆ˜âŸ¨refl âŸ©
         [ g âˆ˜ pbâ‚-to-can , h âˆ˜ pbâ‚‚-to-can ] âˆ˜ pâ‚ (pullbackâ‚‚ f) âˆ˜ can-to-pbâ‚‚         â‰ˆâŸ¨ pullË¡ injectâ‚‚ âŸ©
         (h âˆ˜ pbâ‚‚-to-can) âˆ˜ can-to-pbâ‚‚                                               â‰ˆâŸ¨ cancelÊ³ (Iso.isoË¡ $ _â‰…_.iso $ up-to-iso ğ’ pbâ‚‚ (pullbackâ‚‚ f)) âŸ©
         h                                                                           âˆ

      IsCoproduct.unique pullback-of-cp-is-cp'   {_}{u}{g}{h}  uâˆ˜pâ‚pbâ‚â‰ˆg uâˆ˜pâ‚pbâ‚‚â‰ˆh  = unique
        (begin
                     u âˆ˜ pâ‚ (pullbackâ‚ f)            â‰ˆË˜âŸ¨ pullÊ³ pâ‚-â‰ˆâ‚ âŸ©
                     (u âˆ˜ pâ‚ pbâ‚) âˆ˜ pbâ‚-to-can       â‰ˆâŸ¨ uâˆ˜pâ‚pbâ‚â‰ˆg âŸ©âˆ˜âŸ¨refl âŸ©
                     g âˆ˜ pbâ‚-to-can                  âˆ)
        (begin
                     u âˆ˜ pâ‚ (pullbackâ‚‚ f)           â‰ˆË˜âŸ¨ pullÊ³ pâ‚-â‰ˆâ‚‚ âŸ©
                     (u âˆ˜ pâ‚ pbâ‚‚) âˆ˜ pbâ‚‚-to-can       â‰ˆâŸ¨ uâˆ˜pâ‚pbâ‚‚â‰ˆh âŸ©âˆ˜âŸ¨refl âŸ©
                     h âˆ˜ pbâ‚‚-to-can                  âˆ)


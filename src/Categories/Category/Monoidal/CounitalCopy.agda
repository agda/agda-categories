{-# OPTIONS --without-K --safe #-}

open import Level
open import Categories.Category.Core using (Category)
open import Categories.Object.Monoid using (IsMonoid)
open import Categories.Category.Monoidal.Core using (Monoidal)
open import Categories.Category.Monoidal.Symmetric using (Symmetric)
open import Data.Product using (_,_)

import Categories.Category.Monoidal.Properties
import Categories.Category.Monoidal.Utilities as MonoidalUtils
import Categories.Category.Monoidal.Braided.Properties as BraidedProps


-- Counital copy categories as described by Cockett & Lack in "Restriction categories III"

module Categories.Category.Monoidal.CounitalCopy where
  record CounitalCopy {o â„“ e} {ğ’ : Category o â„“ e} {monoidal : Monoidal ğ’} (symmetric : Symmetric monoidal) : Set (suc (o âŠ” â„“ âŠ” e)) where
    open Category ğ’
    open Symmetric symmetric
    open BraidedProps braided using () renaming (module Shorthands to BraidedShorthands)
    open BraidedShorthands using (Ïƒâ‡’)
    open MonoidalUtils monoidal using (module Shorthands)
    open Shorthands
    open Categories.Category.Monoidal.Properties monoidal using (monoidal-Op)

    field
      isComonoid : âˆ€ X â†’ IsMonoid (monoidal-Op) X


    Î” : âˆ€ {X} â†’ X â‡’ X âŠ—â‚€ X
    Î” {X} = IsMonoid.Î¼ (isComonoid X)
    Î´ : âˆ€ {X} â†’ X â‡’ unit
    Î´ {X} = IsMonoid.Î· (isComonoid X)

    field
      natural : âˆ€ {A B} (f : A â‡’ B) â†’ Î” âˆ˜ f â‰ˆ (f âŠ—â‚ f) âˆ˜ Î”
      inverseâ‚ : Î” {unit} âˆ˜ Î»â‡’ â‰ˆ id
      inverseâ‚‚ : Î»â‡’ âˆ˜ Î” {unit} â‰ˆ id
      cocommutative : âˆ€ {A} â†’ Ïƒâ‡’ âˆ˜ Î” â‰ˆ Î” {A}
      preserves : âˆ€ {X Y} â†’ Î±â‡ âˆ˜ (id âŠ—â‚ Î±â‡’) âˆ˜ (id âŠ—â‚ ((Ïƒâ‡’ âŠ—â‚ id) âˆ˜ Î±â‡)) âˆ˜ Î±â‡’ âˆ˜ (Î” âŠ—â‚ Î”) â‰ˆ Î” {X âŠ—â‚€ Y}
    
    module _ {X : Obj} where
      open IsMonoid (isComonoid X) hiding (Î¼; Î·) renaming (assoc to Î”-assoc; identityË¡ to Î´-identityË¡; identityÊ³ to Î´-identityÊ³) public

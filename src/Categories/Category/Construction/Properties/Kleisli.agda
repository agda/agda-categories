{-# OPTIONS --without-K --safe #-}
module Categories.Category.Construction.Properties.Kleisli where

open import Level
import Relation.Binary.PropositionalEquality as â‰¡

open import Categories.Adjoint
open import Categories.Adjoint.Properties
open import Categories.Category
open import Categories.Functor using (Functor; _âˆ˜F_)
open import Categories.Functor.Equivalence
open import Categories.Monad
import Categories.Morphism.Reasoning as MR

open import Categories.Adjoint.Construction.Kleisli
open import Categories.Category.Construction.Kleisli

private
  variable
    o â„“ e : Level
    ğ’ ğ’Ÿ : Category o â„“ e

module _ {F : Functor ğ’ ğ’Ÿ} {G : Functor ğ’Ÿ ğ’} (FâŠ£G : Adjoint F G) where
  private
    T : Monad ğ’
    T = adjointâ‡’monad FâŠ£G

    ğ’â‚œ : Category _ _ _
    ğ’â‚œ = Kleisli T

    module ğ’ = Category ğ’
    module ğ’Ÿ = Category ğ’Ÿ
    module ğ’â‚œ = Category ğ’â‚œ


    module T = Monad T
    module F = Functor F
    module G = Functor G

    open Adjoint FâŠ£G

  -- Maclane's Comparison Functor
  ComparisonF : Functor ğ’â‚œ ğ’Ÿ
  ComparisonF = record
    { Fâ‚€ = Î» X â†’ F.Fâ‚€ X
    ; Fâ‚ = Î» {A} {B} f â†’ ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ f ]
    ; identity = zig
    ; homomorphism = Î» {X} {Y} {Z} {f} {g} â†’ begin
      ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ F.Fâ‚ (ğ’ [ ğ’ [ G.Fâ‚ (counit.Î· (F.Fâ‚€ Z)) âˆ˜ G.Fâ‚ (F.Fâ‚ g)] âˆ˜ f ])]                 â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism âŸ©
      ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ ğ’Ÿ [ F.Fâ‚ (ğ’ [ G.Fâ‚ (counit.Î· (F.Fâ‚€ Z)) âˆ˜ G.Fâ‚ (F.Fâ‚ g) ]) âˆ˜ F.Fâ‚ f ] ]          â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ F.homomorphism  âŸ©âˆ˜âŸ¨refl âŸ©
      ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ ğ’Ÿ [ ğ’Ÿ [ F.Fâ‚ (G.Fâ‚ (counit.Î· (F.Fâ‚€ Z))) âˆ˜ F.Fâ‚ (G.Fâ‚ (F.Fâ‚ g)) ] âˆ˜ F.Fâ‚ f ] ]   â‰ˆâŸ¨ centerâ»Â¹ refl refl âŸ©
      ğ’Ÿ [ ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ F.Fâ‚ (G.Fâ‚ (counit.Î· (F.Fâ‚€ Z))) ] âˆ˜ ğ’Ÿ [ F.Fâ‚ (G.Fâ‚ (F.Fâ‚ g)) âˆ˜ F.Fâ‚ f ] ]   â‰ˆâŸ¨ counit.commute (counit.Î· (F.Fâ‚€ Z)) âŸ©âˆ˜âŸ¨refl âŸ©
      ğ’Ÿ [ ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ (counit.Î· (F.Fâ‚€ (G.Fâ‚€ (F.Fâ‚€ Z)))) ] âˆ˜ ğ’Ÿ [ F.Fâ‚ (G.Fâ‚ (F.Fâ‚ g)) âˆ˜ F.Fâ‚ f ] ] â‰ˆâŸ¨ extendÂ² (counit.commute (F.Fâ‚ g))  âŸ©
      ğ’Ÿ [ ğ’Ÿ [ counit.Î· (F.Fâ‚€ Z) âˆ˜ F.Fâ‚ g ] âˆ˜ ğ’Ÿ [ counit.Î· (F.Fâ‚€ Y) âˆ˜ F.Fâ‚ f ] ]                               âˆ
    ; F-resp-â‰ˆ = Î» eq â†’ ğ’Ÿ.âˆ˜-resp-â‰ˆÊ³ (F.F-resp-â‰ˆ eq)
    }
    where
      open ğ’Ÿ.HomReasoning
      open ğ’Ÿ.Equiv
      open MR ğ’Ÿ

  private
    L = ComparisonF
    module L = Functor L
    module Gâ‚œ = Functor (Forgetful T)
    module Fâ‚œ = Functor (Free T)

  Gâˆ˜Lâ‰¡Forgetful : (G âˆ˜F L) â‰¡F Forgetful T
  Gâˆ˜Lâ‰¡Forgetful = record
    { eqâ‚€ = Î» X â†’ â‰¡.refl
    ; eqâ‚ = Î» {A} {B} f â†’ begin
      ğ’ [ ğ’.id âˆ˜ G.Fâ‚ (ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ f ]) ]        â‰ˆâŸ¨ ğ’.identityË¡ âŸ©
      G.Fâ‚ (ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ f ])                      â‰ˆâŸ¨ G.homomorphism âŸ©
      ğ’ [ G.Fâ‚ (counit.Î· (F.Fâ‚€ B)) âˆ˜ G.Fâ‚ (F.Fâ‚ f) ]               â‰ˆË˜âŸ¨ ğ’.identityÊ³ âŸ©
      ğ’ [ ğ’ [ G.Fâ‚ (counit.Î· (F.Fâ‚€ B)) âˆ˜ G.Fâ‚ (F.Fâ‚ f) ] âˆ˜ ğ’.id ] âˆ

    }
    where
      open ğ’.HomReasoning

  Lâˆ˜Freeâ‰¡F : (L âˆ˜F Free T) â‰¡F F
  Lâˆ˜Freeâ‰¡F = record
    { eqâ‚€ = Î» X â†’ â‰¡.refl
    ; eqâ‚ = Î» {A} {B} f â†’ begin
      ğ’Ÿ [ ğ’Ÿ.id âˆ˜ ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ (ğ’ [ unit.Î· B âˆ˜ f ]) ] ] â‰ˆâŸ¨ ğ’Ÿ.identityË¡ âŸ©
      ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ (ğ’ [ unit.Î· B âˆ˜ f ]) ]               â‰ˆâŸ¨ pushÊ³ F.homomorphism âŸ©
      ğ’Ÿ [ ğ’Ÿ [ counit.Î· (F.Fâ‚€ B) âˆ˜ F.Fâ‚ (unit.Î· B) ] âˆ˜ F.Fâ‚ f ]          â‰ˆâŸ¨ elimË¡ zig âŸ©
      F.Fâ‚ f                                                              â‰ˆË˜âŸ¨ ğ’Ÿ.identityÊ³ âŸ©
      ğ’Ÿ [ F.Fâ‚ f âˆ˜ ğ’Ÿ.id ]                                               âˆ
    }
    where
      open ğ’Ÿ.HomReasoning
      open MR ğ’Ÿ

{-# OPTIONS --without-K --safe #-}

open import Categories.Category

-- we use duality to prove properties about coequalizer
module Categories.Diagram.Coequalizer.Properties {o â„“ e} (ğ’ : Category o â„“ e) where

open Category ğ’

open import Categories.Diagram.Coequalizer ğ’ using (Coequalizer; IsCoequalizer; Coequalizerâ‡’Epi; up-to-iso)
open import Categories.Morphism ğ’ using (_RetractOf_; _â‰…_)
open _â‰…_
import Categories.Morphism.Reasoning as MR
open import Categories.Diagram.Equalizer op using (Equalizer)
open import Categories.Diagram.Equalizer.Properties op using (section-equalizer)
open import Categories.Diagram.Duality ğ’ using (Coequalizerâ‡’coEqualizer; IscoEqualizerâ‡’IsCoequalizer)
open import Categories.Diagram.KernelPair ğ’ using (KernelPair)
open import Categories.Diagram.Pullback ğ’ using (Pullback; IsPullback)
open import Categories.Morphism.Regular ğ’ using (RegularEpi)


import Relation.Binary.Reasoning.Setoid as SR

open Pullback hiding (universal; unique)

private
  variable
    A B : Obj
    f g : A â‡’ B

module _ (coe : Coequalizer f g) where
  open Coequalizer coe

  private
    equalizer : Equalizer f g
    equalizer = Coequalizerâ‡’coEqualizer coe

  open Equalizer equalizer
    using (uniqueâ€²; unique-diagram)
    renaming ( id-equalize      to id-coequalize
             ; equalize-resp-â‰ˆ  to coequalize-resp-â‰ˆ
             ; equalize-resp-â‰ˆâ€² to coequalize-resp-â‰ˆâ€²
             )
    public

-- a regular epi is a coequalizer of its kernel pair
regular-is-coeq-kp : {A B : Obj} (f : A â‡’ B) â†’ RegularEpi f â†’ (kp : KernelPair f) â†’ IsCoequalizer (pâ‚ kp) (pâ‚‚ kp) f
regular-is-coeq-kp {A} {B} f record { C = D ; h = h ; g = g ; coequalizer = coeq } kp = record
  { equality   = IsPullback.commute (isPullback kp)
  ; coequalize = Î» {_}{u} uâˆ˜pâ‚â‰ˆuâˆ˜pâ‚‚ â†’ coequalize (uâˆ˜hâ‰ˆuâˆ˜g u uâˆ˜pâ‚â‰ˆuâˆ˜pâ‚‚)
  ; universal  = universal
  ; unique     = unique
  }

  where
    open IsCoequalizer coeq
    pb-univ : D â‡’ P kp
    pb-univ = IsPullback.universal (isPullback kp) equality

    uâˆ˜hâ‰ˆuâˆ˜g : {X : Obj} â†’ (u : A â‡’ X) â†’ u âˆ˜ (pâ‚ kp) â‰ˆ u âˆ˜ (pâ‚‚ kp) â†’ u âˆ˜ h â‰ˆ u âˆ˜ g
    uâˆ˜hâ‰ˆuâˆ˜g {X} u uâˆ˜pâ‚â‰ˆuâˆ˜pâ‚‚ = begin
      u âˆ˜ h                   â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ pâ‚âˆ˜universalâ‰ˆhâ‚ kp âŸ©
      u âˆ˜ (pâ‚ kp  âˆ˜ pb-univ)  â‰ˆâŸ¨ pullË¡ uâˆ˜pâ‚â‰ˆuâˆ˜pâ‚‚ âŸ©
      (u âˆ˜ pâ‚‚ kp) âˆ˜ pb-univ   â‰ˆâŸ¨ pullÊ³ (pâ‚‚âˆ˜universalâ‰ˆhâ‚‚ kp) âŸ©
      u âˆ˜ g                   âˆ
      where
        open HomReasoning
        open MR ğ’

retract-coequalizer : âˆ€ {X Y} {f : Y â‡’ X} {g : X â‡’ Y} â†’ f RetractOf g â†’ IsCoequalizer (g âˆ˜ f) id f
retract-coequalizer fâˆ˜gâ‰ˆid = IscoEqualizerâ‡’IsCoequalizer (section-equalizer fâˆ˜gâ‰ˆid)


module SplitCoequalizer {A B C : Obj} {f g : A â‡’ B} {e : B â‡’ C}
  (t : B â‡’ A) (s : C â‡’ B) (eq : e âˆ˜ f â‰ˆ e âˆ˜ g) where
  -- split coequalizer are coequalizer --
  splitCoequalizerâ‡’Coequalizer : (tisSection : f âˆ˜ t â‰ˆ id)
                                 (sisSection : e âˆ˜ s â‰ˆ id)
                                 (sq : s âˆ˜ e â‰ˆ g âˆ˜ t)
                               â†’ IsCoequalizer f g e
  splitCoequalizerâ‡’Coequalizer tisSection sisSection sq = record
    { equality = eq
    ; coequalize = Î» {_} {h} _ â†’ h âˆ˜ s
    ; universal = Î» {_} {h} {hâˆ˜fâ‰ˆhâˆ˜g} â†’ begin
      h           â‰ˆâŸ¨ introÊ³ tisSection âŸ©
      h âˆ˜ f âˆ˜ t   â‰ˆâŸ¨ extendÊ³ hâˆ˜fâ‰ˆhâˆ˜g âŸ©
      h âˆ˜ g âˆ˜ t   â‰ˆâŸ¨ pushÊ³ (âŸº sq) âŸ©
      (h âˆ˜ s) âˆ˜ e âˆ
    ; unique = Î» {_} {h} {i} {hâˆ˜fâ‰ˆhâˆ˜g} hâ‰ˆiâˆ˜e â†’ begin
      i         â‰ˆâŸ¨ introÊ³ sisSection âŸ©
      i âˆ˜ e âˆ˜ s â‰ˆâŸ¨ pullË¡ (âŸº hâ‰ˆiâˆ˜e) âŸ©
      h âˆ˜ s     âˆ
    }
    where
      open HomReasoning
      open MR ğ’

  splitCoequalizerâ‡’Coequalizer-sym : (tisSection : g âˆ˜ t â‰ˆ id)
                                     (sisSection : e âˆ˜ s â‰ˆ id)
                                     (sq : s âˆ˜ e â‰ˆ f âˆ˜ t)
                                   â†’ IsCoequalizer f g e
  splitCoequalizerâ‡’Coequalizer-sym tisSection sisSection sq = record
    { equality = eq
    ; coequalize = Î» {_} {h} _ â†’ h âˆ˜ s
    ; universal = Î» {_} {h} {hâˆ˜fâ‰ˆhâˆ˜g} â†’ begin
      h           â‰ˆâŸ¨ introÊ³ tisSection âŸ©
      h âˆ˜ g âˆ˜ t   â‰ˆâŸ¨ extendÊ³ (âŸº hâˆ˜fâ‰ˆhâˆ˜g) âŸ©
      h âˆ˜ f âˆ˜ t   â‰ˆâŸ¨ pushÊ³ (âŸº sq) âŸ©
      (h âˆ˜ s) âˆ˜ e âˆ
    ; unique = Î» {_} {h} {i} {hâˆ˜fâ‰ˆhâˆ˜g} hâ‰ˆiâˆ˜e â†’ begin
      i         â‰ˆâŸ¨ introÊ³ sisSection âŸ©
      i âˆ˜ e âˆ˜ s â‰ˆâŸ¨ pullË¡ (âŸº hâ‰ˆiâˆ˜e) âŸ©
      h âˆ˜ s     âˆ
    }
    where
      open HomReasoning
      open MR ğ’

open SplitCoequalizer public

open Categories.Category.Definitions ğ’ using (CommutativeSquare)

module MapBetweenCoequalizers
  {Aâ‚ Bâ‚ Aâ‚‚ Bâ‚‚ : Obj} {fâ‚ gâ‚ : Aâ‚ â‡’ Bâ‚} {fâ‚‚ gâ‚‚ : Aâ‚‚ â‡’ Bâ‚‚}
  (Î± : Aâ‚ â‡’ Aâ‚‚) (Î² : Bâ‚ â‡’ Bâ‚‚)
  (sqâ‚ : CommutativeSquare Î± fâ‚ fâ‚‚ Î²)                -- fâ‚‚ âˆ˜ Î± â‰ˆ Î² âˆ˜ fâ‚
  (sqâ‚‚ : CommutativeSquare Î± gâ‚ gâ‚‚ Î²)                -- gâ‚‚ âˆ˜ Î± â‰ˆ Î² âˆ˜ gâ‚
  where
  open Coequalizer

  â‡’coequalize : (coeqâ‚‚ : Coequalizer fâ‚‚ gâ‚‚) â†’ (arr coeqâ‚‚ âˆ˜ Î²) âˆ˜ fâ‚ â‰ˆ (arr coeqâ‚‚ âˆ˜ Î²) âˆ˜ gâ‚
  â‡’coequalize coeqâ‚‚ = begin
    (arr coeqâ‚‚ âˆ˜ Î²) âˆ˜ fâ‚ â‰ˆâŸ¨ extendË¡ (âŸº sqâ‚) âŸ©
    (arr coeqâ‚‚ âˆ˜ fâ‚‚) âˆ˜ Î± â‰ˆâŸ¨ equality coeqâ‚‚ âŸ©âˆ˜âŸ¨refl âŸ©
    (arr coeqâ‚‚ âˆ˜ gâ‚‚) âˆ˜ Î± â‰ˆâŸ¨ extendË¡ sqâ‚‚ âŸ©
    (arr coeqâ‚‚ âˆ˜ Î²) âˆ˜ gâ‚ âˆ
    where
      open HomReasoning
      open MR ğ’

  â‡’MapBetweenCoeq : (coeqâ‚ : Coequalizer fâ‚ gâ‚)
                  â†’ (coeqâ‚‚ : Coequalizer fâ‚‚ gâ‚‚)
                  â†’ obj coeqâ‚ â‡’ obj coeqâ‚‚
  â‡’MapBetweenCoeq coeqâ‚ coeqâ‚‚ = coequalize coeqâ‚ (â‡’coequalize coeqâ‚‚)
    where
      open HomReasoning

  â‡’MapBetweenCoeqSq : (coeqâ‚ : Coequalizer fâ‚ gâ‚)
                    â†’ (coeqâ‚‚ : Coequalizer fâ‚‚ gâ‚‚)
                    â†’ CommutativeSquare
                        Î² (arr coeqâ‚)
                        (arr coeqâ‚‚) (â‡’MapBetweenCoeq coeqâ‚ coeqâ‚‚)
  â‡’MapBetweenCoeqSq coeqâ‚ coeqâ‚‚ = universal coeqâ‚

open MapBetweenCoequalizers public

CoeqOfIsomorphicDiagram : {A B : Obj} {f g : A â‡’ B} (coeq : Coequalizer f g )
                        â†’ {A' B' : Obj}
                        â†’ (a : A â‰… A') (b : B â‰… B')
                        â†’ Coequalizer (from b âˆ˜ f âˆ˜ to a) (from b âˆ˜ g âˆ˜ to a)
CoeqOfIsomorphicDiagram {f = f} {g} coeq {A'} {B'} a b = record
  { arr = arr âˆ˜ to b
  ; isCoequalizer = record
    { equality = begin
        (arr âˆ˜ to b) âˆ˜ from b âˆ˜ f âˆ˜ to a â‰ˆâŸ¨ assocÂ²Î³Î² âŸ©
        (arr âˆ˜ to b âˆ˜ from b) âˆ˜ f âˆ˜ to a â‰ˆâŸ¨ elimÊ³ (isoË¡ b) âŸ©âˆ˜âŸ¨refl âŸ©
        arr âˆ˜ f âˆ˜ to a                   â‰ˆâŸ¨ extendÊ³ equality âŸ©
        arr âˆ˜ g âˆ˜ to a                   â‰ˆâŸ¨ introÊ³ (isoË¡ b) âŸ©âˆ˜âŸ¨refl âŸ©
        (arr âˆ˜ to b âˆ˜ from b) âˆ˜ g âˆ˜ to a â‰ˆâŸ¨ assocÂ²Î²Î³ âŸ©
        (arr âˆ˜ to b) âˆ˜ from b âˆ˜ g âˆ˜ to a âˆ
    ; coequalize = coequalize'
    ; universal =  Î» {_} {h} {eq} â†’ begin
        h                             â‰ˆâŸ¨ switch-fromtoÊ³ b universal âŸ©
        (coequalize' eq âˆ˜ arr) âˆ˜ to b â‰ˆâŸ¨ assoc âŸ©
        coequalize' eq âˆ˜ (arr âˆ˜ to b) âˆ
    ; unique = Î» {_} {h} {i} {eq} e â†’ unique (âŸº (switch-tofromÊ³ b (begin
        (i âˆ˜ arr) âˆ˜ to b â‰ˆâŸ¨ assoc âŸ©
        i âˆ˜ arr âˆ˜ to b   â‰ˆâŸ¨ âŸº e âŸ©
        h                âˆ)))
    }
  }
  where
    open Coequalizer coeq
    open HomReasoning
    open MR ğ’
    
    f' g' : A' â‡’ B'
    f' = from b âˆ˜ f âˆ˜ to a
    g' = from b âˆ˜ g âˆ˜ to a

    equalize'â‡’equalize : {C : Obj} {h : B' â‡’ C}
                         (eq : h âˆ˜ f' â‰ˆ h âˆ˜ g')
                       â†’ (h âˆ˜ from b) âˆ˜ f â‰ˆ (h âˆ˜ from b) âˆ˜ g
    equalize'â‡’equalize {_} {h} eq = cancel-toÊ³ a (begin
      ((h âˆ˜ from b) âˆ˜ f) âˆ˜ to a â‰ˆâŸ¨ assocÂ²Î±Îµ âŸ©
      h âˆ˜ f'                    â‰ˆâŸ¨ eq âŸ©
      h âˆ˜ g'                    â‰ˆâŸ¨ assocÂ²ÎµÎ± âŸ©
      ((h âˆ˜ from b) âˆ˜ g) âˆ˜ to a âˆ)

    coequalize' : {C : Obj} {h : B' â‡’ C}
                  (eq : h âˆ˜ f' â‰ˆ h âˆ˜ g')
                â†’ obj â‡’ C
    coequalize' eq = coequalize (equalize'â‡’equalize eq)


-- coequalizer commutes with coequalizer
module CoequalizerOfCoequalizer
  {A B C D : Obj} {fâ‚ fâ‚‚ : A â‡’ B} {gâ‚ gâ‚‚ : A â‡’ C} {hâ‚ hâ‚‚ : B â‡’ D} {iâ‚ iâ‚‚ : C â‡’ D}
  (coeqá¶  : Coequalizer fâ‚ fâ‚‚) (coeqáµ : Coequalizer gâ‚ gâ‚‚)
  (coeqÊ° : Coequalizer hâ‚ hâ‚‚) (coeqâ± : Coequalizer iâ‚ iâ‚‚)
  (let open Coequalizer)
  (fâ‡’iâ‚ fâ‡’iâ‚‚ : obj coeqá¶  â‡’ obj coeqâ±)
  (gâ‡’hâ‚ gâ‡’hâ‚‚ : obj coeqáµ â‡’ obj coeqÊ°)
  (sqâ‚á¶ â± : CommutativeSquare (arr coeqá¶ ) hâ‚ fâ‡’iâ‚ (arr coeqâ±))
  (sqâ‚‚á¶ â± : CommutativeSquare (arr coeqá¶ ) hâ‚‚ fâ‡’iâ‚‚ (arr coeqâ±))
  (sqâ‚áµÊ° : CommutativeSquare iâ‚ (arr coeqáµ) (arr coeqÊ°) gâ‡’hâ‚)
  (sqâ‚‚áµÊ° : CommutativeSquare iâ‚‚ (arr coeqáµ) (arr coeqÊ°) gâ‡’hâ‚‚)
  (coeqcoeqáµÊ° : Coequalizer gâ‡’hâ‚ gâ‡’hâ‚‚) where

  {-
          fâ‚â‚‚
       A ====> B ----> coeqá¶ 
       ||      ||       ||
    gâ‚â‚‚||   hâ‚â‚‚||  sqá¶ â± ||
       vv iâ‚â‚‚  vv       vv        t
       C ====> D ----> coeqâ± ----------
       |       |         |             |
       | sqáµÊ°  |  arrSq  |             |
       v       v         v             v
     coeqáµ==>coeqÊ° --> coeqcoeqáµÊ° Â·Â·Â·> T
               .               coequalize
               .                       ^
               .                       .
               .........................
                            u
  -}

  -- We construct a coequalizer of the parallel pair fâ‡’iâ‚, fâ‡’iâ‚‚
  -- its components will be called: objá¶ â±, arrá¶ â±, ...

  open HomReasoning

  objá¶ â± : Obj
  objá¶ â± = obj coeqcoeqáµÊ°

  arrá¶ â± : obj coeqâ± â‡’ objá¶ â±
  arrá¶ â± = â‡’MapBetweenCoeq (arr coeqáµ) (arr coeqÊ°) (âŸº sqâ‚áµÊ°) (âŸº sqâ‚‚áµÊ°) coeqâ± coeqcoeqáµÊ°

  abstract
    arrSq : arr coeqcoeqáµÊ° âˆ˜ arr coeqÊ° â‰ˆ arrá¶ â± âˆ˜ arr coeqâ±
    arrSq = â‡’MapBetweenCoeqSq (arr coeqáµ) (arr coeqÊ°) (âŸº sqâ‚áµÊ°) (âŸº sqâ‚‚áµÊ°) coeqâ± coeqcoeqáµÊ°

    equalityá¶ â±âˆ˜arr : (arrá¶ â± âˆ˜ fâ‡’iâ‚) âˆ˜ arr coeqá¶   â‰ˆ (arrá¶ â± âˆ˜ fâ‡’iâ‚‚) âˆ˜ arr coeqá¶ 
    equalityá¶ â±âˆ˜arr = begin
      (arrá¶ â± âˆ˜ fâ‡’iâ‚) âˆ˜ arr coeqá¶         â‰ˆâŸ¨ extendË¡ sqâ‚á¶ â± âŸ©
      (arrá¶ â± âˆ˜ arr coeqâ±) âˆ˜ hâ‚          â‰ˆâŸ¨ âŸº arrSq âŸ©âˆ˜âŸ¨refl âŸ©
      (arr coeqcoeqáµÊ° âˆ˜ arr coeqÊ°) âˆ˜ hâ‚ â‰ˆâŸ¨ extendË¡ (equality coeqÊ°) âŸ©
      (arr coeqcoeqáµÊ° âˆ˜ arr coeqÊ°) âˆ˜ hâ‚‚ â‰ˆâŸ¨ arrSq âŸ©âˆ˜âŸ¨refl âŸ©
      (arrá¶ â± âˆ˜ arr coeqâ±) âˆ˜ hâ‚‚          â‰ˆâŸ¨ extendË¡ (âŸº sqâ‚‚á¶ â±) âŸ©
      (arrá¶ â± âˆ˜ fâ‡’iâ‚‚) âˆ˜ arr coeqá¶         âˆ
      where
        open MR ğ’

    equalityá¶ â± : arrá¶ â± âˆ˜ fâ‡’iâ‚ â‰ˆ arrá¶ â± âˆ˜ fâ‡’iâ‚‚
    equalityá¶ â± = Coequalizerâ‡’Epi coeqá¶  (arrá¶ â± âˆ˜ fâ‡’iâ‚) (arrá¶ â± âˆ˜ fâ‡’iâ‚‚) equalityá¶ â±âˆ˜arr


    commutes : {T : Obj} {t : obj coeqâ± â‡’ T} (eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚)
             â†’ (t âˆ˜ arr coeqâ±) âˆ˜ hâ‚ â‰ˆ (t âˆ˜ arr coeqâ±) âˆ˜ hâ‚‚
    commutes {_} {t} eq = begin
      (t âˆ˜ arr coeqâ±) âˆ˜ hâ‚   â‰ˆâŸ¨ extendË¡ (âŸº sqâ‚á¶ â±) âŸ©
      (t âˆ˜ fâ‡’iâ‚) âˆ˜ arr coeqá¶  â‰ˆâŸ¨ eq âŸ©âˆ˜âŸ¨refl âŸ©
      (t âˆ˜ fâ‡’iâ‚‚) âˆ˜ arr coeqá¶  â‰ˆâŸ¨ extendË¡ sqâ‚‚á¶ â± âŸ©
      (t âˆ˜ arr coeqâ±) âˆ˜ hâ‚‚   âˆ
      where
        open MR ğ’
  
    u : {T : Obj} {t : obj coeqâ± â‡’ T} (eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚)
      â†’ obj coeqÊ° â‡’ T
    u eq = coequalize coeqÊ° (commutes eq)

    uEqualizesâˆ˜arr : {T : Obj} {t : obj coeqâ± â‡’ T} (eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚)
                   â†’ (u eq âˆ˜ gâ‡’hâ‚) âˆ˜ arr coeqáµ â‰ˆ (u eq âˆ˜ gâ‡’hâ‚‚) âˆ˜ arr coeqáµ
    uEqualizesâˆ˜arr {t = t} eq = begin
      (u eq âˆ˜ gâ‡’hâ‚) âˆ˜ arr coeqáµ â‰ˆâŸ¨ extendË¡ (âŸº sqâ‚áµÊ°) âŸ©
      (u eq âˆ˜ arr coeqÊ°) âˆ˜ iâ‚   â‰ˆâŸ¨ âŸº (universal coeqÊ°) âŸ©âˆ˜âŸ¨refl âŸ©
      (t âˆ˜ arr coeqâ±) âˆ˜ iâ‚      â‰ˆâŸ¨ extendË¡ (equality coeqâ±) âŸ©
      (t âˆ˜ arr coeqâ±) âˆ˜ iâ‚‚      â‰ˆâŸ¨ universal coeqÊ° âŸ©âˆ˜âŸ¨refl âŸ©
      (u eq âˆ˜ arr coeqÊ°) âˆ˜ iâ‚‚   â‰ˆâŸ¨ extendË¡ sqâ‚‚áµÊ° âŸ©
      (u eq âˆ˜ gâ‡’hâ‚‚) âˆ˜ arr coeqáµ âˆ
      where
        open MR ğ’

    uEqualizes : {T : Obj} {t : obj coeqâ± â‡’ T} (eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚)
               â†’ u eq âˆ˜ gâ‡’hâ‚ â‰ˆ u eq âˆ˜ gâ‡’hâ‚‚
    uEqualizes eq = Coequalizerâ‡’Epi coeqáµ (u eq âˆ˜ gâ‡’hâ‚) (u eq âˆ˜ gâ‡’hâ‚‚) (uEqualizesâˆ˜arr eq)

    coequalizeá¶ â± : {T : Obj} {t : obj coeqâ± â‡’ T} â†’ t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚ â†’ objá¶ â± â‡’ T
    coequalizeá¶ â± eq = coequalize coeqcoeqáµÊ° (uEqualizes eq)

    universalá¶ â±âˆ˜arr : {T : Obj} {t : obj coeqâ± â‡’ T} {eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚}
                    â†’ t âˆ˜ arr coeqâ± â‰ˆ (coequalizeá¶ â± eq âˆ˜ arrá¶ â±) âˆ˜ arr coeqâ±
    universalá¶ â±âˆ˜arr {t = t} {eq} = begin
      t âˆ˜ arr coeqâ±                                  â‰ˆâŸ¨ universal coeqÊ° âŸ©
      u eq âˆ˜ arr coeqÊ°                               â‰ˆâŸ¨ universal coeqcoeqáµÊ° âŸ©âˆ˜âŸ¨refl âŸ©
      (coequalizeá¶ â± eq âˆ˜ arr coeqcoeqáµÊ°) âˆ˜ arr coeqÊ° â‰ˆâŸ¨ extendË¡ arrSq âŸ©
      (coequalizeá¶ â± eq âˆ˜ arrá¶ â±) âˆ˜ arr coeqâ±          âˆ
      where
        open MR ğ’

    universalá¶ â± : {T : Obj} {t : obj coeqâ± â‡’ T} {eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚}
                â†’ t â‰ˆ coequalizeá¶ â± eq âˆ˜ arrá¶ â±
    universalá¶ â± {t = t} {eq} = Coequalizerâ‡’Epi coeqâ± t (coequalizeá¶ â± eq âˆ˜ arrá¶ â±) universalá¶ â±âˆ˜arr

    uniqueá¶ â±âˆ˜arrâˆ˜arr : {T : Obj} {t : obj coeqâ± â‡’ T} {i : objá¶ â± â‡’ T} {eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚}
                     â†’ t â‰ˆ i âˆ˜ arrá¶ â±
                     â†’ (i âˆ˜ arr coeqcoeqáµÊ°) âˆ˜ arr coeqÊ°
                     â‰ˆ (coequalizeá¶ â± eq  âˆ˜ arr coeqcoeqáµÊ°) âˆ˜ arr coeqÊ°
    uniqueá¶ â±âˆ˜arrâˆ˜arr {t = t} {i} {eq} factors = begin
      (i âˆ˜ arr coeqcoeqáµÊ°) âˆ˜ arr coeqÊ°                â‰ˆâŸ¨ extendË¡ arrSq âŸ©
      (i âˆ˜ arrá¶ â±) âˆ˜ arr coeqâ±                         â‰ˆâŸ¨ âŸº factors âŸ©âˆ˜âŸ¨refl âŸ©
      t âˆ˜ arr coeqâ±                                   â‰ˆâŸ¨ universalá¶ â± âŸ©âˆ˜âŸ¨refl âŸ©
      (coequalizeá¶ â± eq âˆ˜ arrá¶ â±) âˆ˜ arr coeqâ±           â‰ˆâŸ¨ extendË¡ (âŸº arrSq) âŸ©
      (coequalizeá¶ â± eq  âˆ˜ arr coeqcoeqáµÊ°) âˆ˜ arr coeqÊ° âˆ
      where
        open MR ğ’

    uniqueá¶ â± : {T : Obj} {t : obj coeqâ± â‡’ T} {i : objá¶ â± â‡’ T} {eq : t âˆ˜ fâ‡’iâ‚ â‰ˆ t âˆ˜ fâ‡’iâ‚‚}
             â†’ t â‰ˆ i âˆ˜ arrá¶ â±
             â†’ i â‰ˆ coequalizeá¶ â± eq
    uniqueá¶ â± {i = i} {eq} factors = Coequalizerâ‡’Epi coeqcoeqáµÊ° i (coequalizeá¶ â± eq) (
                                          Coequalizerâ‡’Epi coeqÊ°
                                          (i âˆ˜ arr coeqcoeqáµÊ°)
                                          (coequalizeá¶ â± eq  âˆ˜ arr coeqcoeqáµÊ°)
                                          (uniqueá¶ â±âˆ˜arrâˆ˜arr factors)
                                        )
  -- end abstract --

  coeqcoeqá¶ â± : Coequalizer fâ‡’iâ‚ fâ‡’iâ‚‚
  coeqcoeqá¶ â± = record
    { obj = objá¶ â±
    ; arr = arrá¶ â±
    ; isCoequalizer = record
      { equality = equalityá¶ â±
      ; coequalize = coequalizeá¶ â±
      ; universal = universalá¶ â±
      ; unique = uniqueá¶ â±
      }
    }

  CoeqsAreIsomorphic : (coeq : Coequalizer fâ‡’iâ‚ fâ‡’iâ‚‚) â†’ obj coeq â‰… obj coeqcoeqáµÊ°
  CoeqsAreIsomorphic coeq = up-to-iso coeq coeqcoeqá¶ â±

  -- The Isomorphism of coequalizers fits into a commutative pentagon --
  -- We need this for proving some coherences in the bicategory of monads and bimodules --
  IsoFitsInPentagon : (coeq : Coequalizer fâ‡’iâ‚ fâ‡’iâ‚‚)
                    â†’ arr coeqcoeqáµÊ° âˆ˜ arr coeqÊ°
                      â‰ˆ from (CoeqsAreIsomorphic coeq) âˆ˜ arr coeq  âˆ˜ arr coeqâ±
  IsoFitsInPentagon coeq = begin
    arr coeqcoeqáµÊ° âˆ˜ arr coeqÊ°                               â‰ˆâŸ¨ arrSq âŸ©
    arrá¶ â± âˆ˜ arr coeqâ±                                        â‰ˆâŸ¨ universal coeq âŸ©âˆ˜âŸ¨refl âŸ©
    (from (CoeqsAreIsomorphic coeq) âˆ˜ arr coeq) âˆ˜ arr coeqâ±  â‰ˆâŸ¨ assoc âŸ©
    from (CoeqsAreIsomorphic coeq) âˆ˜ arr coeq âˆ˜ arr coeqâ±    âˆ

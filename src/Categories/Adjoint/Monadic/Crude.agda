{-# OPTIONS --without-K --safe #-}

open import Categories.Adjoint
open import Categories.Category
open import Categories.Functor

-- The crude monadicity theorem.
module Categories.Adjoint.Monadic.Crude {o â„“ e oâ€² â„“â€² eâ€²} {ğ’ : Category o â„“ e} {ğ’Ÿ : Category oâ€² â„“â€² eâ€²}
                                        {L : Functor ğ’ ğ’Ÿ} {R : Functor ğ’Ÿ ğ’} (adjoint : L âŠ£ R) where

open import Level
open import Function using (_$_)

open import Categories.Adjoint.Properties
open import Categories.Adjoint.Monadic
open import Categories.NaturalTransformation.NaturalIsomorphism
open import Categories.NaturalTransformation
open import Categories.Monad

open import Categories.Diagram.Coequalizer
open import Categories.Diagram.ReflexivePair

open import Categories.Category.Construction.EilenbergMoore
open import Categories.Category.Construction.Properties.EilenbergMoore

open import Categories.Morphism
import Categories.Morphism.Reasoning as MR

private
  module L = Functor L
  module R = Functor R

  module ğ’ = Category ğ’
  module ğ’Ÿ = Category ğ’Ÿ

  module adjoint = Adjoint adjoint

  T : Monad ğ’
  T = adjointâ‡’monad adjoint

  ğ’áµ€ : Category _ _ _
  ğ’áµ€ = EilenbergMoore T

  Comparison : Functor ğ’Ÿ ğ’áµ€
  Comparison = ComparisonF adjoint

  module Comparison = Functor Comparison

  open Coequalizer

-- If ğ’Ÿ has coequalizers of reflexive pairs, then the comparison functor has a left adjoint.
module _ (has-reflexive-coequalizers : âˆ€ {A B} {f g : ğ’Ÿ [ A , B ]} â†’ ReflexivePair ğ’Ÿ f g â†’ Coequalizer ğ’Ÿ f g) where

  -- The key part of the proof. As we have all reflexive coequalizers, we can create the following coequalizer.
  -- We can think of this as identifying the action of the algebra lifted to a "free" structure
  -- and the counit of the adjunction, as the unit of the adjunction (also lifted to the "free structure") is a section of both.
  coeq : (M : Module T) â†’ Coequalizer ğ’Ÿ (L.Fâ‚ (Module.action M)) (adjoint.counit.Î· (L.â‚€ (Module.A M)))
  coeq M = has-reflexive-coequalizers record
    { s = L.Fâ‚ (adjoint.unit.Î· (Module.A M))
    ; isReflexivePair = record
      { sectionâ‚— = begin
        ğ’Ÿ [ L.Fâ‚ (Module.action M) âˆ˜ L.Fâ‚ (adjoint.unit.Î· (Module.A M)) ] â‰ˆË˜âŸ¨ L.homomorphism âŸ©
        L.Fâ‚ (ğ’ [ Module.action M âˆ˜ adjoint.unit.Î· (Module.A M) ] )       â‰ˆâŸ¨ L.F-resp-â‰ˆ (Module.identity M) âŸ©
        L.Fâ‚ ğ’.id                                                         â‰ˆâŸ¨ L.identity âŸ©
        ğ’Ÿ.id                                                              âˆ
      ; sectionáµ£ = begin
        ğ’Ÿ [ adjoint.counit.Î· (L.â‚€ (Module.A M)) âˆ˜ L.Fâ‚ (adjoint.unit.Î· (Module.A M)) ] â‰ˆâŸ¨ adjoint.zig âŸ©
        ğ’Ÿ.id âˆ
      }
    }
      where
      open ğ’Ÿ.HomReasoning

  -- If we have coequalizers of reflexive pairs, then we can define an "inverse" to the comparison functor.
  Comparisonâ»Â¹ : Functor ğ’áµ€ ğ’Ÿ
  Comparisonâ»Â¹ = record
    { Fâ‚€ = Î» M â†’ obj (coeq M)
    ; Fâ‚ = Î» {M} {N} Î± â†’ coequalize (coeq M) $ begin
      ğ’Ÿ [ ğ’Ÿ [ arr (coeq N) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr Î±) ] âˆ˜ L.Fâ‚ (Module.action M) ]                             â‰ˆâŸ¨ pullÊ³ (âŸº L.homomorphism) âŸ©
      ğ’Ÿ [ arr (coeq N) âˆ˜ L.Fâ‚ (ğ’ [ Moduleâ‡’.arr Î± âˆ˜ Module.action M ]) ]                                    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ L.F-resp-â‰ˆ (Moduleâ‡’.commute Î±) âŸ©
      ğ’Ÿ [ arr (coeq N) âˆ˜ L.Fâ‚ (ğ’ [ Module.action N âˆ˜ R.Fâ‚ (L.Fâ‚ (Moduleâ‡’.arr Î±)) ]) ]                      â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ L.homomorphism âŸ©
      ğ’Ÿ [ arr (coeq N) âˆ˜ ğ’Ÿ [ L.Fâ‚ (Module.action N) âˆ˜ L.Fâ‚ (R.Fâ‚ (L.Fâ‚ (Moduleâ‡’.arr Î±))) ] ]               â‰ˆâŸ¨ pullË¡ (equality (coeq N)) âŸ©
      ğ’Ÿ [ ğ’Ÿ [ arr (coeq N) âˆ˜ adjoint.counit.Î· (L.Fâ‚€ (Module.A N)) ] âˆ˜ L.Fâ‚ (R.Fâ‚ (L.Fâ‚ (Moduleâ‡’.arr Î±))) ] â‰ˆâŸ¨ extendË¡ (adjoint.counit.commute (L.Fâ‚ (Moduleâ‡’.arr Î±))) âŸ©
      ğ’Ÿ [ ğ’Ÿ [ arr (coeq N) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr Î±) ] âˆ˜ adjoint.counit.Î· (L.â‚€ (Module.A M)) ]                âˆ
    ; identity = Î» {A} â†’ âŸº $ unique (coeq A) $ begin
      ğ’Ÿ [ arr (coeq A) âˆ˜ L.Fâ‚ ğ’.id ] â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ L.identity âŸ©
      ğ’Ÿ [ arr (coeq A) âˆ˜ ğ’Ÿ.id ]      â‰ˆâŸ¨ id-comm âŸ©
      ğ’Ÿ [ ğ’Ÿ.id âˆ˜ arr (coeq A) ]      âˆ
    ; homomorphism = Î» {X} {Y} {Z} {f} {g} â†’ âŸº $ unique (coeq X) $ begin
      ğ’Ÿ [ arr (coeq Z) âˆ˜ L.Fâ‚ (ğ’ [ Moduleâ‡’.arr g âˆ˜ Moduleâ‡’.arr f ]) ]        â‰ˆâŸ¨ ğ’Ÿ.âˆ˜-resp-â‰ˆÊ³ L.homomorphism â—‹ ğ’Ÿ.sym-assoc âŸ©
      ğ’Ÿ [ ğ’Ÿ [ arr (coeq Z) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr g) ] âˆ˜ L.Fâ‚ (Moduleâ‡’.arr f) ] â‰ˆâŸ¨ universal (coeq Y) âŸ©âˆ˜âŸ¨refl âŸ©
      ğ’Ÿ [ ğ’Ÿ [ coequalize (coeq Y) _ âˆ˜ arr (coeq Y) ] âˆ˜ L.Fâ‚ (Moduleâ‡’.arr f) ] â‰ˆâŸ¨ extendË¡ (universal (coeq X)) âŸ©
      ğ’Ÿ [ ğ’Ÿ [ coequalize (coeq Y) _ âˆ˜ coequalize (coeq X) _ ] âˆ˜ arr (coeq X) ] âˆ
    ; F-resp-â‰ˆ = Î» {A} {B} {f} {g} eq â†’ unique (coeq A) $ begin
      ğ’Ÿ [ arr (coeq B) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr g) ] â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ L.F-resp-â‰ˆ (ğ’.Equiv.sym eq) âŸ©
      ğ’Ÿ [ arr (coeq B) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr f) ] â‰ˆâŸ¨ universal (coeq A) âŸ©
      ğ’Ÿ [ coequalize (coeq A) _ âˆ˜ arr (coeq A) ] âˆ
    }
    where
      open ğ’Ÿ.HomReasoning
      open MR ğ’Ÿ

  module Comparisonâ»Â¹ = Functor Comparisonâ»Â¹

  -- If ğ’Ÿ has reflexive coequalizers, then the "inverse" to the comparison functor is actually adjoint.
  Comparisonâ»Â¹âŠ£Comparison : Comparisonâ»Â¹ âŠ£ Comparison
  Adjoint.unit Comparisonâ»Â¹âŠ£Comparison = ntHelper record
    { Î· = Î» M â†’ record
      { arr = ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ adjoint.unit.Î· (Module.A M) ]
      ; commute = begin
        ğ’ [ ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ adjoint.unit.Î· (Module.A M) ] âˆ˜ Module.action M ]                                          â‰ˆâŸ¨ pullÊ³ (adjoint.unit.commute (Module.action M)) âŸ©
        -- It would be nice to have a reasoning combinator doing this "âŸº homomorphism ... homomorphism" pattern
        ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ ğ’ [ R.Fâ‚ (L.Fâ‚ (Module.action M)) âˆ˜ adjoint.unit.Î· (R.Fâ‚€ (L.Fâ‚€ (Module.A M))) ] ]              â‰ˆâŸ¨ pullË¡ (âŸº R.homomorphism) âŸ©
        ğ’ [ R.Fâ‚ (ğ’Ÿ [ arr (coeq M) âˆ˜ L.Fâ‚ (Module.action M) ]) âˆ˜ adjoint.unit.Î· (R.Fâ‚€ (L.Fâ‚€ (Module.A M))) ]                     â‰ˆâŸ¨ (R.F-resp-â‰ˆ (equality (coeq M)) âŸ©âˆ˜âŸ¨refl) âŸ©
        ğ’ [ R.Fâ‚ (ğ’Ÿ [ arr (coeq M) âˆ˜ adjoint.counit.Î· (L.Fâ‚€ (Module.A M)) ]) âˆ˜ adjoint.unit.Î· (R.Fâ‚€ (L.Fâ‚€ (Module.A M))) ]       â‰ˆâŸ¨ (R.homomorphism âŸ©âˆ˜âŸ¨refl) âŸ©
        ğ’ [ ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ R.Fâ‚ (adjoint.counit.Î· (L.Fâ‚€ (Module.A M))) ] âˆ˜ adjoint.unit.Î· (R.Fâ‚€ (L.Fâ‚€ (Module.A M))) ] â‰ˆâŸ¨ cancelÊ³ adjoint.zag âŸ©
        -- FIXME Use something like cancel here
        R.Fâ‚ (arr (coeq M))                                                                                                        â‰ˆË˜âŸ¨ R.F-resp-â‰ˆ (ğ’Ÿ.identityÊ³) âŸ©
        R.Fâ‚ (ğ’Ÿ [ arr (coeq M) âˆ˜ ğ’Ÿ.id ])                                                                                          â‰ˆË˜âŸ¨ R.F-resp-â‰ˆ (ğ’Ÿ.âˆ˜-resp-â‰ˆÊ³ adjoint.zig) âŸ©
        R.Fâ‚ (ğ’Ÿ [ arr (coeq M) âˆ˜ ğ’Ÿ [ adjoint.counit.Î· (L.Fâ‚€ (Module.A M)) âˆ˜ L.Fâ‚ (adjoint.unit.Î· (Module.A M)) ] ])               â‰ˆâŸ¨ R.F-resp-â‰ˆ (MR.extendÊ³ ğ’Ÿ (adjoint.counit.sym-commute (arr (coeq M)))) âŸ©
        R.Fâ‚ (ğ’Ÿ [ adjoint.counit.Î· (obj (coeq M)) âˆ˜ ğ’Ÿ [ L.Fâ‚ (R.Fâ‚ (arr (coeq M))) âˆ˜ L.Fâ‚ (adjoint.unit.Î· (Module.A M)) ] ])      â‰ˆË˜âŸ¨ R.F-resp-â‰ˆ (ğ’Ÿ.âˆ˜-resp-â‰ˆÊ³ L.homomorphism) âŸ©
        R.Fâ‚ (ğ’Ÿ [ adjoint.counit.Î· (obj (coeq M)) âˆ˜ L.Fâ‚ (ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ adjoint.unit.Î· (Module.A M)])])               â‰ˆâŸ¨ R.homomorphism âŸ©
        ğ’ [ R.Fâ‚ (adjoint.counit.Î· (obj (coeq M))) âˆ˜ R.Fâ‚ (L.Fâ‚ (ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ adjoint.unit.Î· (Module.A M)])) ]       âˆ
      }
    ; commute = Î» {M} {N} f â†’ begin
      ğ’ [ ğ’ [ R.Fâ‚ (arr (coeq N)) âˆ˜ adjoint.unit.Î· (Module.A N) ] âˆ˜ Moduleâ‡’.arr f ]               â‰ˆâŸ¨ extendË¡ (adjoint.unit.commute (Moduleâ‡’.arr f)) âŸ©
      ğ’ [ ğ’ [ R.Fâ‚ (arr (coeq N)) âˆ˜ R.Fâ‚ (L.Fâ‚ (Moduleâ‡’.arr f)) ] âˆ˜ adjoint.unit.Î· (Module.A M) ] â‰ˆË˜âŸ¨ R.homomorphism âŸ©âˆ˜âŸ¨refl âŸ©
      ğ’ [ R.Fâ‚ (ğ’Ÿ [ arr (coeq N) âˆ˜ L.Fâ‚ (Moduleâ‡’.arr f) ]) âˆ˜ adjoint.unit.Î· (Module.A M) ]        â‰ˆâŸ¨ R.F-resp-â‰ˆ (universal (coeq M)) âŸ©âˆ˜âŸ¨refl âŸ©
      ğ’ [ R.Fâ‚ (ğ’Ÿ [ (coequalize (coeq M) _) âˆ˜ (arr (coeq M)) ]) âˆ˜ adjoint.unit.Î· (Module.A M) ]    â‰ˆâŸ¨ pushË¡ R.homomorphism âŸ©
      ğ’ [ R.Fâ‚ (coequalize (coeq M) _) âˆ˜ ğ’ [ R.Fâ‚ (arr (coeq M)) âˆ˜ adjoint.unit.Î· (Module.A M) ] ] âˆ
    }
    where
      open ğ’.HomReasoning
      open MR ğ’
  Adjoint.counit Comparisonâ»Â¹âŠ£Comparison = ntHelper record
    { Î· = Î» X â†’ coequalize (coeq (Comparison.Fâ‚€ X)) (adjoint.counit.commute (adjoint.counit.Î· X))
    ; commute = Î» {X} {Y} f â†’ begin
      ğ’Ÿ [ coequalize (coeq (Comparison.Fâ‚€ Y)) _ âˆ˜ coequalize (coeq (Comparison.Fâ‚€ X)) _ ]        â‰ˆâŸ¨ unique (coeq (Comparison.Fâ‚€ X)) (adjoint.counit.sym-commute f â—‹ pushË¡ (universal (coeq (Comparison.Fâ‚€ Y))) â—‹ pushÊ³ (universal (coeq (Comparison.Fâ‚€ X)))) âŸ©
      coequalize (coeq (Comparison.Fâ‚€ X)) (extendË¡ (adjoint.counit.commute (adjoint.counit.Î· X))) â‰ˆË˜âŸ¨ unique (coeq (Comparison.Fâ‚€ X)) (pushÊ³ (universal (coeq (Comparison.Fâ‚€ X)))) âŸ©
      ğ’Ÿ [ f âˆ˜ coequalize (coeq (Comparison.Fâ‚€ X)) _ ]                                            âˆ
    }
    where
      open ğ’Ÿ.HomReasoning
      open MR ğ’Ÿ
  Adjoint.zig Comparisonâ»Â¹âŠ£Comparison {X} = begin
    ğ’Ÿ [ coequalize (coeq (Comparison.Fâ‚€ (Comparisonâ»Â¹.Fâ‚€ X))) _ âˆ˜ coequalize (coeq X) _ ] â‰ˆâŸ¨ unique (coeq X) (âŸº adjoint.RLadjunctâ‰ˆid â—‹ pushË¡ (universal (coeq (Comparison.Fâ‚€ (Comparisonâ»Â¹.Fâ‚€ X)))) â—‹ pushÊ³ (universal (coeq X))) âŸ©
    coequalize (coeq X) {h = arr (coeq X)} (equality (coeq X))                             â‰ˆË˜âŸ¨ unique (coeq X) (âŸº ğ’Ÿ.identityË¡) âŸ©
    ğ’Ÿ.id                                                                                  âˆ
    where
      open ğ’Ÿ.HomReasoning
      open MR ğ’Ÿ
  Adjoint.zag Comparisonâ»Â¹âŠ£Comparison {A} = begin
    ğ’ [ R.Fâ‚ (coequalize (coeq (Comparison.Fâ‚€ A)) _) âˆ˜  ğ’ [ R.Fâ‚ (arr (coeq (Comparison.Fâ‚€ A))) âˆ˜ adjoint.unit.Î· (Module.A (Comparison.Fâ‚€ A)) ] ] â‰ˆâŸ¨ pullË¡ (âŸº R.homomorphism) âŸ©
    ğ’ [ R.Fâ‚ (ğ’Ÿ [ coequalize (coeq (Comparison.Fâ‚€ A)) _ âˆ˜ arr (coeq (Comparison.Fâ‚€ A)) ]) âˆ˜ adjoint.unit.Î· (Module.A (Comparison.Fâ‚€ A)) ]         â‰ˆË˜âŸ¨ R.F-resp-â‰ˆ (universal (coeq (Comparison.Fâ‚€ A))) âŸ©âˆ˜âŸ¨refl âŸ©
    ğ’ [ R.Fâ‚ (adjoint.counit.Î· A) âˆ˜ adjoint.unit.Î· (R.Fâ‚€ A) ]                                                                                      â‰ˆâŸ¨ adjoint.zag âŸ©
    ğ’.id                                                                                                                                           âˆ
    where
      open ğ’.HomReasoning
      open MR ğ’

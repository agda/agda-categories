{-# OPTIONS --without-K --safe #-}

-- The 'original' version of Lawvere Theory, based on
-- Nat^op and IOO functors. Contrast with the weak version at
-- https://ncatlab.org/nlab/show/Lawvere+theory
-- Unfortunately, many results on the weak version are not in
-- the literature, so doing that development would be new research.

module Categories.Theory.Lawvere where

open import Data.Nat using (‚Ñï)
open import Level

open import Categories.Category.Cartesian using (Cartesian)
open import Categories.Category.Cartesian.Bundle using (CartesianCategory)
import Categories.Category.Core as Cat
open import Categories.Category.Instance.Nat using (Nat; Natop-Cartesian)
open import Categories.Category.Instance.Setoids
open import Categories.Category.Monoidal.Instance.Setoids using (Setoids-CartesianCategory)
open import Categories.Category.Unbundled using (Category)
open import Categories.Category.Unbundled.Properties using (pack‚Ä≤; unpack‚Ä≤)
open import Categories.Functor using (Functor; _‚àòF_) renaming (id to idF)
open import Categories.Functor.Cartesian
open import Categories.Functor.Cartesian.Properties
open import Categories.Functor.IdentityOnObjects

private
  variable
    ‚Ñì e ‚Ñì‚Ä≤ e‚Ä≤ ‚Ñì‚Ä≥ e‚Ä≥ : Level

record LawvereTheory (‚Ñì e : Level) : Set (suc (‚Ñì ‚äî e)) where
  private
    ùí© = Cat.Category.op Nat
  field
    L : Category ‚Ñï ‚Ñì e
  L‚Ä≤ : Cat.Category 0‚Ñì ‚Ñì e
  L‚Ä≤ = pack‚Ä≤ L
  field
    T : Cartesian L‚Ä≤
  CartT : CartesianCategory 0‚Ñì ‚Ñì e
  CartT = record { U = L‚Ä≤ ; cartesian = T }
  field
    I : IdentityOnObjects (unpack‚Ä≤ ùí©) L
    CartF : IsCartesianF Natop-Cartesian CartT (IOO‚áíFunctor I)

record LT-Hom (T‚ÇÅ : LawvereTheory ‚Ñì e) (T‚ÇÇ : LawvereTheory ‚Ñì‚Ä≤ e‚Ä≤) : Set (‚Ñì ‚äî e ‚äî ‚Ñì‚Ä≤ ‚äî e‚Ä≤) where
  private
    module T‚ÇÅ = LawvereTheory T‚ÇÅ
    module T‚ÇÇ = LawvereTheory T‚ÇÇ

  field
    cartF : CartesianF T‚ÇÅ.CartT T‚ÇÇ.CartT

  module cartF = CartesianF cartF using (F)

LT-id : {A : LawvereTheory ‚Ñì e} ‚Üí LT-Hom A A
LT-id = record { cartF = idF-CartesianF _ }

LT-‚àò : {A : LawvereTheory ‚Ñì e} {B : LawvereTheory ‚Ñì‚Ä≤ e‚Ä≤} {C : LawvereTheory ‚Ñì‚Ä≥ e‚Ä≥} ‚Üí
       LT-Hom B C ‚Üí LT-Hom A B ‚Üí LT-Hom A C
LT-‚àò G H = record { cartF = ‚àò-CartesianF (cartF G) (cartF H) }
  where open LT-Hom

record T-Algebra (LT : LawvereTheory ‚Ñì e) : Set (‚Ñì ‚äî e ‚äî suc (‚Ñì‚Ä≤ ‚äî e‚Ä≤)) where
  private
    module LT = LawvereTheory LT
  field
    cartF : CartesianF LT.CartT (Setoids-CartesianCategory ‚Ñì‚Ä≤ e‚Ä≤)

  module cartF = CartesianF cartF

  mod : Functor LT.L‚Ä≤ (Setoids ‚Ñì‚Ä≤ e‚Ä≤)
  mod = cartF.F

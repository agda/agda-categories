{-# OPTIONS --without-K --safe --lossy-unification #-}

open import Categories.Bicategory

module Categories.Bicategory.Monad.Bimodule {o â„“ e t} {ğ’ : Bicategory o â„“ e t} where

open import Level
open import Categories.Bicategory.Monad using (Monad)
import Categories.Bicategory.Extras as Bicat
open Bicat ğ’

record Bimodule (Mâ‚ Mâ‚‚ : Monad ğ’) : Set (o âŠ” â„“ âŠ” e) where
  open Monad using (C; T; Î¼; Î·)
  field
    F       : C Mâ‚ â‡’â‚ C Mâ‚‚
    actionË¡ : F âˆ˜â‚ T Mâ‚ â‡’â‚‚ F
    actionÊ³ : T Mâ‚‚ âˆ˜â‚ F â‡’â‚‚ F

    assoc     : actionÊ³ âˆ˜áµ¥ (T Mâ‚‚ â–· actionË¡) âˆ˜áµ¥ associator.from â‰ˆ actionË¡ âˆ˜áµ¥ (actionÊ³ â— T Mâ‚)
    sym-assoc : actionË¡ âˆ˜áµ¥ (actionÊ³ â— T Mâ‚) âˆ˜áµ¥ associator.to â‰ˆ actionÊ³ âˆ˜áµ¥ (T Mâ‚‚ â–· actionË¡)

    assoc-actionË¡     : actionË¡ âˆ˜áµ¥ (F â–· Î¼ Mâ‚) âˆ˜áµ¥ associator.from â‰ˆ actionË¡ âˆ˜áµ¥ (actionË¡ â— T Mâ‚)
    sym-assoc-actionË¡ : actionË¡ âˆ˜áµ¥ (actionË¡ â— T Mâ‚) âˆ˜áµ¥ associator.to â‰ˆ actionË¡ âˆ˜áµ¥ (F â–· Î¼ Mâ‚)
    assoc-actionÊ³     : actionÊ³ âˆ˜áµ¥ (Î¼ Mâ‚‚ â— F) âˆ˜áµ¥ associator.to â‰ˆ actionÊ³ âˆ˜áµ¥ (T Mâ‚‚ â–· actionÊ³)
    sym-assoc-actionÊ³ : actionÊ³ âˆ˜áµ¥ (T Mâ‚‚ â–· actionÊ³) âˆ˜áµ¥ associator.from â‰ˆ actionÊ³ âˆ˜áµ¥ (Î¼ Mâ‚‚ â— F)

    identityË¡ : actionË¡ âˆ˜áµ¥ (F â–· Î· Mâ‚) âˆ˜áµ¥ unitorÊ³.to â‰ˆ idâ‚‚
    identityÊ³ : actionÊ³ âˆ˜áµ¥ (Î· Mâ‚‚ â— F) âˆ˜áµ¥ unitorË¡.to â‰ˆ idâ‚‚

id-bimodule : (M : Monad ğ’) â†’ Bimodule M M
id-bimodule M = record
  { F = T
  ; actionË¡ = Î¼
  ; actionÊ³ = Î¼
  ; assoc = assoc
  ; sym-assoc = sym-assoc
  ; assoc-actionË¡ = assoc
  ; sym-assoc-actionË¡ = sym-assoc
  ; assoc-actionÊ³ = sym-assoc
  ; sym-assoc-actionÊ³ = assoc
  ; identityË¡ = identityË¡
  ; identityÊ³ = identityÊ³
  }
  where open Monad M

record Bimodulehomomorphism {Mâ‚ Mâ‚‚ : Monad ğ’} (Bâ‚ Bâ‚‚ : Bimodule Mâ‚ Mâ‚‚) : Set (â„“ âŠ” e) where
  open Monad using (T)
  open Bimodule using (F; actionË¡; actionÊ³)
  field
    Î±       : F Bâ‚ â‡’â‚‚ F Bâ‚‚
    linearË¡ : actionË¡ Bâ‚‚ âˆ˜áµ¥ (Î± â— T Mâ‚) â‰ˆ Î± âˆ˜áµ¥ actionË¡ Bâ‚
    linearÊ³ : actionÊ³ Bâ‚‚ âˆ˜áµ¥ (T Mâ‚‚ â–· Î±) â‰ˆ Î± âˆ˜áµ¥ actionÊ³ Bâ‚

id-bimodule-hom : {Mâ‚ Mâ‚‚ : Monad ğ’} â†’ {B : Bimodule Mâ‚ Mâ‚‚} â†’ Bimodulehomomorphism B B
id-bimodule-hom {Mâ‚} {Mâ‚‚} {B} = record
  { Î± = idâ‚‚
  ; linearË¡ = begin
      actionË¡ âˆ˜áµ¥ (idâ‚‚ â— T Mâ‚) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ idâ‚‚â— âŸ©
      actionË¡ âˆ˜áµ¥ idâ‚‚          â‰ˆâŸ¨ identityâ‚‚Ê³ âŸ©
      actionË¡                 â‰ˆâŸ¨ âŸº identityâ‚‚Ë¡ âŸ©
      idâ‚‚ âˆ˜áµ¥ actionË¡          âˆ
  ; linearÊ³ = begin
      actionÊ³ âˆ˜áµ¥ (T Mâ‚‚ â–· idâ‚‚) â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ â–·idâ‚‚ âŸ©
      actionÊ³ âˆ˜áµ¥ idâ‚‚          â‰ˆâŸ¨ identityâ‚‚Ê³ âŸ©
      actionÊ³                 â‰ˆâŸ¨ âŸº identityâ‚‚Ë¡ âŸ©
      idâ‚‚ âˆ˜áµ¥ actionÊ³          âˆ
  }
  where
    open Monad using (C; T)
    open Bimodule B using (actionË¡; actionÊ³)
    open hom.HomReasoning

bimodule-hom-âˆ˜ : {Mâ‚ Mâ‚‚ : Monad ğ’} â†’ {Bâ‚ Bâ‚‚ Bâ‚ƒ : Bimodule Mâ‚ Mâ‚‚}
                 â†’ Bimodulehomomorphism Bâ‚‚ Bâ‚ƒ â†’ Bimodulehomomorphism Bâ‚ Bâ‚‚ â†’ Bimodulehomomorphism Bâ‚ Bâ‚ƒ
bimodule-hom-âˆ˜ {Mâ‚} {Mâ‚‚} {Bâ‚} {Bâ‚‚} {Bâ‚ƒ} g f = record
  { Î± = Î± g âˆ˜áµ¥ Î± f
  ; linearË¡ = begin
      actionË¡ Bâ‚ƒ âˆ˜áµ¥ (Î± g âˆ˜áµ¥ Î± f) â— T Mâ‚            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŸº âˆ˜áµ¥-distr-â— âŸ©
      actionË¡ Bâ‚ƒ âˆ˜áµ¥ (Î± g â— T Mâ‚) âˆ˜áµ¥ (Î± f â— T Mâ‚)   â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©
      (actionË¡ Bâ‚ƒ âˆ˜áµ¥ (Î± g â— T Mâ‚)) âˆ˜áµ¥ (Î± f â— T Mâ‚) â‰ˆâŸ¨ linearË¡ g âŸ©âˆ˜âŸ¨refl âŸ©
      (Î± g âˆ˜áµ¥ actionË¡ Bâ‚‚) âˆ˜áµ¥ (Î± f â— T Mâ‚)          â‰ˆâŸ¨ assocâ‚‚ âŸ©
      Î± g âˆ˜áµ¥ actionË¡ Bâ‚‚ âˆ˜áµ¥ (Î± f â— T Mâ‚)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ linearË¡ f âŸ©
      Î± g âˆ˜áµ¥ Î± f âˆ˜áµ¥ actionË¡ Bâ‚                     â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©
      (Î± g âˆ˜áµ¥ Î± f) âˆ˜áµ¥ actionË¡ Bâ‚                   âˆ
  ; linearÊ³ = begin
      actionÊ³ Bâ‚ƒ âˆ˜áµ¥ T Mâ‚‚ â–· (Î± g âˆ˜áµ¥ Î± f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ (âŸº âˆ˜áµ¥-distr-â–·) âŸ©
      actionÊ³ Bâ‚ƒ âˆ˜áµ¥ (T Mâ‚‚ â–· Î± g) âˆ˜áµ¥ (T Mâ‚‚ â–· Î± f)   â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©
      (actionÊ³ Bâ‚ƒ âˆ˜áµ¥ (T Mâ‚‚ â–· Î± g)) âˆ˜áµ¥ (T Mâ‚‚ â–· Î± f) â‰ˆâŸ¨ linearÊ³ g âŸ©âˆ˜âŸ¨refl âŸ©
      (Î± g âˆ˜áµ¥ actionÊ³ Bâ‚‚) âˆ˜áµ¥ (T Mâ‚‚ â–· Î± f)          â‰ˆâŸ¨ assocâ‚‚ âŸ©
      Î± g âˆ˜áµ¥ actionÊ³ Bâ‚‚ âˆ˜áµ¥ (T Mâ‚‚ â–· Î± f)            â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ linearÊ³ f âŸ©
      Î± g âˆ˜áµ¥ Î± f âˆ˜áµ¥ actionÊ³ Bâ‚                     â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©
      (Î± g âˆ˜áµ¥ Î± f) âˆ˜áµ¥ actionÊ³ Bâ‚                   âˆ
  }
  where
    open Bimodulehomomorphism using (Î±; linearË¡; linearÊ³)
    open Monad using (C; T)
    open Bimodule using (F; actionË¡; actionÊ³)
    open hom.HomReasoning

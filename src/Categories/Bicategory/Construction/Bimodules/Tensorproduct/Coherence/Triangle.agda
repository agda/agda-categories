{-# OPTIONS --without-K --safe --lossy-unification #-}

open import Categories.Bicategory
open import Categories.Bicategory.LocalCoequalizers

open import Categories.Bicategory.Monad
open import Categories.Bicategory.Monad.Bimodule
open import Categories.Bicategory.Monad.Bimodule.Homomorphism renaming (Bimodulehomomorphism to Bimodhom)


-- We will prove that the associator and unitor in the bicategory of monads and bimodules --
-- satisfies the triangle law. --

module Categories.Bicategory.Construction.Bimodules.Tensorproduct.Coherence.Triangle
  {o â„“ e t} {ğ’ : Bicategory o â„“ e t} {localCoeq : LocalCoequalizers ğ’} {Mâ‚ Mâ‚‚ Mâ‚ƒ : Monad ğ’}
  {Bâ‚‚ : Bimodule Mâ‚‚ Mâ‚ƒ} {Bâ‚ : Bimodule Mâ‚ Mâ‚‚} where

import Categories.Bicategory.LocalCoequalizers
open ComposeWithLocalCoequalizer ğ’ localCoeq

import Categories.Bicategory.Construction.Bimodules.TensorproductOfBimodules {ğ’ = ğ’} {localCoeq} as TensorproductOfBimodules
import Categories.Bicategory.Construction.Bimodules.TensorproductOfHomomorphisms {ğ’ = ğ’} {localCoeq} as TensorproductOfHomomorphisms
open TensorproductOfBimodules using () renaming (Tensorproduct to infixr 30 _âŠ—â‚€_)
open TensorproductOfHomomorphisms using () renaming (Tensorproduct to infixr 30 _âŠ—â‚_)

Id-Bimod : {M : Monad ğ’} â†’ Bimodule M M
Id-Bimod {M} = id-bimodule M

import Categories.Bicategory.Extras as Bicat
open Bicat ğ’

import Categories.Diagram.Coequalizer
import Categories.Morphism
import Categories.Morphism.Reasoning.Iso

-- To get constructions of the hom-categories with implicit arguments into scope --
private
  module HomCat {X} {Y} where
    open Categories.Morphism (hom X Y) public using (_â‰…_)
    open Categories.Diagram.Coequalizer (hom X Y) public
    open Categories.Morphism.Reasoning.Iso (hom X Y) public

open HomCat

open import Categories.Bicategory.Construction.Bimodules.Tensorproduct.Associator
  {ğ’ = ğ’} {localCoeq}
  using (associator-âŠ—-from; hexagon)
import Categories.Bicategory.Construction.Bimodules.Tensorproduct.Unitor
  {ğ’ = ğ’} {localCoeq} as Unitor
open Unitor.Left-Unitor using (UnitorË¡âŠ—From) renaming (triangle to left-unitor-triangle)
open Unitor.Right-Unitor using (UnitorÊ³âŠ—From) renaming (triangle to right-unitor-triangle)

open TensorproductOfBimodules using (CoeqBimods)
open TensorproductOfHomomorphisms using (Î±Sq-âŠ—)

open Monad Mâ‚‚ using () renaming (T to Tâ‚‚)
open Bimodule Bâ‚ using () renaming (F to Fâ‚; actionÊ³ to actionÊ³â‚)
open Bimodule Bâ‚‚ using () renaming (F to Fâ‚‚; actionË¡ to actionË¡â‚‚)

abstract
  triangleâŠ—âˆ˜arrÂ² : ((Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
                   âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚}))
                   âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
                   âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
                   â‰ˆ (Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚} âŠ—â‚ id-bimodule-hom {B = Bâ‚})
                     âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
                     âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
  triangleâŠ—âˆ˜arrÂ² = begin

    ((Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚}))
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
    â‰ˆâŸ¨ assocâ‚‚ âŸ©

    (Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚}))
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚)
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
    â‰ˆâŸ¨ assocâ‚‚ âŸ©

    Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚})
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚)
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŸº (hexagon {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚}) âŸ©

    Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ (Id-Bimod âŠ—â‚€ Bâ‚))
    âˆ˜áµ¥ Fâ‚‚ â–· Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚)
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©

    (Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ (Id-Bimod âŠ—â‚€ Bâ‚)))
    âˆ˜áµ¥ Fâ‚‚ â–· Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚)
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ âŸº (Î±Sq-âŠ— (id-bimodule-hom {B = Bâ‚‚}) (UnitorË¡âŠ—From {B = Bâ‚})) âŸ©âˆ˜âŸ¨refl âŸ©

    (Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Fâ‚‚ â–· Bimodhom.Î± (UnitorË¡âŠ—From {B = Bâ‚}))
    âˆ˜áµ¥ Fâ‚‚ â–· Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚)
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ assocâ‚‚ âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Fâ‚‚ â–· Bimodhom.Î± (UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Fâ‚‚ â–· Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚)
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ sym-assocâ‚‚ âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ (Fâ‚‚ â–· Bimodhom.Î± (UnitorË¡âŠ—From {B = Bâ‚})
    âˆ˜áµ¥ Fâ‚‚ â–· Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚))
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âˆ˜áµ¥-distr-â–· âŸ©âˆ˜âŸ¨refl âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Fâ‚‚ â–· (Bimodhom.Î± (UnitorË¡âŠ—From {B = Bâ‚})
            âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Id-Bimod Bâ‚))
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ â–·-resp-â‰ˆ (left-unitor-triangle {B = Bâ‚}) âŸ©âˆ˜âŸ¨refl âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Fâ‚‚ â–· actionÊ³â‚
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©

    (Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Fâ‚‚ â–· actionÊ³â‚)
    âˆ˜áµ¥ associator.from {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
    â‰ˆâŸ¨ âŸº (switch-tofromÊ³ associator Fâ‚‚âŠ—Fâ‚equality-var) âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ actionË¡â‚‚ â— Fâ‚
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ â—-resp-â‰ˆ ( âŸº (right-unitor-triangle {B = Bâ‚‚})) âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ (Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚})
        âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod)) â— Fâ‚
    â‰ˆâŸ¨ reflâŸ©âˆ˜âŸ¨ âŸº âˆ˜áµ¥-distr-â— âŸ©

    Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚}) â— Fâ‚
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
    â‰ˆâŸ¨ sym-assocâ‚‚ âŸ©

    (Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
    âˆ˜áµ¥ Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚}) â— Fâ‚)
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚
    â‰ˆâŸ¨ Î±Sq-âŠ— (UnitorÊ³âŠ—From {B = Bâ‚‚}) (id-bimodule-hom {B = Bâ‚}) âŸ©âˆ˜âŸ¨refl âŸ©

    (Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚} âŠ—â‚ id-bimodule-hom {B = Bâ‚})
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods Bâ‚‚ Id-Bimod) â— Fâ‚ âˆ

    where
      open hom.HomReasoning
      
      Fâ‚‚âŠ—Fâ‚equality-var : (Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
                          âˆ˜áµ¥ actionË¡â‚‚ â— Fâ‚)
                          âˆ˜áµ¥ associator.to {f = Fâ‚‚} {Tâ‚‚} {Fâ‚}
                          â‰ˆ Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚)
                            âˆ˜áµ¥ Fâ‚‚ â–· actionÊ³â‚
      Fâ‚‚âŠ—Fâ‚equality-var = begin
        (Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚) âˆ˜áµ¥ actionË¡â‚‚ â— Fâ‚) âˆ˜áµ¥ associator.to â‰ˆâŸ¨ assocâ‚‚ âŸ©
        Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚) âˆ˜áµ¥ actionË¡â‚‚ â— Fâ‚ âˆ˜áµ¥ associator.to â‰ˆâŸ¨ âŸº (Coequalizer.equality (CoeqBimods Bâ‚‚ Bâ‚)) âŸ©
        Coequalizer.arr (CoeqBimods Bâ‚‚ Bâ‚) âˆ˜áµ¥ Fâ‚‚ â–· actionÊ³â‚ âˆ

  triangleâŠ—âˆ˜arr : (Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
                  âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚}))
                  âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚)
                  â‰ˆ Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚} âŠ—â‚ id-bimodule-hom {B = Bâ‚})
                    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚)
                    
  triangleâŠ—âˆ˜arr = Coequalizerâ‡’Epi
                    ((CoeqBimods Bâ‚‚ Id-Bimod) coeq-â— Fâ‚)
                    ((Bimodhom.Î± (id-bimodule-hom âŠ—â‚ UnitorË¡âŠ—From)
                    âˆ˜áµ¥ Bimodhom.Î± associator-âŠ—-from)
                    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
                    (Bimodhom.Î± (UnitorÊ³âŠ—From âŠ—â‚ id-bimodule-hom)
                    âˆ˜áµ¥ Coequalizer.arr (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚))
                    triangleâŠ—âˆ˜arrÂ²
  
  triangleâŠ— : Bimodhom.Î± (id-bimodule-hom {B = Bâ‚‚} âŠ—â‚ UnitorË¡âŠ—From {B = Bâ‚})
              âˆ˜áµ¥ Bimodhom.Î± (associator-âŠ—-from {Bâ‚ƒ = Bâ‚‚} {Id-Bimod} {Bâ‚})
              â‰ˆ Bimodhom.Î± (UnitorÊ³âŠ—From {B = Bâ‚‚} âŠ—â‚ id-bimodule-hom {B = Bâ‚})

  triangleâŠ— = Coequalizerâ‡’Epi
                (CoeqBimods (Bâ‚‚ âŠ—â‚€ Id-Bimod) Bâ‚)
                (Bimodhom.Î± (id-bimodule-hom âŠ—â‚ UnitorË¡âŠ—From)
                âˆ˜áµ¥ Bimodhom.Î± associator-âŠ—-from)
                (Bimodhom.Î± (UnitorÊ³âŠ—From âŠ—â‚ id-bimodule-hom))
                triangleâŠ—âˆ˜arr

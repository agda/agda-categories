{-# OPTIONS --without-K --safe #-}

open import Categories.Category.Core

-- Idempotents and Split Idempotents
module Categories.Morphism.Idempotent {o â„“ e} (ğ’ : Category o â„“ e) where

open import Level

open import Categories.Morphism ğ’
open import Categories.Morphism.Reasoning ğ’

open Category ğ’
open HomReasoning

record Idempotent (A : Obj) : Set (â„“ âŠ” e) where
  field
    idem       : A â‡’ A
    idempotent : idem âˆ˜ idem â‰ˆ idem

record IsSplitIdempotent {A : Obj} (i : A â‡’ A) : Set (o âŠ” â„“ âŠ” e) where
  field
    {obj}    : Obj
    retract  : A â‡’ obj
    section  : obj â‡’ A
    retracts : retract âˆ˜ section â‰ˆ id
    splits   : section âˆ˜ retract â‰ˆ i

  retract-absorb : retract âˆ˜ i â‰ˆ retract
  retract-absorb = begin
    retract âˆ˜ i                 â‰ˆË˜âŸ¨ reflâŸ©âˆ˜âŸ¨ splits âŸ©
    retract âˆ˜ section âˆ˜ retract â‰ˆâŸ¨ cancelË¡ retracts âŸ©
    retract                     âˆ

  section-absorb : i âˆ˜ section â‰ˆ section
  section-absorb = begin
    i âˆ˜ section                   â‰ˆË˜âŸ¨ splits âŸ©âˆ˜âŸ¨refl âŸ©
    (section âˆ˜ retract) âˆ˜ section â‰ˆâŸ¨ cancelÊ³ retracts âŸ©
    section                       âˆ

  idempotent : i âˆ˜ i â‰ˆ i
  idempotent = begin
    i âˆ˜ i                                     â‰ˆË˜âŸ¨ splits âŸ©âˆ˜âŸ¨ splits âŸ©
    (section âˆ˜ retract) âˆ˜ (section âˆ˜ retract) â‰ˆâŸ¨ cancelInner retracts âŸ©
    section âˆ˜ retract                         â‰ˆâŸ¨ splits âŸ©
    i                                         âˆ

record SplitIdempotent (A : Obj) : Set (o âŠ” â„“ âŠ” e) where
  field
    idem : A â‡’ A
    isSplitIdempotent : IsSplitIdempotent idem

  open IsSplitIdempotent isSplitIdempotent public

-- All split idempotents are idempotent
SplitIdempotentâ‡’Idempotent : âˆ€ {A} â†’ SplitIdempotent A â†’ Idempotent A
SplitIdempotentâ‡’Idempotent Split = record { Split }
  where
    module Split = SplitIdempotent Split

module _ {A} {f : A â‡’ A} (S T : IsSplitIdempotent f) where
  private
    module S = IsSplitIdempotent S
    module T = IsSplitIdempotent T

  split-idempotent-unique : S.obj â‰… T.obj
  split-idempotent-unique = record
    { from = T.retract âˆ˜ S.section
    ; to = S.retract âˆ˜ T.section
    ; iso = record
      { isoË¡ = begin
        (S.retract âˆ˜ T.section) âˆ˜ (T.retract âˆ˜ S.section) â‰ˆâŸ¨ center T.splits âŸ©
        S.retract âˆ˜ f âˆ˜ S.section                         â‰ˆâŸ¨ pullË¡ S.retract-absorb âŸ©
        S.retract âˆ˜ S.section                             â‰ˆâŸ¨ S.retracts âŸ©
        id                                                âˆ
      ; isoÊ³ = begin
        (T.retract âˆ˜ S.section) âˆ˜ (S.retract âˆ˜ T.section) â‰ˆâŸ¨ center S.splits âŸ©
        T.retract âˆ˜ f âˆ˜ T.section                         â‰ˆâŸ¨ pullË¡ T.retract-absorb âŸ©
        T.retract âˆ˜ T.section                             â‰ˆâŸ¨ T.retracts âŸ©
        id                                                âˆ
      }
    }

{-# OPTIONS --without-K --safe #-}


module Categories.Object.Subobject.Properties where

open import Level
open import Data.Product
open import Data.Unit
open import Function using (_$_)

open import Relation.Binary using (_=[_]â‡’_)
open import Relation.Binary.Bundles
open import Relation.Binary.OrderMorphism

open import Categories.Category
open import Categories.Functor
open import Categories.Functor.Presheaf
open import Categories.Category.Slice
open import Categories.Object.Subobject
open import Categories.Diagram.Pullback renaming (glue to glue-pullback)
open import Categories.Diagram.Pullback.Properties
open import Categories.Category.Instance.Posets
open import Categories.Category.Instance.Setoids
open import Categories.Adjoint.Instance.PosetCore
import Categories.Morphism as Mor
import Categories.Morphism.Reasoning as MR
open import Categories.Morphism.Notation


module _ {o â„“ e} {ğ’ : Category o â„“ e} (has-pullbacks : âˆ€ {A B X} â†’ (f : ğ’ [ A , X ]) â†’ (g : ğ’ [ B , X ]) â†’ Pullback ğ’ f g) where
  private
    module ğ’ = Category ğ’

  open ğ’.HomReasoning
  open ğ’.Equiv

  open Mor ğ’
  open MR ğ’
  open _â†£_

  -- The Subobject functor, into the category of posets
  Subâ‚š : Presheaf ğ’ (Posets (o âŠ” â„“ âŠ” e) (â„“ âŠ” e) (â„“ âŠ” e))
  Subâ‚š = record
    { Fâ‚€ = Subobjects ğ’
    ; Fâ‚ = Î» f â†’ record
      { fun = morphism f
      ; monotone = Î» {(Î± , m) (Î² , n)} h â†’ monotone f {(Î± , m)} {Î² , n} h
      }
    ; identity = Î» {A} {(Î± , m)} â†’
      let pid = has-pullbacks ğ’.id (mor m)
      in record
        { from = record
          { h = Pullback.pâ‚‚ pid
          ; â–³ = âŸº (Pullback.commute pid) â—‹ ğ’.identityË¡
          }
        ; to = record
          { h = Pullback.universal pid id-comm-sym
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pid
          }
        ; iso = record
          { isoË¡ = pullback-identity ğ’ pid
          ; isoÊ³ = Pullback.pâ‚‚âˆ˜universalâ‰ˆhâ‚‚ pid
          }
        }
    ; homomorphism = Î» {X} {Y} {Z} {f} {g} {(Î± , m)} â†’
      let pfg = has-pullbacks (ğ’ [ f âˆ˜ g ]) (mor m)
          pf = has-pullbacks f (mor m)
          pg = has-pullbacks g (Pullback.pâ‚ pf)
          iso = up-to-iso ğ’ pfg (glue-pullback ğ’ pf pg)
          module iso = _â‰…_ iso
      in record
        { from = record
          { h = iso.from
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pg
          }
        ; to = record
          { h = iso.to
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pfg
          }
        ; iso = record
          { isoË¡ = iso.isoË¡
          ; isoÊ³ = iso.isoÊ³
          }
        }
    ; F-resp-â‰ˆ = Î» {A B f g} eq {(Î± , m)} â†’
      let pf = has-pullbacks f (mor m)
          pg = has-pullbacks g (mor m)
          iso = up-to-iso ğ’ pf (pullback-resp-â‰ˆ ğ’ pg (sym eq) refl)
          module iso = _â‰…_ iso
      in record
        { from = record
          { h = iso.from
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pg
          }
        ; to = record
          { h = iso.to
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pf
          }
        ; iso = record
          { isoË¡ = iso.isoË¡
          ; isoÊ³ = iso.isoÊ³
          }
        }
    }
    where
      morphism : âˆ€ {A B} â†’ (f : ğ’ [ B , A ]) â†’ Î£[ Î± âˆˆ ğ’.Obj ] (Î± â†£ A) â†’ Î£[ Î² âˆˆ ğ’.Obj ] (Î² â†£ B)
      morphism f (Î± , m) = 
        let pb = has-pullbacks f (mor m)
        in Pullback.P pb , record
          { mor = Pullback.pâ‚ pb
          ; mono = Pullback-resp-Mono ğ’ pb (mono m)
          }

      monotone : âˆ€ {A B} (f : ğ’ [ B , A ]) â†’ Poset._â‰¤_ (Subobjects ğ’ A) =[ morphism f ]â‡’ Poset._â‰¤_ (Subobjects ğ’ B)
      monotone f {(Î± , m)} {(Î² , n)} h =
        let pm = has-pullbacks f (mor m)
            pn = has-pullbacks f (mor n)
        in record
          { h = Pullback.universal pn $ begin
              ğ’ [ f âˆ˜ Pullback.pâ‚ pm ] â‰ˆâŸ¨ Pullback.commute pm âŸ©
              ğ’ [ mor m âˆ˜ Pullback.pâ‚‚ pm ] â‰ˆâŸ¨ pushË¡ (âŸº (Sliceâ‡’.â–³ h)) âŸ©
              ğ’ [ mor n âˆ˜ ğ’ [ Sliceâ‡’.h h âˆ˜ Pullback.pâ‚‚ pm ] ] âˆ
          ; â–³ = Pullback.pâ‚âˆ˜universalâ‰ˆhâ‚ pn
          }

  -- The subobject functor as a presheaf on Setoids.
  -- This is just Subâ‚š composed with the 'Core'
  Sub : Presheaf ğ’ (Setoids (o âŠ” â„“ âŠ” e) (â„“ âŠ” e))
  Sub =  Core âˆ˜F Subâ‚š

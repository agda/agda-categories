<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Categories.Category.Instance.Groupoids</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--without-K</a> <a id="25" class="Pragma">--safe</a> <a id="32" class="Symbol">#-}</a>
<a id="36" class="Keyword">module</a> <a id="43" href="Categories.Category.Instance.Groupoids.html" class="Module">Categories.Category.Instance.Groupoids</a> <a id="82" class="Keyword">where</a>

<a id="89" class="Comment">-- The category of groupoids.</a>
<a id="119" class="Comment">--</a>
<a id="122" class="Comment">-- This category should maybe be called &quot;Ho(Groupoids)&quot; or &quot;Ho(Gpd)&quot;</a>
<a id="191" class="Comment">-- instead.  The &quot;homsets&quot; are not the &quot;usual&quot; ones consisting of</a>
<a id="257" class="Comment">-- functors, but consist instead of equivalence classes of functors up</a>
<a id="328" class="Comment">-- to natural isomorphism.  This is because homsets here are really</a>
<a id="396" class="Comment">-- hom-setoids and we pick natural isomorphism as the equivalence</a>
<a id="462" class="Comment">-- relation for these setoids.</a>
<a id="493" class="Comment">--</a>
<a id="496" class="Comment">-- See https://ncatlab.org/nlab/show/Ho%28Cat%29</a>

<a id="546" class="Keyword">open</a> <a id="551" class="Keyword">import</a> <a id="558" href="Level.html" class="Module">Level</a>
<a id="564" class="Keyword">open</a> <a id="569" class="Keyword">import</a> <a id="576" href="Categories.Category.html" class="Module">Categories.Category</a>
<a id="596" class="Keyword">open</a> <a id="601" class="Keyword">import</a> <a id="608" href="Categories.Category.Groupoid.html" class="Module">Categories.Category.Groupoid</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Categories.Functor.html" class="Module">Categories.Functor</a> <a id="668" class="Symbol">as</a> <a id="671" class="Module">Fctr</a> <a id="676" class="Keyword">using</a> <a id="682" class="Symbol">(</a><a id="683" href="Categories.Functor.Core.html#248" class="Record">Functor</a><a id="690" class="Symbol">;</a> <a id="692" href="Categories.Functor.html#747" class="Function Operator">_∘F_</a><a id="696" class="Symbol">)</a>
<a id="698" class="Keyword">open</a> <a id="703" class="Keyword">import</a> <a id="710" href="Categories.Functor.Properties.html" class="Module">Categories.Functor.Properties</a> <a id="740" class="Keyword">using</a> <a id="746" class="Symbol">(</a><a id="747" href="Categories.Functor.Properties.html#2843" class="Function Operator">[_]-resp-Iso</a><a id="759" class="Symbol">)</a>
<a id="761" class="Keyword">import</a> <a id="768" href="Categories.Morphism.IsoEquiv.html" class="Module">Categories.Morphism.IsoEquiv</a> <a id="797" class="Symbol">as</a> <a id="800" class="Module">IsoEquiv</a>
<a id="809" class="Keyword">open</a> <a id="814" class="Keyword">import</a> <a id="821" href="Categories.NaturalTransformation.NaturalIsomorphism.html" class="Module">Categories.NaturalTransformation.NaturalIsomorphism</a>
  <a id="875" class="Keyword">using</a> <a id="881" class="Symbol">(</a><a id="882" href="Categories.NaturalTransformation.NaturalIsomorphism.html#651" class="Record">NaturalIsomorphism</a><a id="900" class="Symbol">;</a> <a id="902" href="Categories.NaturalTransformation.NaturalIsomorphism.html#7073" class="Function">associator</a><a id="912" class="Symbol">;</a> <a id="914" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6216" class="Function">unitorˡ</a><a id="921" class="Symbol">;</a> <a id="923" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6312" class="Function">unitorʳ</a><a id="930" class="Symbol">;</a> <a id="932" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6406" class="Function">unitor²</a><a id="939" class="Symbol">;</a> <a id="941" href="Categories.NaturalTransformation.NaturalIsomorphism.html#5279" class="Function">isEquivalence</a><a id="954" class="Symbol">;</a> <a id="956" href="Categories.NaturalTransformation.NaturalIsomorphism.html#3893" class="Function Operator">_ⓘₕ_</a><a id="960" class="Symbol">;</a> <a id="962" href="Categories.NaturalTransformation.NaturalIsomorphism.html#4964" class="Function">sym</a><a id="965" class="Symbol">)</a>
<a id="967" class="Keyword">private</a>
  <a id="977" class="Keyword">variable</a>
    <a id="990" href="Categories.Category.Instance.Groupoids.html#990" class="Generalizable">o</a> <a id="992" href="Categories.Category.Instance.Groupoids.html#992" class="Generalizable">ℓ</a> <a id="994" href="Categories.Category.Instance.Groupoids.html#994" class="Generalizable">e</a> <a id="996" class="Symbol">:</a> <a id="998" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="1005" class="Keyword">open</a> <a id="1010" href="Categories.Category.Groupoid.html#807" class="Module">Groupoid</a> <a id="1019" class="Keyword">using</a> <a id="1025" class="Symbol">(</a><a id="1026" href="Categories.Category.Groupoid.html#874" class="Field">category</a><a id="1034" class="Symbol">)</a>

<a id="1037" class="Comment">-- The category of groupoids.</a>

<a id="Groupoids"></a><a id="1068" href="Categories.Category.Instance.Groupoids.html#1068" class="Function">Groupoids</a> <a id="1078" class="Symbol">:</a> <a id="1080" class="Symbol">∀</a> <a id="1082" href="Categories.Category.Instance.Groupoids.html#1082" class="Bound">o</a> <a id="1084" href="Categories.Category.Instance.Groupoids.html#1084" class="Bound">ℓ</a> <a id="1086" href="Categories.Category.Instance.Groupoids.html#1086" class="Bound">e</a> <a id="1088" class="Symbol">→</a> <a id="1090" href="Categories.Category.Core.html#442" class="Record">Category</a> <a id="1099" class="Symbol">(</a><a id="1100" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Categories.Category.Instance.Groupoids.html#1082" class="Bound">o</a> <a id="1107" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1109" href="Categories.Category.Instance.Groupoids.html#1084" class="Bound">ℓ</a> <a id="1111" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a>  <a id="1114" href="Categories.Category.Instance.Groupoids.html#1086" class="Bound">e</a><a id="1115" class="Symbol">))</a> <a id="1118" class="Symbol">(</a><a id="1119" href="Categories.Category.Instance.Groupoids.html#1082" class="Bound">o</a> <a id="1121" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1123" href="Categories.Category.Instance.Groupoids.html#1084" class="Bound">ℓ</a> <a id="1125" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1127" href="Categories.Category.Instance.Groupoids.html#1086" class="Bound">e</a><a id="1128" class="Symbol">)</a> <a id="1130" class="Symbol">(</a><a id="1131" href="Categories.Category.Instance.Groupoids.html#1082" class="Bound">o</a> <a id="1133" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1135" href="Categories.Category.Instance.Groupoids.html#1084" class="Bound">ℓ</a> <a id="1137" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1139" href="Categories.Category.Instance.Groupoids.html#1086" class="Bound">e</a><a id="1140" class="Symbol">)</a>
<a id="1142" href="Categories.Category.Instance.Groupoids.html#1068" class="Function">Groupoids</a> <a id="1152" href="Categories.Category.Instance.Groupoids.html#1152" class="Bound">o</a> <a id="1154" href="Categories.Category.Instance.Groupoids.html#1154" class="Bound">ℓ</a> <a id="1156" href="Categories.Category.Instance.Groupoids.html#1156" class="Bound">e</a> <a id="1158" class="Symbol">=</a> <a id="1160" class="Keyword">record</a>
  <a id="1169" class="Symbol">{</a> <a id="1171" href="Categories.Category.Core.html#559" class="Field">Obj</a>       <a id="1181" class="Symbol">=</a> <a id="1183" href="Categories.Category.Groupoid.html#807" class="Record">Groupoid</a> <a id="1192" href="Categories.Category.Instance.Groupoids.html#1152" class="Bound">o</a> <a id="1194" href="Categories.Category.Instance.Groupoids.html#1154" class="Bound">ℓ</a> <a id="1196" href="Categories.Category.Instance.Groupoids.html#1156" class="Bound">e</a>
  <a id="1200" class="Symbol">;</a> <a id="1202" href="Categories.Category.Core.html#575" class="Field Operator">_⇒_</a>       <a id="1212" class="Symbol">=</a> <a id="1214" class="Symbol">λ</a> <a id="1216" href="Categories.Category.Instance.Groupoids.html#1216" class="Bound">G</a> <a id="1218" href="Categories.Category.Instance.Groupoids.html#1218" class="Bound">H</a> <a id="1220" class="Symbol">→</a> <a id="1222" href="Categories.Functor.Core.html#248" class="Record">Functor</a> <a id="1230" class="Symbol">(</a><a id="1231" href="Categories.Category.Groupoid.html#874" class="Field">category</a> <a id="1240" href="Categories.Category.Instance.Groupoids.html#1216" class="Bound">G</a><a id="1241" class="Symbol">)</a> <a id="1243" class="Symbol">(</a><a id="1244" href="Categories.Category.Groupoid.html#874" class="Field">category</a> <a id="1253" href="Categories.Category.Instance.Groupoids.html#1218" class="Bound">H</a><a id="1254" class="Symbol">)</a>
  <a id="1258" class="Symbol">;</a> <a id="1260" href="Categories.Category.Core.html#595" class="Field Operator">_≈_</a>       <a id="1270" class="Symbol">=</a> <a id="1272" href="Categories.NaturalTransformation.NaturalIsomorphism.html#651" class="Record">NaturalIsomorphism</a>
  <a id="1293" class="Symbol">;</a> <a id="1295" href="Categories.Category.Core.html#630" class="Field">id</a>        <a id="1305" class="Symbol">=</a> <a id="1307" href="Categories.Functor.html#349" class="Function">Fctr.id</a>
  <a id="1317" class="Symbol">;</a> <a id="1319" href="Categories.Category.Core.html#656" class="Field Operator">_∘_</a>       <a id="1329" class="Symbol">=</a> <a id="1331" href="Categories.Functor.html#747" class="Function Operator">_∘F_</a>
  <a id="1338" class="Symbol">;</a> <a id="1340" href="Categories.Category.Core.html#715" class="Field">assoc</a>     <a id="1350" class="Symbol">=</a> <a id="1352" class="Symbol">λ</a> <a id="1354" class="Symbol">{</a><a id="1355" href="Categories.Category.Instance.Groupoids.html#1355" class="Bound">_</a> <a id="1357" href="Categories.Category.Instance.Groupoids.html#1357" class="Bound">_</a> <a id="1359" href="Categories.Category.Instance.Groupoids.html#1359" class="Bound">_</a> <a id="1361" href="Categories.Category.Instance.Groupoids.html#1361" class="Bound">_</a> <a id="1363" href="Categories.Category.Instance.Groupoids.html#1363" class="Bound">F</a> <a id="1365" href="Categories.Category.Instance.Groupoids.html#1365" class="Bound">G</a> <a id="1367" href="Categories.Category.Instance.Groupoids.html#1367" class="Bound">H</a><a id="1368" class="Symbol">}</a> <a id="1370" class="Symbol">→</a> <a id="1372" href="Categories.NaturalTransformation.NaturalIsomorphism.html#7073" class="Function">associator</a> <a id="1383" href="Categories.Category.Instance.Groupoids.html#1363" class="Bound">F</a> <a id="1385" href="Categories.Category.Instance.Groupoids.html#1365" class="Bound">G</a> <a id="1387" href="Categories.Category.Instance.Groupoids.html#1367" class="Bound">H</a>
  <a id="1391" class="Symbol">;</a> <a id="1393" href="Categories.Category.Core.html#1004" class="Field">sym-assoc</a> <a id="1403" class="Symbol">=</a> <a id="1405" class="Symbol">λ</a> <a id="1407" class="Symbol">{</a><a id="1408" href="Categories.Category.Instance.Groupoids.html#1408" class="Bound">_</a> <a id="1410" href="Categories.Category.Instance.Groupoids.html#1410" class="Bound">_</a> <a id="1412" href="Categories.Category.Instance.Groupoids.html#1412" class="Bound">_</a> <a id="1414" href="Categories.Category.Instance.Groupoids.html#1414" class="Bound">_</a> <a id="1416" href="Categories.Category.Instance.Groupoids.html#1416" class="Bound">F</a> <a id="1418" href="Categories.Category.Instance.Groupoids.html#1418" class="Bound">G</a> <a id="1420" href="Categories.Category.Instance.Groupoids.html#1420" class="Bound">H</a><a id="1421" class="Symbol">}</a> <a id="1423" class="Symbol">→</a> <a id="1425" href="Categories.NaturalTransformation.NaturalIsomorphism.html#4964" class="Function">sym</a> <a id="1429" class="Symbol">(</a><a id="1430" href="Categories.NaturalTransformation.NaturalIsomorphism.html#7073" class="Function">associator</a> <a id="1441" href="Categories.Category.Instance.Groupoids.html#1416" class="Bound">F</a> <a id="1443" href="Categories.Category.Instance.Groupoids.html#1418" class="Bound">G</a> <a id="1445" href="Categories.Category.Instance.Groupoids.html#1420" class="Bound">H</a><a id="1446" class="Symbol">)</a>
  <a id="1450" class="Symbol">;</a> <a id="1452" href="Categories.Category.Core.html#1096" class="Field">identityˡ</a> <a id="1462" class="Symbol">=</a> <a id="1464" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6216" class="Function">unitorˡ</a>
  <a id="1474" class="Symbol">;</a> <a id="1476" href="Categories.Category.Core.html#1145" class="Field">identityʳ</a> <a id="1486" class="Symbol">=</a> <a id="1488" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6312" class="Function">unitorʳ</a>
  <a id="1498" class="Symbol">;</a> <a id="1500" href="Categories.Category.Core.html#1339" class="Field">identity²</a> <a id="1510" class="Symbol">=</a> <a id="1512" href="Categories.NaturalTransformation.NaturalIsomorphism.html#6406" class="Function">unitor²</a>
  <a id="1522" class="Symbol">;</a> <a id="1524" href="Categories.Category.Core.html#1384" class="Field">equiv</a>     <a id="1534" class="Symbol">=</a> <a id="1536" href="Categories.NaturalTransformation.NaturalIsomorphism.html#5279" class="Function">isEquivalence</a>
  <a id="1552" class="Symbol">;</a> <a id="1554" href="Categories.Category.Core.html#1438" class="Field">∘-resp-≈</a>  <a id="1564" class="Symbol">=</a> <a id="1566" href="Categories.NaturalTransformation.NaturalIsomorphism.html#3893" class="Function Operator">_ⓘₕ_</a>
  <a id="1573" class="Symbol">}</a>

<a id="1576" class="Keyword">module</a> <a id="1583" href="Categories.Category.Instance.Groupoids.html#1583" class="Module">_</a> <a id="1585" class="Symbol">{</a><a id="1586" href="Categories.Category.Instance.Groupoids.html#1586" class="Bound">o</a> <a id="1588" href="Categories.Category.Instance.Groupoids.html#1588" class="Bound">ℓ</a> <a id="1590" href="Categories.Category.Instance.Groupoids.html#1590" class="Bound">e</a> <a id="1592" href="Categories.Category.Instance.Groupoids.html#1592" class="Bound">o′</a> <a id="1595" href="Categories.Category.Instance.Groupoids.html#1595" class="Bound">ℓ′</a> <a id="1598" href="Categories.Category.Instance.Groupoids.html#1598" class="Bound">e′</a><a id="1600" class="Symbol">}</a> <a id="1602" class="Symbol">{</a><a id="1603" href="Categories.Category.Instance.Groupoids.html#1603" class="Bound">G</a> <a id="1605" class="Symbol">:</a> <a id="1607" href="Categories.Category.Groupoid.html#807" class="Record">Groupoid</a> <a id="1616" href="Categories.Category.Instance.Groupoids.html#1586" class="Bound">o</a> <a id="1618" href="Categories.Category.Instance.Groupoids.html#1588" class="Bound">ℓ</a> <a id="1620" href="Categories.Category.Instance.Groupoids.html#1590" class="Bound">e</a><a id="1621" class="Symbol">}</a> <a id="1623" class="Symbol">{</a><a id="1624" href="Categories.Category.Instance.Groupoids.html#1624" class="Bound">H</a> <a id="1626" class="Symbol">:</a> <a id="1628" href="Categories.Category.Groupoid.html#807" class="Record">Groupoid</a> <a id="1637" href="Categories.Category.Instance.Groupoids.html#1592" class="Bound">o′</a> <a id="1640" href="Categories.Category.Instance.Groupoids.html#1595" class="Bound">ℓ′</a> <a id="1643" href="Categories.Category.Instance.Groupoids.html#1598" class="Bound">e′</a><a id="1645" class="Symbol">}</a>
         <a id="1656" class="Symbol">(</a><a id="1657" href="Categories.Category.Instance.Groupoids.html#1657" class="Bound">F</a> <a id="1659" class="Symbol">:</a> <a id="1661" href="Categories.Functor.Core.html#248" class="Record">Functor</a> <a id="1669" class="Symbol">(</a><a id="1670" href="Categories.Category.Groupoid.html#874" class="Field">category</a> <a id="1679" href="Categories.Category.Instance.Groupoids.html#1603" class="Bound">G</a><a id="1680" class="Symbol">)</a> <a id="1682" class="Symbol">(</a><a id="1683" href="Categories.Category.Groupoid.html#874" class="Field">category</a> <a id="1692" href="Categories.Category.Instance.Groupoids.html#1624" class="Bound">H</a><a id="1693" class="Symbol">))</a>
         <a id="1705" class="Keyword">where</a>

  <a id="1714" class="Keyword">private</a>
    <a id="1726" class="Keyword">module</a> <a id="1733" href="Categories.Category.Instance.Groupoids.html#1733" class="Module">G</a> <a id="1735" class="Symbol">=</a> <a id="1737" href="Categories.Category.Groupoid.html#807" class="Module">Groupoid</a> <a id="1746" href="Categories.Category.Instance.Groupoids.html#1603" class="Bound">G</a>
    <a id="1752" class="Keyword">module</a> <a id="1759" href="Categories.Category.Instance.Groupoids.html#1759" class="Module">H</a> <a id="1761" class="Symbol">=</a> <a id="1763" href="Categories.Category.Groupoid.html#807" class="Module">Groupoid</a> <a id="1772" href="Categories.Category.Instance.Groupoids.html#1624" class="Bound">H</a>
  <a id="1776" class="Keyword">open</a> <a id="1781" href="Categories.Functor.Core.html#248" class="Module">Functor</a> <a id="1789" href="Categories.Category.Instance.Groupoids.html#1657" class="Bound">F</a>
  <a id="1793" class="Keyword">open</a> <a id="1798" href="Categories.Morphism.IsoEquiv.html" class="Module">IsoEquiv</a> <a id="1807" class="Symbol">(</a><a id="1808" href="Categories.Category.Groupoid.html#874" class="Field">category</a> <a id="1817" href="Categories.Category.Instance.Groupoids.html#1624" class="Bound">H</a><a id="1818" class="Symbol">)</a> <a id="1820" class="Keyword">using</a> <a id="1826" class="Symbol">(</a><a id="1827" href="Categories.Morphism.IsoEquiv.html#441" class="Function">to-unique</a><a id="1836" class="Symbol">)</a>

  <a id="1841" class="Comment">-- Functors preserve inverses</a>

  <a id="1874" href="Categories.Category.Instance.Groupoids.html#1874" class="Function">F-resp-⁻¹</a> <a id="1884" class="Symbol">:</a> <a id="1886" class="Symbol">∀</a> <a id="1888" class="Symbol">{</a><a id="1889" href="Categories.Category.Instance.Groupoids.html#1889" class="Bound">A</a> <a id="1891" href="Categories.Category.Instance.Groupoids.html#1891" class="Bound">B</a><a id="1892" class="Symbol">}</a> <a id="1894" class="Symbol">(</a><a id="1895" href="Categories.Category.Instance.Groupoids.html#1895" class="Bound">f</a> <a id="1897" class="Symbol">:</a> <a id="1899" href="Categories.Category.Instance.Groupoids.html#1889" class="Bound">A</a> <a id="1901" href="Categories.Category.Core.html#575" class="Function Operator">G.⇒</a> <a id="1905" href="Categories.Category.Instance.Groupoids.html#1891" class="Bound">B</a><a id="1906" class="Symbol">)</a> <a id="1908" class="Symbol">→</a> <a id="1910" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1913" class="Symbol">(</a><a id="1914" href="Categories.Category.Instance.Groupoids.html#1895" class="Bound">f</a> <a id="1916" href="Categories.Category.Groupoid.html#365" class="Function Operator">G.⁻¹</a><a id="1920" class="Symbol">)</a> <a id="1922" href="Categories.Category.Core.html#595" class="Function Operator">H.≈</a> <a id="1926" class="Symbol">(</a><a id="1927" href="Categories.Functor.Core.html#455" class="Field">F₁</a> <a id="1930" href="Categories.Category.Instance.Groupoids.html#1895" class="Bound">f</a><a id="1931" class="Symbol">)</a> <a id="1933" href="Categories.Category.Groupoid.html#365" class="Function Operator">H.⁻¹</a>
  <a id="1940" href="Categories.Category.Instance.Groupoids.html#1874" class="Function">F-resp-⁻¹</a> <a id="1950" href="Categories.Category.Instance.Groupoids.html#1950" class="Bound">f</a> <a id="1952" class="Symbol">=</a> <a id="1954" href="Categories.Morphism.IsoEquiv.html#441" class="Function">to-unique</a> <a id="1964" class="Symbol">(</a><a id="1965" href="Categories.Functor.Properties.html#2843" class="Function Operator">[</a> <a id="1967" href="Categories.Category.Instance.Groupoids.html#1657" class="Bound">F</a> <a id="1969" href="Categories.Functor.Properties.html#2843" class="Function Operator">]-resp-Iso</a> <a id="1980" href="Categories.Category.Groupoid.html#399" class="Function">G.iso</a><a id="1985" class="Symbol">)</a> <a id="1987" href="Categories.Category.Groupoid.html#399" class="Function">H.iso</a> <a id="1993" href="Relation.Binary.Structures.html#1596" class="Function">H.Equiv.refl</a>
</pre></body></html>